#!/usr/bin/env gura
import(markdown)

baseNames = [
	'basement'
	'base64'
	'bmp'
	'bzip2'
	'cairo'
	//'canvas'
	'conio'
	'csv'
	'curl'
	'diff'
	'emf'
	'example'
	'freetype'
	'fs'
	'gif'
	'glu'
	'glut'
	'gmp'
	'guri'
	'gzip'
	'hash'
	'http'
	'jpeg'
	'llvm'
	'markdown'
	'math'
	'midi'
	'msico'
	//'mswin'
	'mysql'
	'opengl'
	'os'
	'path'
	'png'
	'postgresql'
	'ppm'
	're'
	//'sdl'
	'sdl2'
	'sqlite3'
	'sys'
	'tar'
	//'tcl'
	'tester'
	'tiff'
	'uuid'
	'wav'
	'wx'
	'xml'
	'xpm'
	'yaml'
	'zip'
]
//baseNames = ['basement']
moduleNames = 'makedoc_' + baseNames
!path.exists('tmp') && fs.mkdir('tmp')
fs.copy('../src/module-' + baseNames + '/makedoc.gura', 'tmp/' + moduleNames + '.gura'):overwrite
scope {
	moduleName = 'makedoc_lib'
	fs.copy('../src/lib/makedoc.gura', 'tmp/' + moduleName + '.gura'):overwrite
	moduleNames.insert(1, moduleName)
}
sys.path.insert(0, 'tmp')
doc = markdown.document()
doc << R'''
# About This Reference

This reference explains about functions and classes that are shipped with Gura interpreter.
Refer to Gura Language Manual if you want information
about syntax and specifications of Gura language itself.

# Explanatory Note

Functions in this reference are described in a generic expression.
For example, if there is a reference described like `func(num:number)`,
it means that `func` function takes one argument named `num` with value type of `number`.
You can call it like `func(3)`.

If an argument is optional, the argument name is followed by a symbol `?`.
For example: `func(num?:number)`.
You can call it as `func(2)` or can omit the arugument like `func()`.

If the the arument name has `*` symbol followed, the arument takes zero or more values.
For a function that has a generic expression `func(args*:number)`,
it can be called like `func()`, `func(3)`, `func(3, 4)`, `func(3, 4, 2)`, and so on.

If the the arument name has `+` symbol followed, the arument takes one or more values.
For a function that has a generic expression `func(args+:number)`,
it can be called like `func(3)`, `func(3, 4)`, `func(3, 4, 2)`, and so on.
In difference with `*`, it must take at least one value.

An argument may have a default value.
The default value is described with `=>` operator like `func(num:number => 4)`.
In such a case, if `num` is omitted, the default value `4` shall be used.

# Predefined Variables

<table>
<tr><th>Variable</th><th>Type</th><th>Explanation</th></tr>

<tr><td><code>*</code></td><td><code>iterator</code></td>
<td>An iterator instance equivalent with "<code>0..</code>".</td></tr>

<tr><td><code>-</code></td><td><code>nil</code></td>
<td>Value of <code>nil</code>.</td></tr>

<tr><td><code>@rem</code></td><td><code>nil</code></td>
<td>Value of <code>nil</code>.</td></tr>

<tr><td><code>__name__</code></td><td><code>string</code></td>
<td>If the current script is a main one that the interpreter launches, this variable is set to <code>'__main__'</code>.
If it is imported by another as a module, this variable is set to that module name.</td></tr>

<tr><td><code>false</code></td><td><code>boolean</code></td>
<td>Value of false.</td></tr>

<tr><td><code>nil</code></td><td><code>nil</code></td>
<td>Value of <code>nil</code>.</td></tr>

<tr><td><code>root</code></td><td><code>environment</code></td>
<td>Top level scope.</td></tr>

<tr><td><code>true</code></td><td><code>boolean</code></td>
<td>Value of true.</td></tr>

</table>
'''

moduleNames.each {|moduleName|
	println('module: ', moduleName.replace('makedoc_', ''))
	import(&moduleName).write(doc)
}

header = R'''
<html>
<head>
<meta charset="utf-8">
<title>Gura Library Reference</title>
<link rel="stylesheet" href="styles.css">
</head>
<body class="wrapper">
<div class="title">Gura Library Reference</div>
<div class="copyright">Copyright &copy; 2011-2015 ypsitau (<a href="ypsitau@nifty.com">ypsitau@nifty.com</a>)</div>
<div class="official-site">Official Site: <a href="http://www.gura-lang.org/">http://www.gura-lang.org/</a></div>
<hr />
'''
footer = R'''
</body>
</html>
'''

RendererCustom = class {
	__init__() = {
		this.out = nil
		this.indices = dim(6) {0}
		this.captionIndex = true
	}
	Render(doc:markdown.document):void = {
		this.RenderItem(doc.root)
		this.out = nil
	}
	RenderItem(item:markdown.item):map:void = {
		if (item.type == `h1) {
			this.out = open(format('chapter-%s.html', this.indices[0] + 1), 'w')
		}
		if (item.type == `root) {
			this.RenderItem(item.children)
		} elsif (item.type == `h1) {
			this.out.print('<h1>')
			if (this.captionIndex) {
				this.indices[0] += 1
				this.indices[1..5] = 0
				this.out.printf('<span class="caption-index-1">%d</span>',
								this.indices[0])
			}
			this.out.printf('<a name="%s">', markdown.MakeCaptionId([this.indices[0]]))
			this.out.print('</a>')
			this.RenderItem(item.children)
			this.out.print('</h1>\n')
		} elsif (item.type == `h2) {
			this.out.print('<h2>')
			if (this.captionIndex) {
				this.indices[1] += 1
				this.indices[2..5] = 0
				this.out.printf('<span class="caption-index-2">%d.%d</span>',
								this.indices[0..1]*)
			}
			this.out.printf('<a name="%s">', markdown.MakeCaptionId(this.indices[0..1]))
			this.out.print('</a>')
			this.RenderItem(item.children)
			this.out.print('</h2>\n')
		} elsif (item.type == `h3) {
			this.out.print('<h3>')
			if (this.captionIndex) {
				this.indices[2] += 1
				this.indices[3..5] = 0
				this.out.printf('<span class="caption-index-3">%d.%d.%d</span>',
								this.indices[0..2]*)
			}
			this.out.printf('<a name="%s">', markdown.MakeCaptionId(this.indices[0..2]))
			this.out.print('</a>')
			this.RenderItem(item.children)
			this.out.print('</h3>\n')
		} elsif (item.type == `h4) {
			this.out.print('<h4>')
			if (this.captionIndex) {
				this.indices[3] += 1
				this.indices[4..5] = 0
				this.out.printf('<span class="caption-index-4">%d.%d.%d.%d</span>',
								this.indices[0..3]*)
			}
			this.out.printf('<a name="%s">', markdown.MakeCaptionId(this.indices[0..3]))
			this.out.print('</a>')
			this.RenderItem(item.children)
			this.out.print('</h4>\n')
		} elsif (item.type == `h5) {
			this.out.print('<h5>')
			if (this.captionIndex) {
				this.indices[4] += 1
				this.indices[5] = 0
				this.out.printf('<span class="caption-index-5">%d.%d.%d.%d.%d</span>',
								this.indices[0..4]*)
			}
			this.out.printf('<a name="%s">', markdown.MakeCaptionId(this.indices[0..4]))
			this.out.print('</a>')
			this.RenderItem(item.children)
			this.out.print('</h5>\n')
		} elsif (item.type == `h6) {
			this.out.print('<h6>')
			if (this.captionIndex) {
				this.indices[5] += 1
				this.out.printf('<span class="caption-index-6">%d.%d.%d.%d.%d.%d</span>',
								this.indices[0..5]*)
			}
			this.out.printf('<a name="%s">', markdown.MakeCaptionId(this.indices[0..5]))
			this.out.print('</a>')
			this.RenderItem(item.children)
			this.out.print('</h6>\n')
		} elsif (item.type == `p) {
			this.out.print('<p>\n')
			this.RenderItem(item.children)
			this.out.println()
			this.out.print('</p>\n')
		} elsif (item.type == `blockquote) {
			this.out.print('<blockquote>\n')
			this.RenderItem(item.children)
			this.out.println()
			this.out.print('</blockquote>\n')
		} elsif (item.type == `em) {
			this.out.print('<em>')
			this.RenderItem(item.children)
			this.out.print('</em>')
		} elsif (item.type == `strong) {
			this.out.print('<strong>')
			this.RenderItem(item.children)
			this.out.print('</strong>')
		} elsif (item.type == `codeblock) {
			this.out.print('<pre><code>')
			this.RenderItem(item.children)
			this.out.print('</code></pre>\n')
		} elsif (item.type == `ol) {
			this.out.print('<ol>\n')
			this.RenderItem(item.children)
			this.out.print('</ol>\n')
		} elsif (item.type == `ul) {
			this.out.print('<ul>\n')
			this.RenderItem(item.children)
			this.out.print('</ul>\n')
		} elsif (item.type == `li) {
			this.out.print('<li>')
			this.RenderItem(item.children)
			this.out.print('</li>\n')
		} elsif (item.type == `line) {
			this.RenderItem(item.children)
			this.out.print('\n')
		} elsif (item.type == `a) {
			this.out.print('<a')
			item.url && this.out.print(' href="', item.url.encodeuri(), '"')
			item.title && this.out.print(' title="', item.title.escapehtml(), '"')
			this.out.print('>')
			this.RenderItem(item.children)
			this.out.print('</a>')
		} elsif (item.type == `img) {
			this.out.print('<img')
			item.url && this.out.print(' src="', item.url.encodeuri(), '"')
			item.text && this.out.print(' alt="', item.text.escapehtml(), '"')
			item.title && this.out.print(' title="', item.title.escapehtml(), '"')
			this.out.print('>')
		} elsif (item.type == `text) {
			this.out.print(item.text.escapehtml())
		} elsif (item.type == `code) {
			this.out.print('<code>')
			this.out.print(item.text.escapehtml())
			this.out.print('</code>')
		} elsif (item.type == `entity) {
			this.out.print('&', item.text.escapehtml(), ';')
		} elsif (item.type == `tag) {
			this.out.print('<')
			this.out.print(item.text)
			item.attrs && this.out.print(' ', item.attrs)
			if (item.children) {
				this.out.println('>')
				item.children && this.RenderItem(item.children)
				this.out.print('</')
				this.out.print(item.text)
				this.out.println('>')
			} else {
				this.out.println(' />')
			}
		} elsif (item.type == `hr) {
			this.out.print('<hr />\n')
		} elsif (item.type == `br) {
			this.out.print('<br />\n')
		}
	}
}

fileName = 'gura-lib-e.html'
open(fileName, 'w') {|out|
	println('rendering TOC ..')
	out.print(header)
	out.println('<h1>Table of Contents</h1>')
	doc.render@toc {|level:number, indices[]:number, captionId:string, text:string|
		if (level == 0) {
			out.printf(
					'<div><span class="toc-index-1">%d</span><a href="#%s">%s</a>\</div>\n',
					indices[0], captionId, text)
		} elsif (level == 1 && indices[0] in [4, 6]) {
			out.printf(
					'<div><span class="toc-index-2">%d.%d</span><a href="#%s">%s</a>\</div>\n',
					indices[0], indices[1], captionId, text)
		}
	}
	out.println('<hr />')
	println('rendering document body ..')
	markdown.Renderer@html(out, true).Render(doc)
	out.print(footer)
}
println(fileName, ' was created.')
