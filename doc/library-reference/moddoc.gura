#!/usr/bin/env gura
//==============================================================================
// makedoc.gura
// Helper module to create a foo.doc module that provides document for a module.
//==============================================================================
import(markdown)

suffixmgr(`string).assign(`moddoc) {|str| str.template().render()}

_reference(lang:symbol, func:function, fullname:string, fmt:string):map = {
	/*
	str = '**' + fullname.replaces(['_', '\\_', '`', '\\`']) + '**\n\n'
	if (fmt.find('`')) {
		str += '`` ' + fmt + '``\n\n'
	} else {
		str += '`' + fmt + '`\n\n'
	}
	if (help = function.gethelp(func, lang)) {
		str +=  help.text
		if (!str.endswith('\n')) { str += '\n' }
		str += '\n'
	}
	*/
	str = format('<funcentry fullname="%s" format="%s" />\n', fullname, fmt.escapehtml())
	if (help = function.gethelp(func, lang)) {
		str +=  help.text
		if (!str.endswith('\n')) { str += '\n' }
		str += '\n'
	}
	str
}

reference(lang:symbol, funcs*:function) = {
	funcs = funcs.each()
	_reference(lang, funcs, function.getfullname(funcs), function.getformat(funcs))
}

reference@iterable(lang:symbol, funcs*:function) = {
	funcs = funcs.each()
	_reference(lang, funcs,
			 function.getfullname(funcs):*replace('iterator#', 'iterable#'),
			 function.getformat(funcs):*replace('iterator#', 'iterable#'))
}

register(mod:environment, d:dict) = {
	mod.write(doc:markdown.document, lang:symbol) = {
		doc << d.get(lang, d[d.keys().next()])
	}
	if (mod.__name__ == '__main__') {
		mod.write(markdown.document(), `en).render@html(sys.stdout)
	}
} % {`en, 'markdown', R'''
Registers a dictionary of document texts written in Markdown format
that are associated with language symbols as their keys.

The argument `mod` takes an `environment` instance referring to the module
envioronemnt in which the documents are provided.
You should usually specify `locals()` for that argument.

Below is an example to call this function:

    text@en = '..'
    text@ja = '..'
    text@fr = '..'
    register(locals(), %{
        `en => text@en
        `ja => text@ja
        `fr => text@fr
	})

It creates a module function that has the following format:

    write(doc:markdown.document, lang:symbol)

The argument `doc` is a `markdown.document` instance into which the document is poured.

The argument `lang` is a symbol that specifies the language.
'''}
