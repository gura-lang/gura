#!/usr/bin/env gura
# encoding: utf-8
//-----------------------------------------------------------------------------
// guradrill
// copyright (c) Yutaka SAITO
//-----------------------------------------------------------------------------
import(wx)
import(re)
import(gif)
import(jpeg)
import(yaml)
import(cairo)
import(units)

//-----------------------------------------------------------------------------
// Point
//-----------------------------------------------------------------------------
Point = struct(x:number, y:number) {
	ToList() = [this.x, this.y]
	Offset(xOff:number, yOff:number) = Point(this.x + xOff, this.y + yOff)
}

//-----------------------------------------------------------------------------
// Fraction
//-----------------------------------------------------------------------------
Fraction = struct(num:number, denom:number) {
	Reduce() = {
		gcd = math.gcd(this.num, this.denom)
		Fraction(this.num / gcd, this.denom / gcd)
	}
	ToText() = format('%d/%d', this.num, this.denom)
}

//-----------------------------------------------------------------------------
// Rect
//-----------------------------------------------------------------------------
Rect = struct(x:number, y:number, width:number, height:number) {
	ToList() = [this.x, this.y, this.width, this.height]
	Center() = Point(this.x + this.width / 2, this.y + this.height / 2)
	Left() = this.x
	Right() = this.x + this.width
	Top() = this.y
	Bottom() = this.y + this.height
	LeftTop() = Point(this.x, this.y)
	RightTop() = Point(this.x + this.width, this.y)
	LeftBottom() = Point(this.x, this.y + this.height)
	RightBottom() = Point(this.x + this.width, this.y + this.height)
	LeftVCenter() = Point(this.x, this.y + this.height / 2)
	RightVCenter() = Point(this.x + this.width, this.y + this.height / 2)
	HCenterTop() = Point(this.x + this.width / 2, this.y)
	HCenterBottom() = Point(this.x + this.width / 2, this.y + this.height)
}

//-----------------------------------------------------------------------------
// PaperInfo
//-----------------------------------------------------------------------------
PaperInfo = class {
	__init__(wdPaper:number, htPaper:number, wdLeftMgn:number, wdRightMgn:number,
									htTopMgn:number, htBottomMgn:number) = {
		[this.wdPaper, this.htPaper] = [wdPaper, htPaper]
		[this.wdLeftMgn, this.wdRightMgn] = [wdLeftMgn, wdRightMgn]
		[this.htTopMgn, this.htBottomMgn] = [htTopMgn, htBottomMgn]
	}
	CalcPageRect() = Rect(this.wdLeftMgn, this.htTopMgn,
			this.wdPaper - (this.wdLeftMgn + this.wdRightMgn),
			this.htPaper - (this.htTopMgn + this.htBottomMgn))
}

//-----------------------------------------------------------------------------
// App
//-----------------------------------------------------------------------------
App = class(wx.App) {
	OnInit() = {
		this.paperInfo = PaperInfo(210, 297, 10, 10, 10, 15)
		[this.wdCanvas, this.htCanvas] = [210, 297] * 2.0
		htImage = 20
		this.imgList = wx.ImageList(16, htImage)
		image(`rgba, 16, htImage, `white) {|img|
			img.cairo {|cr|
				cairo.pattern.create_linear(0, 0, 16, 16) {|pat|
					pat.add_color_stop_rgb(0, .9, .9, .9)
					pat.add_color_stop_rgb(1, 0, 0, 0)
					cr.set_source(pat)
				}
				cr.move_to(3, 4).line_to(13, 9).line_to(3, 14).line_to(5, 9)
				cr.fill()
			}
			this.IMG_History = this.imgList.Add(img)
		}
		image(`rgba, 16, htImage, `white) {|img|
			img.cairo {|cr|
				cairo.pattern.create_linear(0, 0, 16, 16) {|pat|
					pat.add_color_stop_rgb(0, 1, .6, .6)
					pat.add_color_stop_rgb(1, .7, .3, .3)
					cr.set_source(pat)
				}
				cr.rectangle(2, 2, 13, 13)
				cr.fill()
				cr.set_source_color(`white)
				cr.move_to(4, 8).rel_line_to(8, 0)
				cr.move_to(8, 4).rel_line_to(0, 8)
				cr.set_line_width(3)
				cr.stroke()
			}
			this.IMG_Plus = this.imgList.Add(img)
		}
		image(`rgba, 16, htImage, `white) {|img|
			img.cairo {|cr|
				cairo.pattern.create_linear(0, 0, 16, 16) {|pat|
					pat.add_color_stop_rgb(0, .6, .6, 1)
					pat.add_color_stop_rgb(1, .3, .3, .7)
					cr.set_source(pat)
				}
				cr.rectangle(2, 2, 13, 13)
				cr.fill()
				cr.set_source_color(`white)
				cr.move_to(4, 8).rel_line_to(8, 0)
				cr.set_line_width(3)
				cr.stroke()
			}
			this.IMG_Minus = this.imgList.Add(img)
		}
		image(`rgba, 16, htImage, `white) {|img|
			img.cairo {|cr|
				cairo.pattern.create_linear(0, 0, 16, 16) {|pat|
					pat.add_color_stop_rgb(0, .4, .8, .4)
					pat.add_color_stop_rgb(1, .1, .4, .1)
					cr.set_source(pat)
				}
				cr.rectangle(2, 2, 13, 13)
				cr.fill()
				cr.set_source_color(`white)
				cr.move_to(4, 4).rel_line_to(8, 8)
				cr.move_to(12, 4).rel_line_to(-8, 8)
				cr.set_line_width(3)
				cr.stroke()
			}
			this.IMG_Multiply = this.imgList.Add(img)
		}
		image(`rgba, 16, htImage, `white) {|img|
			img.cairo {|cr|
				cairo.pattern.create_linear(0, 0, 16, 16) {|pat|
					pat.add_color_stop_rgb(0, .8, .8, .4)
					pat.add_color_stop_rgb(1, .4, .4, .1)
					cr.set_source(pat)
				}
				cr.rectangle(2, 2, 13, 13)
				cr.fill()
				cr.set_source_color(`white)
				cr.move_to(3, 8).rel_line_to(10, 0)
				cr.set_line_width(3)
				cr.stroke()
				cr.arc(8, 4, 2)
				cr.arc(8, 12, 2)
				cr.fill()
			}
			this.IMG_Divide = this.imgList.Add(img)
		}
		if (sys.argv.len() < 2) {
			this.fileNameCfg = path.join(sys.localdir, 'guradrill.yml')
		} else {
			this.fileNameCfg = sys.argv[1]
		}
		try { this.cfg = yaml.read(this.fileNameCfg) } except { this.cfg = %{} }
		this.cfg.store():default {
			'wdFrame'			=> 680
			'htFrame'			=> 680
			'wdNavi'			=> 200
			'select'			=> %{
				'category'		=> ''
				'composerName'	=> ''
			}
			'composers'			=> []
		}
		this.composers = for (composerClass in composerClasses):list { composerClass() }
		try {
			for (composer in this.composers) {
				cfgsComposer = this.cfg['composers']
				cfgComposer = cfgsComposer.find(
						(cfgsComposer:*get('category') == composer.category) & \
				 		(cfgsComposer:*get('composerName') == composer.composerName))
				!cfgComposer && continue
				composer.attrsHist = ComposerAttr(cfgComposer['attrsHist'])
				composer.attrCur = ComposerAttr(cfgComposer['attrCur'])
				composer.attrCur.name = ''
			}
		} except {
			fs.remove(this.fileNameCfg)
			sys.exit(1)
		}
		frame = Frame(nil)
		//wx.StatusBar(frame)
		frame.Show()
		this.SetTopWindow(frame)
		true
	}
	OnExit() = {
		this.cfg['composers'] = for (composer in this.composers):list {
			%{
				'category'		=> composer.category
				'composerName'	=> composer.composerName
				'attrsHist'		=> composer.attrsHist::Export()
				'attrCur'		=> composer.attrCur.Export()
			}
		}
		yaml.write(open(this.fileNameCfg, 'w'), this.cfg)
		0
	}
}

if (sys.platform == `windows) {
	fontFamilyName = 'HGMaruGothicMPRO'
	OpenWithApp(filename:string) = {
		import(mswin)
		[basename, extname] = path.splitext(filename)
		keyName = mswin.HKEY_CLASSES_ROOT.openkey(
				'.' + extname, mswin.KEY_QUERY_VALUE).queryvalue('')
		cmdline = mswin.HKEY_CLASSES_ROOT.openkey(
			keyName + r'\Shell\Open\Command', mswin.KEY_QUERY_VALUE).queryvalue('')
		progname = cmdline.match(r'^\"([^\"]+)\"')[1]
		os.exec(progname, filename):fork
	}
} elsif (sys.platform == `linux) {
	fontFamilyName = 'VL Gothic'
	prognameDict = dict():icase
	IsExist(filename:string):map = {
		path.exists(path.join(os.getenv('PATH').split(':'), filename)).or()
	}
	IsExist('evince') && { prognameDict['pdf'] = 'evince' }
	OpenWithApp(filename:string) = {
		[basename, extname] = path.splitext(filename)
		prognameDict.haskey(extname) && os.exec(prognameDict[extname], filename):fork
	}
}

//-----------------------------------------------------------------------------
// cairo.context extension
//-----------------------------------------------------------------------------
classref(`cairo.context) {|ref|
	ref.Rectangle(rect:Rect) = {
		this.rectangle(rect.ToList()*)
	}
	ref.ShowText(align:symbol, pt:Point, text:string):map = {
		[x, y] = [pt.x, pt.y]
		extText = this.text_extents(text)
		extFont = this.font_extents()
		yOffset = extFont.ascent / 2
		if (align == `center) {
			x -= extText.x_advance / 2
			y += yOffset
		} elsif (align == `east) {
			y += yOffset
		} elsif (align == `west) {
			x -= extText.x_advance
			y += yOffset
		}
		this.move_to(x, y).show_text(text)
		extText
	}
}

//-----------------------------------------------------------------------------
// Question
//-----------------------------------------------------------------------------
FractionQuestion = struct(fracL:Fraction, textOperator:string,
						fracR:Fraction, fracAnswer:Fraction) {
	ToText() = format('%s%s%s', this.fracL.ToText(),
									this.textOperator, this.fracR.ToText())
}

LineQuestion = struct(numL:number, textOperator:string,
						numR:number, numAnswer:number, textAnswer?:string) {
	ToText() = format('%d%s%d', this.numL, this.textOperator, this.numR)
}

BoxQuestion = struct(numL:number, textOperator:string,
						numR:number, numAnswer:number) {
	ToText() = format('%d%s%d', this.numL, this.textOperator, this.numR)
}

BoxQuestionDiv = struct(numL:number, numR:number) {
	ToText() = format('%d/%d', this.numL, this.numR)
}

//-----------------------------------------------------------------------------
// Page
//-----------------------------------------------------------------------------
Page = struct(questions[] => [])

//-----------------------------------------------------------------------------
// ComposerAttr
//-----------------------------------------------------------------------------
ComposerAttr = class {
	__init__(cfg?:dict):map = {
		if (cfg) {
			this.name			= cfg['name']
			this.fileNamePDF	= cfg['fileNamePDF']
			this.cntHorz		= int(cfg['cntHorz'])
			this.cntVert		= int(cfg['cntVert'])
			this.nPages			= int(cfg['nPages'])
			this.kanjiFlag		= (cfg['kanjiFlag'] == 'true')
			this.answerFlag		= (cfg['answerFlag'] == 'true')
			this.scoreBoxFlag	= (cfg['scoreBoxFlag'] == 'true')
			this.seed			= cfg['seed']
		} else {
			this.name			= ''
			this.fileNamePDF	= path.join(sys.localdir, 'guradrill.pdf')
			this.cntHorz		= 1
			this.cntVert		= 1
			this.nPages			= 10
			this.kanjiFlag		= true
			this.answerFlag		= false
			this.scoreBoxFlag	= false
			this.seed			= -1
		}
	}
	HasSameValues(attr) = {
		this.fileNamePDF	== attr.fileNamePDF && \
		this.cntHorz		== attr.cntHorz && \
		this.cntVert		== attr.cntVert && \
		this.nPages			== attr.nPages && \
		this.kanjiFlag		== attr.kanjiFlag && \
		this.answerFlag		== attr.answerFlag && \
		this.scoreBoxFlag	== attr.scoreBoxFlag && \
		this.seed			== attr.seed
	}
	Export() = %{
		'name'			=> this.name
		'fileNamePDF'	=> this.fileNamePDF
		'cntHorz'		=> this.cntHorz
		'cntVert'		=> this.cntVert
		'nPages'		=> this.nPages
		'kanjiFlag'		=> this.kanjiFlag
		'answerFlag'	=> this.answerFlag
		'scoreBoxFlag'	=> this.scoreBoxFlag
		'seed'			=> this.seed
	}
	Clone() = {
		attr = ComposerAttr()
		attr.name			= this.name
		attr.fileNamePDF	= this.fileNamePDF
		attr.cntHorz		= this.cntHorz
		attr.cntVert		= this.cntVert
		attr.nPages			= this.nPages
		attr.kanjiFlag		= this.kanjiFlag
		attr.answerFlag		= this.answerFlag
		attr.scoreBoxFlag	= this.scoreBoxFlag
		attr.seed			= this.seed
		attr
	}
}

//-----------------------------------------------------------------------------
// Composer
//-----------------------------------------------------------------------------
Composer = class {
	wdScoreBox = 30
	htTitle = 16
	htName = 12
	htSpace = 4
	wideDict = %{
		'0' => '０', '1' => '１', '2' => '２', '3' => '３', '4' => '４'
		'5' => '５', '6' => '６', '7' => '７', '8' => '８', '9' => '９'
	}
	__init__(dir:symbol) = {
		this.variableQuestionsHFlag = true
		this.rcPage = wx.GetApp().paperInfo.CalcPageRect()
		this.dir = dir
		this.title = ['', '']
		this.category = ''
		this.composerName = ''
		this.colorQuestion = '#444444'
		this.colorAnswer = '#ff4444'
		this.pages = []
		this.iPageCur = 0
		this.attrsHist = []
		this.attrCur = ComposerAttr()
	}
	ClearPages() = {
		this.attrCur.seed = -1
		this.pages = []
	}
	MakeQuestions() = {
		textList = []
		repeat(this.attrCur.cntHorz * this.attrCur.cntVert):list {
			repeat (1000) {
				question = this.MakeQuestion()
				text = question.ToText()
				!textList.find(text) && break
			}
			textList.add(text)
			question
		}
	}
	CalcRectTitle() = {
		Rect(this.rcPage.x, this.rcPage.y,
					this.rcPage.width - this.wdScoreBox, this.htTitle)
	}
	CalcRectScoreBox() = {
		Rect(this.rcPage.Right() - this.wdScoreBox - 10, this.rcPage.y,
					this.wdScoreBox, this.htTitle)
	}
	CalcRectName() = {
		[wdLeftMgn, wdRightMgn] = [5, 5]
		Rect(this.rcPage.x + wdLeftMgn, this.rcPage.y + this.htTitle,
					this.rcPage.width - (wdLeftMgn + wdRightMgn), this.htName)
	}
	CalcRectBounding(index:number):map = {
		if (this.dir == `horizontal) {
			[iHorz, iVert] = [index % this.attrCur.cntHorz,
										int(index / this.attrCur.cntHorz)]
		} else {
			[iHorz, iVert] = [int(index / this.attrCur.cntVert),
										index % this.attrCur.cntVert]
		}
		[wdQuestion, htQuestion] = [
			this.rcPage.width / this.attrCur.cntHorz,
			(this.rcPage.height - \
				(this.htTitle + this.htName + this.htSpace)) / this.attrCur.cntVert
		]
		[x, y] = [
			this.rcPage.x + iHorz * wdQuestion,
			this.rcPage.y + this.htTitle + this.htName + \
											this.htSpace + iVert * htQuestion
		]
		Rect(x, y, wdQuestion, htQuestion)
	}
	ComposePage(cr:cairo.context, iPage:number) = {
		cr.select_font_face(fontFamilyName,
					cairo.FONT_SLANT_NORMAL, cairo.FONT_WEIGHT_NORMAL)
		cr.set_source_rgb(0, 0, 0)
		rcTitle = this.CalcRectTitle()
		rcScoreBox = this.CalcRectScoreBox()
		rcName = this.CalcRectName()
		if (this.attrCur.scoreBoxFlag) {
			cr.set_line_width(.1)
			cr.Rectangle(rcScoreBox)
			cr.stroke()
		}
		scope {
			pt = rcTitle.LeftVCenter()
			cr.set_font_size(8)
			pt.x += (cr.ShowText(`east, pt,
				cond(this.attrCur.kanjiFlag, this.title[0], this.title[1]))).x_advance
			pt.x += 10
			cr.set_font_size(4)
			cr.ShowText(`east, pt, this.composerName)
		}
		scope {
			cr.set_font_size(4)
			pt = rcName.LeftVCenter()
			pt.x += 13
			pt.x += (cr.ShowText(`east, pt,
					cond(this.attrCur.kanjiFlag, '年', 'ねん'))).x_advance
			pt.x += 13
			pt.x += (cr.ShowText(`east, pt,
					cond(this.attrCur.kanjiFlag, '組', 'くみ'))).x_advance
			pt.x += 13
			pt.x += (cr.ShowText(`east, pt,
					cond(this.attrCur.kanjiFlag, '番', 'ばん'))).x_advance
			pt.x += 8
			cr.ShowText(`east, pt, cond(this.attrCur.kanjiFlag, '名前', 'なまえ'))
			cr.set_line_width(.1)
			cr.move_to(rcName.Left(), rcName.Bottom())
			cr.line_to(rcName.Right(), rcName.Bottom())
			cr.stroke()
		}
		this.ComposePageBody(cr, iPage)
		cr.stroke()
	}
	UpdatePages() = {
		if (this.attrCur.seed < 0) {
			this.attrCur.seed = time.clock()
		}
		randseed(this.attrCur.seed)
		this.pages = repeat (this.attrCur.nPages):list {
			Page(this.MakeQuestions())
		}
		if (this.iPageCur >= this.attrCur.nPages) {
			this.iPageCur = this.attrCur.nPages - 1
		}
	}
	OutputImage(image:image) = {
		this.UpdatePages()
		image.fill(`white)
		scale = 2
		image.cairo {|cr|
			cr.scale(scale, scale)
			this.ComposePage(cr, this.iPageCur)
		}
	}
	OutputPDF() = {
		this.UpdatePages()
		scale = units.mm$pt(210) / 210
		cairo.create(cairo.pdf_surface.create(this.attrCur.fileNamePDF,
								units.mm$pt(210), units.mm$pt(297))) {|cr|
			cr.scale(scale, scale)
			repeat (this.attrCur.nPages) {|iPage|
				this.ComposePage(cr, iPage)
				cr.show_page()
			}
			cr.destroy()
		}
	}
	MakeNumWide(num:number) = this.wideDict[format('%d', num).each()].join()
	MakeTextWide(text:string) = {
		chars = text.each(), this.wideDict.gets(chars, chars).join()
	}
}

//-----------------------------------------------------------------------------
// LineComposer
//-----------------------------------------------------------------------------
LineComposer = class(Composer) {
	__init__() = {|`vertical|
		this.variableQuestionsHFlag = false
		this.fontSize = 8
		this.wdLeft = 17
		this.wdOperator = 12
		this.wdRight = 17
		this.wdEqual = 12
		this.wdAnswer = 30
		this.alignAnswer = `center
		this.Initialize()
	}
	ComposePageBody(cr:cairo.context, iPage:number) = {
		cr.set_font_size(this.fontSize)
		rects = this.CalcRectBounding(
				range(this.attrCur.cntHorz * this.attrCur.cntVert))
		this.DrawQuestion(cr, rects, this.pages[iPage].questions)
	}
	DrawQuestion(cr:cairo.context, rect:Rect, question:LineQuestion):map = {
		cr.set_source_color(this.colorQuestion)
		wdBody = this.wdLeft + this.wdOperator + \
							this.wdRight + this.wdEqual + this.wdAnswer
		htBox = min(rect.height * .9, this.fontSize * 1.5)
		x = rect.x
		yCenter = rect.y + rect.height / 2
		cr.ShowText(`center, Point(x + this.wdLeft / 2, yCenter),
									this.MakeNumWide(question.numL))
		x += this.wdLeft
		cr.ShowText(`center, Point(x + this.wdOperator / 2, yCenter),
									question.textOperator)
		x += this.wdOperator
		cr.ShowText(`center, Point(x + this.wdRight / 2, yCenter),
									this.MakeNumWide(question.numR))
		x += this.wdRight
		cr.ShowText(`center, Point(x + this.wdEqual / 2, yCenter), '＝')
		x += this.wdEqual
		rcAnswer = Rect(x, yCenter - htBox / 2, this.wdAnswer, htBox)
		cr.Rectangle(rcAnswer)
		cr.stroke()
		if (this.attrCur.answerFlag) {
			cr.set_source_color(this.colorAnswer)
			text = if (question.textAnswer) {
				this.MakeTextWide(question.textAnswer)
			} else {
				this.MakeNumWide(question.numAnswer)
			}
			if (this.alignAnswer == `center) {
				cr.ShowText(`center, rcAnswer.Center(), text)
			} elsif (this.alignAnswer == `left) {
				cr.ShowText(`east, rcAnswer.LeftVCenter(), text)
			}
		}
	}
}

//-----------------------------------------------------------------------------
// BoxComposer
//-----------------------------------------------------------------------------
BoxComposer = class(Composer) {
	__init__() = {|`horizontal|
		this.wdLeftMgn = 2, this.wdRightMgn = 2
		this.htTopMgn = 2, this.htBottomMgn = 2
		[this.nCols, this.nRows] = [3, 3]
		[this.wdColMax, this.htRowMax] = [12, 16]
		this.wdTail = 2
		this.Initialize()
	}
	ComposePageBody(cr:cairo.context, iPage:number) = {
		rects = this.CalcRectQuestion(
				range(this.attrCur.cntHorz * this.attrCur.cntVert))
		cr.set_font_size(this.CalcRectCell(this.CalcRectQuestion(0), 0, 0).height * .6)
		this.DrawQuestion(cr, rects, this.pages[iPage].questions)
	}
	CalcRectQuestion(index:number):map = {
		rect = this.CalcRectBounding(index)
		rect.x += 2
		rect.width -= 2 * 2
		width = rect.width - (this.wdLeftMgn + this.wdRightMgn)
		height = rect.height - (this.htTopMgn + this.htBottomMgn)
		[wdCol, htRow] = [width / this.nCols, height / this.nRows]
		wdCol = min(wdCol, this.wdColMax)
		htRow = min(htRow, this.htRowMax)
		if (htRow > wdCol) { htRow = wdCol }
		if (wdCol > htRow) { wdCol = htRow }
		[width, height] = [wdCol * this.nCols, htRow * this.nRows]
		Rect(rect.x + (rect.width - width) / 2,
						rect.y + (rect.height - height) / 2, width, height)
	}
	CalcRectCell(rect:Rect, iRow:number, iCol:number):map = {
		wdCol = rect.width / this.nCols
		htRow = rect.height / this.nRows
		x = rect.x + iCol * wdCol
		y = rect.y + iRow * htRow
		Rect(x, y, wdCol, htRow)
	}
	DrawQuestion(cr:cairo.context, rect:Rect, question):map = {
		cr.set_source_color(this.colorQuestion)
		textL = this.MakeNumWide(question.numL)
		textR = this.MakeNumWide(question.numR)
		iColL = this.nCols - max(textL.len(), textR.len()) - 1
		if (question.istype(`BoxQuestionDiv)) {
			iColR = this.nCols - 1
			cr.ShowText(`center, this.CalcRectCell(rect, 1, iColR - (0..)):*Center(),
												textL.each().reverse())
			cr.ShowText(`center, this.CalcRectCell(rect, 1, iColL - (0..)):*Center():*Offset(-4, 0),
												textR.each().reverse())
			rcCellL = this.CalcRectCell(rect, 1, iColL)
			rcCellR = this.CalcRectCell(rect, 1, iColR)
			cr.move_to(rcCellR.Right() + this.wdTail, rcCellR.Top())
			cr.line_to(rcCellL.Right() - this.wdTail, rcCellL.Top())
			x = rcCellL.Right()
			y1 = (rcCellL.Top() * 2 + rcCellL.Bottom() * 1) / 3
			y2 = (rcCellL.Top() * 1 + rcCellL.Bottom() * 2) / 3
			cr.curve_to(x, y1, x, y2,
					rcCellL.Right() - this.wdTail, rcCellL.Bottom())
			cr.stroke()
			if (this.attrCur.answerFlag) {
				cr.set_source_color(this.colorAnswer)
				numAnswer = int(question.numL / question.numR)
				numAlign = question.numR * numAnswer
				numRest = question.numL - numAlign
				textAnswer = this.MakeNumWide(numAnswer)
				textAlign = this.MakeNumWide(numAlign)
				textRest = this.MakeNumWide(numRest)
				cr.ShowText(`center, this.CalcRectCell(rect, 0, iColR - (0..)):*Center(),
												textAnswer.each().reverse())
				cr.ShowText(`center, this.CalcRectCell(rect, 2, iColR - (0..)):*Center(),
												textAlign.each().reverse())
				cr.ShowText(`center, this.CalcRectCell(rect, 3, iColR - (0..)):*Center(),
												textRest.each().reverse())
				rcCellL = this.CalcRectCell(rect, 3, iColL)
				rcCellR = this.CalcRectCell(rect, 3, iColR)
				cr.move_to(rcCellR.Right() + this.wdTail, rcCellR.Top())
				cr.line_to(rcCellL.Right() - this.wdTail, rcCellL.Top())
				cr.stroke()
			}
		} else {
			iColR = this.nCols - 1
			cr.ShowText(`center, this.CalcRectCell(rect, 0, iColR - (0..)):*Center(),
												textL.each().reverse())
			cr.ShowText(`center, this.CalcRectCell(rect, 1, iColR - (0..)):*Center(),
												textR.each().reverse())
			cr.ShowText(`center, this.CalcRectCell(rect, 1, iColL).Center(),
												question.textOperator)
			rcCellL = this.CalcRectCell(rect, 2, iColL)
			rcCellR = this.CalcRectCell(rect, 2, iColR)
			cr.move_to(rcCellL.Left() - this.wdTail, rcCellL.Top())
			cr.line_to(rcCellR.Right() + this.wdTail, rcCellR.Top())
			cr.stroke()
			if (this.attrCur.answerFlag) {
				cr.set_source_color(this.colorAnswer)
				textAnswer = this.MakeNumWide(question.numAnswer)
				cr.ShowText(`center, this.CalcRectCell(rect, 2, iColR - (0..)):*Center(),
												textAnswer.each().reverse())
			}
		}
	}
}

//-----------------------------------------------------------------------------
// FractionComposer
//-----------------------------------------------------------------------------
FractionComposer = class(Composer) {
	__init__() = {|`vertical|
		this.variableQuestionsHFlag = false
		this.fontSize = 8
		this.wdLeft = 20
		this.wdOperator = 12
		this.wdRight = 20
		this.wdEqual = 12
		this.wdAnswer = 25
		this.alignAnswer = `center
		this.Initialize()
	}
	ComposePageBody(cr:cairo.context, iPage:number) = {
		cr.set_font_size(this.fontSize)
		rects = this.CalcRectBounding(
				range(this.attrCur.cntHorz * this.attrCur.cntVert))
		this.DrawQuestion(cr, rects, this.pages[iPage].questions)
	}
	DrawQuestion(cr:cairo.context, rect:Rect, question:FractionQuestion):map = {
		cr.set_source_color(this.colorQuestion)
		wdBody = this.wdLeft + this.wdOperator + \
							this.wdRight + this.wdEqual + this.wdAnswer
		x = rect.x
		yCenter = rect.y + rect.height / 2
		scope {
			xCenter = x + this.wdLeft / 2
			cr.ShowText(`center, Point(xCenter, (rect.y + yCenter) / 2),
									this.MakeNumWide(question.fracL.num))
			cr.ShowText(`center, Point(xCenter, (rect.Bottom() + yCenter) / 2),
									this.MakeNumWide(question.fracL.denom))
			cr.move_to(x, yCenter)
			cr.line_to(x + this.wdLeft, yCenter)
		}
		x += this.wdLeft
		cr.ShowText(`center, Point(x + this.wdOperator / 2, yCenter),
									question.textOperator)
		x += this.wdOperator
		scope {
			xCenter = x + this.wdRight / 2
			cr.ShowText(`center, Point(xCenter, (rect.y + yCenter) / 2),
									this.MakeNumWide(question.fracR.num))
			cr.ShowText(`center, Point(xCenter, (rect.Bottom() + yCenter) / 2),
									this.MakeNumWide(question.fracR.denom))
			cr.move_to(x, yCenter)
			cr.line_to(x + this.wdRight, yCenter)
		}
		x += this.wdRight
		cr.ShowText(`center, Point(x + this.wdEqual / 2, yCenter), '＝')
		x += this.wdEqual
		cr.stroke()
		//cr.Rectangle(rect)
		//cr.stroke()
		if (this.attrCur.answerFlag) {
			cr.set_source_color(this.colorAnswer)
			xCenter = x + this.wdAnswer / 2
			cr.ShowText(`center, Point(xCenter, (rect.y + yCenter) / 2),
									this.MakeNumWide(question.fracAnswer.num))
			cr.ShowText(`center, Point(xCenter, (rect.Bottom() + yCenter) / 2),
									this.MakeNumWide(question.fracAnswer.denom))
			cr.move_to(x, yCenter)
			cr.line_to(x + this.wdAnswer, yCenter)
			cr.stroke()
		}
	}
}

//-----------------------------------------------------------------------------
// Canvas
//-----------------------------------------------------------------------------
Canvas = class(wx.Panel) {
	__init__(parent:wx.Window) = {|parent, style => wx.BORDER_SUNKEN|
		this.img = image(`rgba, wx.GetApp().wdCanvas, wx.GetApp().htCanvas)
		this.img.fill(`white)
		this.Bind(wx.EVT_ERASE_BACKGROUND) { /* nothing to do */ }
		this.Bind(wx.EVT_SCROLLWIN) {|event|
			this.SetScrollPos(event.GetOrientation(), event.GetPosition())
			this.Refresh()
		}
		this.Bind(wx.EVT_SIZE) {|event|
			[wdClient, htClient] = this.GetClientSizeWH()
			this.SetScrollbar(wx.HORIZONTAL, 0, wdClient, wx.GetApp().wdCanvas)
			this.SetScrollbar(wx.VERTICAL, 0, htClient, wx.GetApp().htCanvas)
			this.Refresh();
			event.Skip()
		}
		this.Bind(wx.EVT_PAINT) {|event|
			[wdClient, htClient] = this.GetClientSizeWH()
			[xOffset, yOffset] = [-this.GetScrollPos(wx.HORIZONTAL),
											-this.GetScrollPos(wx.VERTICAL)]
			dc = wx.PaintDC(this)
			dc.SetPen(wx.TRANSPARENT_PEN)
			dc.SetBrush(wx.Brush(wx.Colour(192, 192, 192)))
			if ((wdBack = wdClient - (this.img.width + xOffset)) > 0) {
				dc.DrawRectangle(wdClient - wdBack, 0, wdBack, htClient);
			}
			if ((htBack = htClient - (this.img.height + yOffset)) > 0) {
				dc.DrawRectangle(0, htClient - htBack, wdClient, htBack)
			}
			dc.DrawBitmap(this.img, xOffset, yOffset, false)
			dc = nil
		}
	}
	DrawContent(composer:Composer) = {
		composer.OutputImage(this.img)
		this.Refresh()
	}
}

composerClasses = @{
	class(FractionComposer) {
		Initialize() = {
			this.category = 'たし算'
			this.title = ['たし算', 'たしざん']
			this.composerName = '分数 ＋ 分数'
			this.imgIcon = wx.GetApp().IMG_Plus
			this.cntHorzMax = 2
			this.cntVertMax = 15
			this.attrCur.cntHorz = 2
			this.attrCur.cntVert = 10
			this.attrCur.kanjiFlag = false
		}
		MakeQuestion() = {
			denom = rand(8) + 2
			num = rand(denom - 2) + 1
			fracL = Fraction(num, denom).Reduce()
			denom = rand(8) + 2
			num = rand(denom - 2) + 1
			fracR = Fraction(num, denom).Reduce()
			fracAnswer = Fraction(fracL.num * fracR.denom + fracR.num * fracL.denom,
											fracL.denom * fracR.denom).Reduce()
			FractionQuestion(fracL, '＋', fracR, fracAnswer)
		}
	}
	class(LineComposer) {
		Initialize() = {
			this.category = 'たし算'
			this.title = ['たし算', 'たしざん']
			this.composerName = '1 桁 ＋ 1 桁 = 1 桁'
			this.imgIcon = wx.GetApp().IMG_Plus
			this.cntHorzMax = 2
			this.cntVertMax = 15
			this.attrCur.cntHorz = 2
			this.attrCur.cntVert = 10
			this.attrCur.kanjiFlag = false
		}
		MakeQuestion() = {
			numL = rand(8) + 1
			numR = rand(8 - numL) + 1
			numAnswer = numL + numR
			LineQuestion(numL, '＋', numR, numAnswer)
		}
	}
	class(LineComposer) {
		Initialize() = {
			this.category = 'たし算'
			this.title = ['たし算', 'たしざん']
			this.composerName = '1 桁 ＋ 1 桁'
			this.imgIcon = wx.GetApp().IMG_Plus
			this.cntHorzMax = 2
			this.cntVertMax = 15
			this.attrCur.cntHorz = 2
			this.attrCur.cntVert = 10
			this.attrCur.kanjiFlag = false
		}
		MakeQuestion() = {
			numL = rand(9) + 1
			numR = rand(9) + 1
			numAnswer = numL + numR
			LineQuestion(numL, '＋', numR, numAnswer)
		}
	}
	class(LineComposer) {
		Initialize() = {
			this.category = 'たし算'
			this.title = ['たし算', 'たしざん']
			this.composerName = '2 桁 ＋ 1 桁'
			this.imgIcon = wx.GetApp().IMG_Plus
			this.cntHorzMax = 2
			this.cntVertMax = 15
			this.attrCur.cntHorz = 2
			this.attrCur.cntVert = 10
			this.attrCur.kanjiFlag = false
		}
		MakeQuestion() = {
			numL = rand(99) + 1
			numR = rand(9) + 1
			numAnswer = numL + numR
			LineQuestion(numL, '＋', numR, numAnswer)
		}
	}
	class(LineComposer) {
		Initialize() = {
			this.category = 'たし算'
			this.title = ['たし算', 'たしざん']
			this.composerName = '2 桁 ＋ 2 桁'
			this.imgIcon = wx.GetApp().IMG_Plus
			this.cntHorzMax = 2
			this.cntVertMax = 15
			this.attrCur.cntHorz = 2
			this.attrCur.cntVert = 10
		}
		MakeQuestion() = {
			numL = rand(99) + 1
			numR = rand(99) + 1
			numAnswer = numL + numR
			LineQuestion(numL, '＋', numR, numAnswer)
		}
	}
	class(LineComposer) {
		Initialize() = {
			this.category = 'ひき算'
			this.title = ['ひき算', 'ひきざん']
			this.composerName = '1 桁 － 1 桁'
			this.imgIcon = wx.GetApp().IMG_Minus
			this.cntHorzMax = 2
			this.cntVertMax = 15
			this.attrCur.cntHorz = 2
			this.attrCur.cntVert = 10
			this.attrCur.kanjiFlag = false
		}
		MakeQuestion() = {
			numL = rand(8) + 2
			numR = rand(numL - 2) + 1
			numAnswer = numL - numR
			LineQuestion(numL, '－', numR, numAnswer)
		}
	}
	class(LineComposer) {
		Initialize() = {
			this.category = 'ひき算'
			this.title = ['ひき算', 'ひきざん']
			this.composerName = '2 桁 － 2 桁'
			this.imgIcon = wx.GetApp().IMG_Minus
			this.cntHorzMax = 2
			this.cntVertMax = 15
			this.attrCur.cntHorz = 2
			this.attrCur.cntVert = 10
		}
		MakeQuestion() = {
			numL = rand(90) + 10
			numR = rand(numL - 2) + 1
			numAnswer = numL - numR
			LineQuestion(numL, '－', numR, numAnswer)
		}
	}
	class(LineComposer) {
		Initialize() = {
			this.category = 'ひき算'
			this.title = ['ひき算', 'ひきざん']
			this.composerName = '3 桁 － 3 桁'
			this.imgIcon = wx.GetApp().IMG_Minus
			this.cntHorzMax = 2
			this.cntVertMax = 15
			this.attrCur.cntHorz = 2
			this.attrCur.cntVert = 10
			this.fontSize = 7
		}
		MakeQuestion() = {
			numL = rand(900) + 100
			numR = rand(numL - 2) + 1
			numAnswer = numL - numR
			LineQuestion(numL, '－', numR, numAnswer)
		}
	}
	class(LineComposer) {
		Initialize() = {
			this.category = 'かけ算'
			this.title = ['かけ算', 'かけざん']
			this.composerName = '1 桁 × 1 桁'
			this.imgIcon = wx.GetApp().IMG_Multiply
			this.cntHorzMax = 2
			this.cntVertMax = 15
			this.attrCur.cntHorz = 2
			this.attrCur.cntVert = 10
		}
		MakeQuestion() = {
			numL = rand(9) + 1
			numR = rand(9) + 1
			numAnswer = numL * numR
			LineQuestion(numL, '×', numR, numAnswer)
		}
	}
	class(LineComposer) {
		Initialize() = {
			this.category = 'かけ算'
			this.title = ['かけ算', 'かけざん']
			this.composerName = '2 桁 × 1 桁'
			this.imgIcon = wx.GetApp().IMG_Multiply
			this.cntHorzMax = 2
			this.cntVertMax = 15
			this.attrCur.cntHorz = 2
			this.attrCur.cntVert = 10
		}
		MakeQuestion() = {
			numL = rand(99) + 1
			numR = rand(9) + 1
			numAnswer = numL * numR
			LineQuestion(numL, '×', numR, numAnswer)
		}
	}
	class(LineComposer) {
		Initialize() = {
			this.category = 'わり算'
			this.title = ['わり算', 'わりざん']
			this.composerName = '2 桁 ÷ 1 桁 = 1 桁 (余りなし)'
			this.imgIcon = wx.GetApp().IMG_Divide
			this.cntHorzMax = 2
			this.cntVertMax = 15
			this.attrCur.cntHorz = 2
			this.attrCur.cntVert = 10
		}
		MakeQuestion() = {
			numAnswer = rand(9) + 1
			numR = rand(9) + 1
			numL = numAnswer * numR
			LineQuestion(numL, '÷', numR, numAnswer)
		}
	}
	class(LineComposer) {
		Initialize() = {
			this.category = 'わり算'
			this.title = ['わり算', 'わりざん']
			this.composerName = '2 桁 ÷ 1 桁 = 1 桁'
			this.imgIcon = wx.GetApp().IMG_Divide
			this.cntHorzMax = 2
			this.cntVertMax = 15
			this.attrCur.cntHorz = 2
			this.attrCur.cntVert = 10
			this.alignAnswer = `left
		}
		MakeQuestion() = {
			numAnswer = rand(9) + 1
			numR = rand(9) + 1
			numRest = rand(numR)
			numL = numAnswer * numR + numRest
			if (numRest == 0) {
				LineQuestion(numL, '÷', numR, 0, format('%d', numAnswer))
			} else {
				LineQuestion(numL, '÷', numR, 0, format('%d … %d', numAnswer, numRest))
			}
		}
	}
	class(LineComposer) {
		Initialize() = {
			this.category = 'わり算'
			this.title = ['わり算', 'わりざん']
			this.composerName = '2 桁 ÷ 1 桁 = 2 桁'
			this.imgIcon = wx.GetApp().IMG_Divide
			this.cntHorzMax = 2
			this.cntVertMax = 15
			this.attrCur.cntHorz = 2
			this.attrCur.cntVert = 10
			this.alignAnswer = `left
			this.wdLeft = 17
			this.wdRight = 10
			this.wdAnswer = 38
		}
		MakeQuestion() = {
			numR = rand(8) + 2
			numL = rand(100 - numR * 10) + numR * 10
			numAnswer = math.floor(numL / numR)
			numRest = numL - numR * numAnswer
			if (numRest == 0) {
				LineQuestion(numL, '÷', numR, 0, format('%d', numAnswer))
			} else {
				LineQuestion(numL, '÷', numR, 0, format('%d … %d', numAnswer, numRest))
			}
		}
	}
	class(BoxComposer) {
		Initialize() = {
			this.category = 'たし算 (筆算)'
			this.title = ['たし算', 'たしざん']
			this.composerName = '2 桁 ＋ 2 桁'
			[this.nCols, this.nRows] = [3, 3]
			this.imgIcon = wx.GetApp().IMG_Plus
			this.cntHorzMax = 4
			this.cntVertMax = 5
			this.attrCur.cntHorz = 4
			this.attrCur.cntVert = 5
		}
		MakeQuestion() = {
			numL = rand(90) + 10
			numR = rand(99) + 1
			numAnswer = numL + numR
			BoxQuestion(numL, '＋', numR, numAnswer)
		}
	}
	class(BoxComposer) {
		Initialize() = {
			this.category = 'たし算 (筆算)'
			this.title = ['たし算', 'たしざん']
			this.composerName = '3 桁 ＋ 3 桁'
			[this.nCols, this.nRows] = [4, 3]
			this.imgIcon = wx.GetApp().IMG_Plus
			this.cntHorzMax = 3
			this.cntVertMax = 6
			this.attrCur.cntHorz = 3
			this.attrCur.cntVert = 6
		}
		MakeQuestion() = {
			numL = rand(900) + 100
			numR = rand(999) + 1
			numAnswer = numL + numR
			BoxQuestion(numL, '＋', numR, numAnswer)
		}
	}
	class(BoxComposer) {
		Initialize() = {
			this.category = 'ひき算 (筆算)'
			this.title = ['ひき算', 'ひきざん']
			this.composerName = '2 桁 － 2 桁'
			[this.nCols, this.nRows] = [3, 3]
			this.imgIcon = wx.GetApp().IMG_Minus
			this.cntHorzMax = 4
			this.cntVertMax = 5
			this.attrCur.cntHorz = 4
			this.attrCur.cntVert = 5
		}
		MakeQuestion() = {
			numL = rand(90) + 10
			numR = rand(numL - 2) + 1
			numAnswer = numL - numR
			BoxQuestion(numL, '－', numR, numAnswer)
		}
	}
	class(BoxComposer) {
		Initialize() = {
			this.category = 'ひき算 (筆算)'
			this.title = ['ひき算', 'ひきざん']
			this.composerName = '3 桁 － 3 桁'
			[this.nCols, this.nRows] = [4, 3]
			this.imgIcon = wx.GetApp().IMG_Minus
			this.cntHorzMax = 3
			this.cntVertMax = 6
			this.attrCur.cntHorz = 3
			this.attrCur.cntVert = 6
		}
		MakeQuestion() = {
			numL = rand(900) + 100
			numR = rand(numL - 2) + 1
			numAnswer = numL - numR
			BoxQuestion(numL, '－', numR, numAnswer)
		}
	}
	class(BoxComposer) {
		Initialize() = {
			this.category = 'かけ算 (筆算)'
			this.title = ['かけ算', 'かけざん']
			this.composerName = '2 桁 × 1 桁'
			[this.nCols, this.nRows] = [3, 3]
			this.imgIcon = wx.GetApp().IMG_Multiply
			this.cntHorzMax = 4
			this.cntVertMax = 5
			this.attrCur.cntHorz = 4
			this.attrCur.cntVert = 5
		}
		MakeQuestion() = {
			numL = rand(90) + 10
			numR = rand(8) + 2
			//if (rand() < .1) { [numL, numR] = [numR, numL] }
			numAnswer = numL * numR
			BoxQuestion(numL, '×', numR, numAnswer)
		}
	}
	class(BoxComposer) {
		Initialize() = {
			this.category = 'かけ算 (筆算)'
			this.title = ['かけ算', 'かけざん']
			this.composerName = '3 桁 × 1 桁'
			[this.nCols, this.nRows] = [4, 3]
			this.imgIcon = wx.GetApp().IMG_Multiply
			this.cntHorzMax = 3
			this.cntVertMax = 5
			this.attrCur.cntHorz = 3
			this.attrCur.cntVert = 5
		}
		MakeQuestion() = {
			numL = rand(900) + 100
			numR = rand(8) + 2
			numAnswer = numL * numR
			BoxQuestion(numL, '×', numR, numAnswer)
		}
	}
	class(BoxComposer) {
		Initialize() = {
			this.category = 'かけ算 (筆算)'
			this.title = ['かけ算', 'かけざん']
			this.composerName = '2 桁 × 2 桁'
			[this.nCols, this.nRows] = [4, 5]
			this.imgIcon = wx.GetApp().IMG_Multiply
			this.cntHorzMax = 3
			this.cntVertMax = 4
			this.attrCur.cntHorz = 3
			this.attrCur.cntVert = 4
		}
		MakeQuestion() = {
			numL = rand(90) + 10
			numR = rand(98) + 2
			if (rand() < .5) {
				[numL, numR] = [numR, numL]
			}
			numAnswer = numL * numR
			BoxQuestion(numL, '×', numR, numAnswer)
		}
	}
	class(BoxComposer) {
		Initialize() = {
			this.category = 'わり算 (筆算)'
			this.title = ['わり算', 'わりざん']
			this.composerName = '2 桁 ÷ 1 桁 = 2 桁'
			[this.nCols, this.nRows] = [3, 6]
			this.imgIcon = wx.GetApp().IMG_Divide
			this.cntHorzMax = 3
			this.cntVertMax = 4
			this.attrCur.cntHorz = 3
			this.attrCur.cntVert = 4
		}
		MakeQuestion() = {
			numR = rand(8) + 2
			numL = rand(100 - numR * 10) + numR * 10
			BoxQuestionDiv(numL, numR)
		}
	}
	class(BoxComposer) {
		Initialize() = {
			this.category = 'わり算 (筆算)'
			this.title = ['わり算', 'わりざん']
			this.composerName = '3 桁 ÷ 1 桁'
			[this.nCols, this.nRows] = [4, 8]
			this.imgIcon = wx.GetApp().IMG_Divide
			this.cntHorzMax = 3
			this.cntVertMax = 3
			this.attrCur.cntHorz = 3
			this.attrCur.cntVert = 3
		}
		MakeQuestion() = {
			numL = rand(900) + 100
			numR = rand(8) + 2
			BoxQuestionDiv(numL, numR)
		}
	}
	class(BoxComposer) {
		Initialize() = {
			this.category = 'わり算 (筆算)'
			this.title = ['わり算', 'わりざん']
			this.composerName = '4 桁 ÷ 1 桁'
			[this.nCols, this.nRows] = [5, 10]
			this.imgIcon = wx.GetApp().IMG_Divide
			this.cntHorzMax = 3
			this.cntVertMax = 3
			this.attrCur.cntHorz = 3
			this.attrCur.cntVert = 3
		}
		MakeQuestion() = {
			numL = rand(9000) + 1000
			numR = rand(8) + 2
			BoxQuestionDiv(numL, numR)
		}
	}
	class(BoxComposer) {
		Initialize() = {
			this.category = 'わり算 (筆算)'
			this.title = ['わり算', 'わりざん']
			this.composerName = '3 桁 ÷ 2 桁'
			[this.nCols, this.nRows] = [5, 6]
			this.imgIcon = wx.GetApp().IMG_Divide
			this.cntHorzMax = 3
			this.cntVertMax = 3
			this.attrCur.cntHorz = 3
			this.attrCur.cntVert = 3
		}
		MakeQuestion() = {
			numL = rand(900) + 100
			numR = rand(10) + 10
			BoxQuestionDiv(numL, numR)
		}
	}
}

//-----------------------------------------------------------------------------
// Panel
//-----------------------------------------------------------------------------
Panel = class(wx.Panel) {
	__init__(parent:wx.Window) = {|parent|
		this.composerCur = nil
		outerBox = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(outerBox)
		wx.BoxSizer(wx.HORIZONTAL) {|hbox|
			outerBox.Add(hbox, wx.SizerFlags().Expand().Border(wx.TOP, 2))
			wx.Button(this, wx.ID_ANY, '新しい問題') {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Expand())
				ctrl.Bind(wx.EVT_BUTTON) {|event|
					if (this.composerCur) {
						this.composerCur.ClearPages()
						this.RefreshCanvas()
					}
				}
			}
			wx.Button(this, wx.ID_ANY, 'PDF 出力') {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.LEFT, 4))
				ctrl.Bind(wx.EVT_BUTTON) {|event|
					composer = this.composerCur
					if (composer) {
						this.SetCursor(wx.StockCursor(wx.CURSOR_WAIT))
						composer.OutputPDF()
						this.SetCursor(wx.StockCursor(wx.CURSOR_ARROW))
						this.treeCtrl.GetItemChildren(composer.itemComposer) {|item|
							itemData = this.treeCtrl.GetGuraData(item)
							if (!itemData) {
								// nothing to do
							} elsif (isinstance(itemData, `ComposerAttr)) {
								attr = itemData
								if (attr.HasSameValues(composer.attrCur)) {
									this.treeCtrl.Delete(item)
								}
							}
						}
						attr = composer.attrCur.Clone()
						stat = path.stat(composer.attrCur.fileNamePDF)
						attr.name = stat.mtime.format('%Y-%m-%d %H:%M:%S')
						composer.attrsHist.add(attr)
						this.treeCtrl.AppendItem(composer.itemComposer,
								attr.name, data => wx.TreeItemData(attr),
								image => wx.GetApp().IMG_History)
						OpenWithApp(composer.attrCur.fileNamePDF)
					}
				}
			}
			wx.TextCtrl(this, wx.ID_ANY) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags(1).Expand().Border(wx.LEFT, 4))
				this.textFileNamePDF = ctrl
				ctrl.Bind(wx.EVT_TEXT) {|event|
					if (this.composerCur) {
						this.composerCur.attrCur.fileNamePDF = ctrl.GetValue()
					}
				}
			}
			wx.Button(this, wx.ID_ANY, '...', size => wx.Size(20, -1)) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Expand())
			}
			wx.StaticText(this, wx.ID_ANY, '作成する枚数') {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().\
							Border(wx.LEFT, 8).Align(wx.ALIGN_CENTRE_VERTICAL))
			}
			wx.SpinCtrl(this, wx.ID_ANY,
						size => wx.Size(60, -1), min => 1, max => 40) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.LEFT, 4))
				this.spinNPages = ctrl
				ctrl.Bind(wx.EVT_SPINCTRL) {|event|
					if (this.composerCur) {
						this.composerCur.attrCur.nPages = ctrl.GetValue()
						this.UpdateControls()
					}
				}
			}
		}
		wx.StaticLine(this, wx.ID_ANY) {|ctrl|
			outerBox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.TOP, 2))
		}
		wx.BoxSizer(wx.HORIZONTAL) {|hbox|
			outerBox.Add(hbox, wx.SizerFlags().Expand().Border(wx.TOP, 2))
			wx.StaticText(this, wx.ID_ANY, '一枚にのせる問題数') {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL))
			}
			wx.SpinCtrl(this, wx.ID_ANY,
						size => wx.Size(50, -1), min => 1, max => 100) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.LEFT, 4))
				this.spinCntHorz = ctrl
				ctrl.Bind(wx.EVT_SPINCTRL) {|event|
					composer = this.composerCur
					if (composer) {
						composer.attrCur.cntHorz = ctrl.GetValue()
						composer.ClearPages()
						this.RefreshCanvas()
					}
				}
			}
			wx.StaticText(this, wx.ID_ANY, '×') {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().\
						Align(wx.ALIGN_CENTRE_VERTICAL).Border(wx.LEFT, 2))
			}
			wx.SpinCtrl(this, wx.ID_ANY,
						size => wx.Size(50, -1), min => 1, max => 100) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.LEFT, 2))
				this.spinCntVert = ctrl
				ctrl.Bind(wx.EVT_SPINCTRL) {|event|
					composer = this.composerCur
					if (composer) {
						composer.attrCur.cntVert = ctrl.GetValue()
						composer.ClearPages()
						this.RefreshCanvas()
					}
				}
			}
			wx.CheckBox(this, wx.ID_ANY, 'タイトルなどに漢字を使う') {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.LEFT, 8))
				this.chkKanjiFlag = ctrl
				ctrl.Bind(wx.EVT_CHECKBOX) {|event|
					if (this.composerCur) {
						this.composerCur.attrCur.kanjiFlag = ctrl.GetValue()
						this.RefreshCanvas()
					}
				}
			}
			wx.CheckBox(this, wx.ID_ANY, '答えを作成する') {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.LEFT, 8))
				this.chkAnswerFlag = ctrl
				ctrl.Bind(wx.EVT_CHECKBOX) {|event|
					if (this.composerCur) {
						this.composerCur.attrCur.answerFlag = ctrl.GetValue()
						this.RefreshCanvas()
					}
				}
			}
			wx.CheckBox(this, wx.ID_ANY, '点数欄を追加する') {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.LEFT, 8))
				this.chkScoreBoxFlag = ctrl
				ctrl.Bind(wx.EVT_CHECKBOX) {|event|
					if (this.composerCur) {
						this.composerCur.attrCur.scoreBoxFlag = ctrl.GetValue()
						this.RefreshCanvas()
					}
				}
			}
			hbox.AddStretchSpacer(1)
			wx.StaticText(this, wx.ID_ANY, '表示枚目') {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Align(wx.ALIGN_CENTRE_VERTICAL))
			}
			wx.SpinCtrl(this, wx.ID_ANY,
						size => wx.Size(60, -1), min => 1, max => 100) {|ctrl|
				hbox.Add(ctrl, wx.SizerFlags().Expand().Border(wx.LEFT, 4))
				this.spinIPage = ctrl
				ctrl.Bind(wx.EVT_SPINCTRL) {|event|
					if (this.composerCur) {
						this.composerCur.iPageCur = ctrl.GetValue() - 1
						this.RefreshCanvas()
					}
				}
			}
		}
		wx.Panel(this) {|panel|
			outerBox.Add(panel, wx.SizerFlags(1).Expand().Border(wx.TOP, 2))
			wx.SashLayoutWindow(panel, wx.ID_ANY,
					style => wx.NO_BORDER | wx.SW_3D | wx.CLIP_CHILDREN) {|sash|
				sash.SetDefaultSize(wx.Size(wx.GetApp().cfg['wdNavi'], 1000));
				sash.SetOrientation(wx.LAYOUT_VERTICAL);
				sash.SetAlignment(wx.LAYOUT_LEFT);
				sash.SetSashVisible(wx.SASH_RIGHT, true);
				panel.sashLeft = sash
			}
			wx.Panel(panel.sashLeft) {|panelNavi|
				outerBox = wx.BoxSizer(wx.VERTICAL)
				panelNavi.SetSizer(outerBox)
				wx.TreeCtrl(panelNavi, wx.ID_ANY, style => wx.TR_HIDE_ROOT | \
								wx.TR_LINES_AT_ROOT | wx.TR_HAS_BUTTONS) {|ctrl|
					outerBox.Add(ctrl, wx.SizerFlags(1).Expand())
					ctrl.SetImageList(wx.GetApp().imgList)
					itemRoot = ctrl.AddRoot('')
					cfgSelect = wx.GetApp().cfg['select']
					itemSelect = nil
					wx.GetApp().composers.each() {|composer|
						foundFlag = false
						ctrl.GetItemChildren(itemRoot) {|itemCategory|
							category = ctrl.GetItemText(itemCategory)
							if (composer.category == category) {
								foundFlag = true
								break
							}
						}
						if (!foundFlag) {
							itemCategory = ctrl.AppendItem(itemRoot,
										composer.category, image => composer.imgIcon)
						}
						itemComposer = ctrl.AppendItem(itemCategory, composer.composerName,
								image => composer.imgIcon, data => wx.TreeItemData(composer))
						ctrl.AppendItem(itemComposer, composer.attrsHist::name,
								image => wx.GetApp().IMG_History,
								data => wx.TreeItemData(composer.attrsHist))
						composer.itemComposer = itemComposer
						if (composer.category == cfgSelect['category'] && \
								composer.composerName == cfgSelect['composerName']) {
							itemSelect = itemComposer
							this.composerCur = composer
						}
					}
					itemSelect && ctrl.SelectItem(itemSelect)
					ctrl.Bind(wx.EVT_TREE_SEL_CHANGED) {|event|
						item = event.GetItem()
						itemData = ctrl.GetGuraData(item)
						composerSel = nil
						if (!itemData) {
							// nothing to do
						} elsif (isinstance(itemData, `Composer)) {
							composerSel = itemData
						} elsif (isinstance(itemData, `ComposerAttr)) {
							attr = itemData
							itemParent = ctrl.GetItemParent(item)
							if (itemParent) {
								itemDataParent = ctrl.GetGuraData(itemParent)
								if (!itemDataParent) {
									// nothing to do
								} elsif (isinstance(itemDataParent, `Composer)) {
									composerSel = itemDataParent
									composerSel.attrCur = attr.Clone()
								}
							}
						}
						if (composerSel) {
							this.composerCur = composerSel
							cfgSelect['category', 'composerName'] = \
										[composerSel.category, composerSel.composerName]
							this.UpdateControls()
						}
					}
					this.treeCtrl = ctrl
				}
			}
			this.canvas = Canvas(panel)
			panel.Bind(wx.EVT_SIZE) {|event|
				wx.LayoutAlgorithm().LayoutWindow(panel, this.canvas)
			}
			panel.Bind(wx.EVT_SASH_DRAGGED) {|event|
				id = event.GetId()
				if (id == panel.sashLeft.GetId()) {
					wdNavi = event.GetDragRect().GetWidth() + 2
					panel.sashLeft.SetDefaultSize(wx.Size(wdNavi, 1000))
					wx.GetApp().cfg['wdNavi'] = wdNavi
				}
				wx.LayoutAlgorithm().LayoutWindow(panel, this.canvas)
				this.canvas.Refresh()
			}
		}
		this.UpdateControls()
	}
	UpdateControls() = {
		composer = this.composerCur
		!composer && return
		this.RefreshCanvas() // this may have modified composer
		this.textFileNamePDF.ChangeValue(composer.attrCur.fileNamePDF)
		this.spinCntHorz.SetValue(composer.attrCur.cntHorz)
		this.spinCntVert.SetValue(composer.attrCur.cntVert)
		this.spinCntHorz.SetRange(1, composer.cntHorzMax)
		this.spinCntVert.SetRange(1, composer.cntVertMax)
		this.spinNPages.SetValue(composer.attrCur.nPages)
		this.spinIPage.SetRange(1, composer.attrCur.nPages)
		this.spinIPage.SetValue(composer.iPageCur + 1)
		this.chkKanjiFlag.SetValue(composer.attrCur.kanjiFlag)
		this.chkAnswerFlag.SetValue(composer.attrCur.answerFlag)
		this.chkScoreBoxFlag.SetValue(composer.attrCur.scoreBoxFlag)
	}
	RefreshCanvas() = {
		this.composerCur && this.canvas.DrawContent(this.composerCur)
	}
}

//-----------------------------------------------------------------------------
// Frame
//-----------------------------------------------------------------------------
Frame = class(wx.Frame) {
	__init__(parent:wx.Window) = {|parent, wx.ID_ANY,
						'Gura ドリル',
						size => wx.Size(wx.GetApp().cfg['wdFrame', 'htFrame']*)|
		this.SetIcon(wx.IconFromXPMData(sample_xpm))
		Panel(this)
		this.Bind(wx.EVT_SIZE) {|event|
			size = event.GetSize()
			wx.GetApp().cfg['wdFrame', 'htFrame'] = [size.GetWidth(), size.GetHeight()]
			event.Skip()
		}
	}
}

sample_xpm = @{
"32 32 6 1",
"  c black",
". c navy",
"X c red",
"o c yellow",
"O c gray100",
"+ c None",
/* pixels */
"++++++++++++++++++++++++++++++++",
"++++++++++++++++++++++++++++++++",
"++++++++++++++++++++++++++++++++",
"++++++++++++++++++++++++++++++++",
"++++++++++++++++++++++++++++++++",
"++++++++              ++++++++++",
"++++++++ ............ ++++++++++",
"++++++++ ............ ++++++++++",
"++++++++ .OO......... ++++++++++",
"++++++++ .OO......... ++++++++++",
"++++++++ .OO......... ++++++++++",
"++++++++ .OO......              ",
"++++++++ .OO...... oooooooooooo ",
"         .OO...... oooooooooooo ",
" XXXXXXX .OO...... oOOooooooooo ",
" XXXXXXX .OO...... oOOooooooooo ",
" XOOXXXX ......... oOOooooooooo ",
" XOOXXXX ......... oOOooooooooo ",
" XOOXXXX           oOOooooooooo ",
" XOOXXXXXXXXX ++++ oOOooooooooo ",
" XOOXXXXXXXXX ++++ oOOooooooooo ",
" XOOXXXXXXXXX ++++ oOOooooooooo ",
" XOOXXXXXXXXX ++++ oooooooooooo ",
" XOOXXXXXXXXX ++++ oooooooooooo ",
" XXXXXXXXXXXX ++++              ",
" XXXXXXXXXXXX ++++++++++++++++++",
"              ++++++++++++++++++",
"++++++++++++++++++++++++++++++++",
"++++++++++++++++++++++++++++++++",
"++++++++++++++++++++++++++++++++",
"++++++++++++++++++++++++++++++++",
"++++++++++++++++++++++++++++++++"
}

//-----------------------------------------------------------------------------
// main function call
//-----------------------------------------------------------------------------
wx.IMPLEMENT_APP(App)
