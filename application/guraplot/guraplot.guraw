#!/usr/bin/env gura
import(tk)
import(cairo)
import(units)
import(yaml)
import(math) {*}

classref(`cairo.context) {|ref|
	ref.hline(x:number, y:number, width:number):map:reduce = {
		this.move_to(x, y).rel_line_to(width, 0)
	}
	ref.vline(x:number, y:number, height:number):map:reduce = {
		this.move_to(x, y).rel_line_to(0, height)
	}
}

fileNameCfg = path.join(sys.localdir, 'guraplot.yml')
try { cfg = yaml.read(fileNameCfg) } catch { cfg = %{} }
cfg.store():default {
	'width' => 740
	'height' => 480
}

//-----------------------------------------------------------------------------
// Graph
//-----------------------------------------------------------------------------
Graph = class {
	public {
		xMin, xMax
		yMin, yMax
		cntSamples,
	}
	__init__() = {
		this.funcs = []
		this.plots = []
		[this.xMin, this.xMax] = [-10, 10]
		[this.yMin, this.yMax] = [-10, 10]
		this.cntSamples = 100
	}
	coordX(x:number):map = {
		this.xAxis + (x - this.xMin) * this.wdAxis / (this.xMax - this.xMin)
	}
	coordY(y:number):map = {
		this.yAxis + this.htAxis - (y - this.yMin) * this.htAxis / (this.yMax - this.yMin)
	}
	coord(x:number, y:number) = [coordX(x), coordY(y)]
	calcStep(tMin:number, tMax:number) = {
		tRange = tMax - tMin
		nScales = [1, 2, 5, 10, 20, 50, 100, 200, 500, 1000]
		tStepBase = 10 ** (int(math.log10(tRange)) - 2)
		tSteps = tStepBase * nScales
		(tSteps.filter(tRange / tSteps >= 10):list).last()
	}
	draw(cr:cairo.context) = {
		[this.wdAxis, this.htAxis] = [cr.surface.width * .9, cr.surface.height * .9]
		[this.xAxis, this.yAxis] = [(cr.surface.width - this.wdAxis) / 2, (cr.surface.height - this.htAxis) / 2]
		cr.set_line_width(1)
		cr.save {
			cr.set_antialias(cairo.ANTIALIAS_NONE)
			cr.rectangle(this.xAxis, this.yAxis, this.wdAxis, this.htAxis)
			cr.stroke()
		}
		cr.rectangle(this.xAxis, this.yAxis, this.wdAxis, this.htAxis)
		cr.clip()
		cr.save {
			cr.set_antialias(cairo.ANTIALIAS_NONE)
			cr.set_dash([], 0)
			cr.vline(this.coordX(0), this.yAxis, this.htAxis)
			cr.hline(this.xAxis, this.coordY(0), this.wdAxis)
			cr.stroke()
			cr.set_dash([2, 2], 0)
			scope {
				tStep = this.calcStep(this.xMin, this.xMax)
				xs = (int(this.xMin / tStep) + \
						(0 .. int((this.xMax - this.xMin) / tStep))) * tStep
				cr.vline(this.coordX(xs), this.yAxis, this.htAxis)
			}
			scope {
				tStep = this.calcStep(this.yMin, this.yMax)
				ys = (int(this.yMin / tStep) + \
						(0 .. int((this.yMax - this.yMin) / tStep))) * tStep
				cr.hline(this.xAxis, this.coordY(ys), this.wdAxis)
			}
			cr.stroke()
		}
		cr.save {
			x = interval(this.xMin, this.xMax, this.cntSamples)
			y = interval(this.yMin, this.yMax, this.cntSamples)
			cr.set_antialias(cairo.ANTIALIAS_DEFAULT)
			for (func in this.funcs) {
				syms = [func.decls:*symbol]
				if (syms.len() != 1) {
					raise(error.ValueError, 'wrong number of arguments')
				}
				sym = syms.first()
				if (sym == `y) {
					cr.line_to(this.coordX(func(y)), this.coordY(y))
				} else {
					cr.line_to(this.coordX(x), this.coordY(func(x)))
				}
				cr.stroke()
			}
		}
		cr.save {
			for (plot in this.plots) {
				t = interval(plot.tMin, plot.tMax, this.cntSamples)
				cr.line_to(this.coordX(eval(plot.x)), this.coordY(eval(plot.y)))
				cr.stroke()
			}
		}
	}
	addFunc(func:function) = {
		idx = (this.funcs::name == func.name).find():index
		idx && this.funcs.erase(idx)
		this.funcs.add(func)
	}
}

//-----------------------------------------------------------------------------
// create a main window
//-----------------------------------------------------------------------------
tk.mainwindow() {|mw|
	itemTitles = []
	itemFocus = nil
	history = []
	graph = Graph()
	canvas = nil
	treeview = nil
	entryCommand = nil
	varCommand = tk.variable('')
	varIndicator = tk.variable('')
	imgGraphTk = nil
	showGraphic() = {
		imgGraphTk && imgGraphTk.delete()
		wdCanvas = canvas.winfo$width()
		htCanvas = canvas.winfo$height()
		img = image(`rgba, wdCanvas, htCanvas, `white)
		img.cairo {|cr|
			try {
				cr.save { graph.draw(cr) }
			} catch {|e|
				varIndicator.string = e.text
			}
		}
		imgGraphTk = img.to_tk()
		canvas.Image(0, 0, image => imgGraphTk, anchor => 'nw')
		itemTitles::raise()
	}
	mw.wm$geometry('%sx%s' % [cfg['width'], cfg['height']])
	mw.wm$title('Gura Plot')
	mw.bind('<Configure>') {|widget:string, width:number, height:number|
		(widget == '.') && (cfg['width', 'height'] = [width, height])
	}
	mw[`menu] = mw.Menu() {|m|
		m.Cascade(label => 'File', underline => 0) {|m|
			m.Command(label => 'Open', underline => 0) {
			}
			m.Command(label => 'Create PDF', underline => 0) {
				cairo.create(cairo.pdf_surface.create('cairo-pdf.pdf',
									units.mm$pt(100), units.mm$pt(100))) {|cr|
					cr.scale(units.mm$pt(100) / 100, units.mm$pt(100) / 100)
					try {
						graph.draw(cr)
					} catch {|e|
						varIndicator.string = e.text
					}
					cr.show_page()
					varIndicator.string = 'graph.pdf'
				}
			}
			m.Separator()
			m.Command(label => 'Exit', underline => 1) {
				mw.destroy()
			}
		}
		m.Cascade(label => 'Window', underline => 0) {|m|
		}
	}
	mw.Frame {|f|
		f.pack(fill => 'x', side => 'left', anchor => 'n')
		f.Button(text => 'Title', anchor => 'n', width => 10) {|w|
			w.pack(side => 'top', pady => [2, 0])
			w.bind(`command) {
				varCommand.string = "title(100, 100, '')"
				entryCommand.icursor(17)
			}
		}
		f.Button(text => 'Function', width => 10) {|w|
			w.pack(side => 'top', pady => [2, 0])
			w.bind(`command) {
				varCommand.string = 'f(x) = x'
				entryCommand.icursor(7)
			}
		}
		f.Button(text => 'Samples', width => 10) {|w|
			w.pack(side => 'top', pady => [2, 0])
			w.bind(`command) {
				varCommand.string = 'samples(%d)' % graph.cntSamples
				entryCommand.icursor(8)
			}
		}
		f.Button(text => 'X range', width => 10) {|w|
			w.pack(side => 'top', pady => [2, 0])
			w.bind(`command) {
				varCommand.string = 'xrange(%d, %d)' % [graph.xMin, graph.xMax]
				entryCommand.icursor(7)
			}
		}
		f.Button(text => 'Y range', width => 10) {|w|
			w.pack(side => 'top', pady => [2, 0])
			w.bind(`command) {
				varCommand.string = 'yrange(%d, %d)' % [graph.yMin, graph.yMax]
				entryCommand.icursor(7)
			}
		}
		f.Button(text => 'PDF', width => 10) {|w|
			w.pack(side => 'top', pady => [2, 0])
			w.bind(`command) {
				cairo.create(cairo.pdf_surface.create('graph.pdf',
									units.mm$pt(100), units.mm$pt(100))) {|cr|
					cr.scale(units.mm$pt(100) / 100, units.mm$pt(100) / 100)
					try {
						graph.draw(cr)
					} catch {|e|
						varIndicator.string = e.text
					}
					cr.show_page()
					varIndicator.string = 'graph.pdf'
				}
			}
		}
	}
	mw.Frame {|f|
		f.pack(side => 'left', fill => 'both', expand => true)
		f.Frame {|f|
			f.pack(side => 'top', fill => 'both', expand => true)
			canvas = f.Canvas(bg => 'white', width => 400, height => 400, border => 2, relief => `sunken) {|w|
				w.pack(side => 'left', fill => 'both', expand => true)
				flag:local = true
				w.bind('<Configure>') {
					if (flag) {
						flag = false
						w.bindafter(100) {
							showGraphic()
							flag:extern = true
						}
					}
				}
				w.bind('<Button-3>') {|X:number, Y:number|
					w.Menu(tearoff => false) {|m|
						m.Command(label => 'Copy as PNG', underline => 0) {
						}
						m.popup(X, Y)
					}
				}
			}
			treeview = f.ttk$Treeview(columns => ['Object', 'Type'],
							show => [`headings], selectmode => `browse) {|w|
				w.pack(fill => 'both', expand => true, pady => [2, 0])
				w.heading('Object', text => 'Object')
				w.heading('Type', text => 'Type')
				w.column('Object', minwidth => 100, width => 150)
				w.column('Type', minwidth => 60, width => 80)
				w.bind('<<TreeviewSelect>>') {
					items = w.Selection()
					println(items::obj)
				}
				w.bind('<<TreeviewOpen>>') {
				}
			}
		}
		f.Frame {|f|
			f.pack(fill => 'x')
			entryCommand = f.Entry(textvariable => varCommand, font => tk.Font(size => 12)) {|w|
				w.pack(fill => 'x')
				w.bind('<Escape>') {
					varCommand.string = ''
				}
				w.bind('<Up>') {
					//println('a')
				}
				w.bind('<Down>') {
					//println('b')
				}
				w.bind('<Return>') {
					varIndicator.string = ''
					try {
						expr = varCommand.string.parse()
						if (expr) {
							rtn = eval(expr)
							if (rtn.istype(`function)) {
								graph.addFunc(rtn)
								item = treeview.Item(values => [rtn.tostring(), 'function'])
								item.obj = rtn
							} elsif (rtn.istype(`number) || rtn.istype(`string)) {
								varIndicator.string = rtn
							} elsif (rtn.istype(`tk.CanvasItem)) {
								item = treeview.Item(values => [rtn.text, 'text'])
								item.obj = rtn
							}
							showGraphic()
							//history.add(varCommand.string)
							//varCommand.string = ''
						}
					} catch {|e|
						varIndicator.string = e.text
					}
				}
				w.focus()
			}
			f.Entry(textvariable => varIndicator, font => tk.Font(size => 12), state => `readonly) {|w|
				w.pack(fill => 'x', pady => [2, 0])
			}
		}
	}
	xrange(a:number, b:number) = {
		if (a < b) {
			[graph.xMin, graph.xMax] = [a, b]
		} elsif (a > b) {
			[graph.xMin, graph.xMax] = [b, a]
		} else {
			raise(error.ValueError, 'invalid range')
		}
	}
	yrange(a:number, b:number) = {
		if (a < b) {
			[graph.yMin, graph.yMax] = [a, b]
		} elsif (a > b) {
			[graph.yMin, graph.yMax] = [b, a]
		} else {
			raise(error.ValueError, 'invalid range')
		}
	}
	samples(cntSamples:number) = {
		graph.cntSamples = cntSamples
	}
	Plot = struct(x:expr, y:expr, tMin:number, tMax:number)
	xy(`x, `y, tMin, tMax) = {
		graph.plots.add(Plot(x, y, tMin, tMax))
	}
	title(x:number, y:number, text:string) = {
		fontName = 'MS Gothic'
		fontSize = 20
		image(`rgba, 100, 100) {|img:image|
			img.cairo {|cr|
				cr.select_font_face(fontName, `normal, `bold)
				cr.set_font_size(fontSize)
				extents = cr.text_extents(text)
			}
		}
		if (extents.width == 0 || extents.height == 0) { return }
		image(`rgba, extents.width, extents.height) {|img:image|
			img.cairo {|cr|
				cr.select_font_face(fontName, `normal, `bold)
				cr.set_font_size(fontSize)
				cr.move_to(0, extents.height).show_text(text)
			}
			item = canvas.Image(x, y, image => img.to_tk())
		}
		item.bind('<ButtonPress>') {
			itemFocus:extern = item
		}
		item.bind('<Motion>') {|x:number, y:number|
			if (itemFocus) {
				itemFocus.coords(x, y)
			}
		}
		item.bind('<ButtonRelease>') {
			itemFocus:extern = nil
		}
		itemTitles.add(item)
		item.text = text
		item
	}
	tk.update()
	showGraphic()
}
tk.mainloop()
yaml.write(open(fileNameCfg, 'w'), cfg)
