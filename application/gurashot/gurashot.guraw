#!/usr/bin/env gura
import(wx)
import(jpeg)
import(cairo)
import(resource)

//-----------------------------------------------------------------------------
// Global Variables
//-----------------------------------------------------------------------------
[wdMgn, htMgn] = [10.0, 10.0]
[wdPaper, htPaper] = [127.0 - wdMgn, 89.0 - wdMgn]

//-----------------------------------------------------------------------------
// FaceRatio
//-----------------------------------------------------------------------------
FaceRatio = struct(center:number, top:number, bottom:number)

//-----------------------------------------------------------------------------
// ComposeProduct
//-----------------------------------------------------------------------------
ComposeProduct(cr:cairo.context, imgOriginal:image, faceRatio:FaceRatio) = {
	[wdMgn, htMgn] = [2, 2]
	[wdGuide, htGuide] = [2, 2]
	[wdImg, htImg, htTop, htFace] = [30, 40, 2, 30]
	//[wdImg, htImg, htTop, htFace] = [35, 45, 4, 34]
	htBottom = htImg - htTop - htFace
	ratioBase = faceRatio.bottom - faceRatio.top
	imgExtract = scope {
		yTop = imgOriginal.height * (faceRatio.top - ratioBase * htTop / htFace)
		yBottom = imgOriginal.height * (faceRatio.bottom + ratioBase * htBottom / htFace)
		height = yBottom - yTop
		width = height * wdImg / htImg
		xLeft = imgOriginal.width * faceRatio.center - width / 2
		[wdDotMgn, htDotMgn] = imgOriginal.height * ratioBase * [wdMgn, htMgn] / htFace
		imgOriginal.crop(xLeft - wdDotMgn, yTop - htDotMgn,
						width + wdDotMgn * 2, height + htDotMgn * 2)
	}
	pattern = cairo.pattern.create_for_surface(imgExtract)
	[wdImgEx, htImgEx] = [wdImg + wdMgn * 2,  htImg + htMgn * 2]
	y = htGuide
	cr.set_line_width(.1)
	cr.set_source_rgb(.2, .2, 1)
	cr.move_to(0, y + htMgn)
	cr.line_to(wdPaper, y + htMgn)
	cr.move_to(0, y + htImgEx - htMgn)
	cr.line_to(wdPaper, y + htImgEx - htMgn)
	repeat(3) {|i|
		x = wdGuide + i * (wdImgEx + wdGuide)
		cr.move_to(x + wdMgn, y - htGuide)
		cr.line_to(x + wdMgn, y + htImgEx + htGuide)
		cr.move_to(x + wdImgEx - wdMgn, y - htGuide)
		cr.line_to(x + wdImgEx - wdMgn, y + htImgEx + htGuide)
		cr.stroke()
		cr.save {
			scale = htImgEx / imgExtract.height
			cr.translate(x, y)
			cr.scale(scale, scale)
			cr.set_source(pattern)
			cr.paint()
		}
	}
}

//-----------------------------------------------------------------------------
// ProductViewer
//-----------------------------------------------------------------------------
ProductViewer = class(wx.Panel) {
	// constructor
	__init__(parent:wx.Window) = {|parent|
		this.imgScreen = nil
		this.imgOriginal = nil
		this.faceRatio = nil
		// bind event handlers
		this.Bind(wx.EVT_ERASE_BACKGROUND) { /* nothing to do */ }
		this.Bind(wx.EVT_SIZE) {|event| this.OnSize(event)}
		this.Bind(wx.EVT_PAINT) {|event| this.OnPaint(event)}
	}
	// methods
	SetExtractedImage(imgOriginal:image, faceRatio:FaceRatio) = {
		this.imgOriginal = imgOriginal
		this.faceRatio = faceRatio
	}
	UpdateContent() = {
		!this.imgOriginal && return
		[wdClient, htClient] = this.GetClientSizeWH()
		this.imgScreen.fill(`gray)
		wdImg = 600, htImg = int(wdImg * htPaper / wdPaper)
		img = image(`rgba, wdImg, htImg, `white)
		img.cairo {|cr|
			scale = wdImg / wdPaper
			cr.scale(scale, scale)
			ComposeProduct(cr, this.imgOriginal, this.faceRatio)
		}
		//[x, y] = [(wdClient - img.width) / 2, (htClient - img.height) / 2]
		[x, y] = [0, 0]
		this.imgScreen.paste(x, y, img)
		this.Refresh()
	}
	// event handler
	OnSize(event:wx.SizeEvent) = {
		[wdClient, htClient] = this.GetClientSizeWH()
		this.imgScreen = image(`rgb, wdClient, htClient, `gray)
		this.UpdateContent()
		this.Refresh()
		event.Skip()
	}
	OnPaint(event:wx.PaintEvent) = {
		dc = wx.PaintDC(this)
		dc.DrawBitmap(this.imgScreen, 0, 0, false)
		dc = nil
	}
}

//-----------------------------------------------------------------------------
// Extractor
//-----------------------------------------------------------------------------
Extractor = class(wx.Panel) {
	RatioCursor = .3
	// constructor
	__init__(parent:wx.Window, productViewer:ProductViewer) = {|parent, size => wx.Size(200, 200)|
		this.productViewer = productViewer
		this.grabTgt = nil
		this.faceRatio = FaceRatio(.5, .1, .6)
		this.imgOriginal = image('face.jpg')
		this.imgScreen = nil
		this.rcImg = wx.Rect(0, 0, 0, 0)
		this.rcBox = wx.Rect(0, 0, 0, 0)
		this.penBox = wx.Pen(wx.Colour(255, 0, 0), 2, wx.SHORT_DASH)
		this.productViewer.SetExtractedImage(this.imgOriginal, this.faceRatio)
		// bind event handlers
		this.Bind(wx.EVT_ERASE_BACKGROUND) { /* nothing to do */ }
		this.Bind(wx.EVT_SIZE) {|event| this.OnSize(event)}
		this.Bind(wx.EVT_PAINT) {|event| this.OnPaint(event)}
		this.Bind(wx.EVT_LEFT_DOWN) {|event| this.OnLeftDown(event)}
		this.Bind(wx.EVT_LEFT_UP) {|event| this.OnLeftUp(event)}
		this.Bind(wx.EVT_MOTION) {|event| this.OnMotion(event)}
	}
	// methods
	SenseBox(pt:wx.Point) = {
		[wdSense, htSense] = [6, 6]
		wx.Rect((this.rcBox.GetLeft() + this.rcBox.GetRight() - wdSense) / 2, this.rcBox.GetTop(),
			wdSense, this.rcBox.height).Contains(pt) && return(`center)
		wx.Rect(this.rcBox.GetLeft(), this.rcBox.GetTop() - htSense / 2,
			this.rcBox.width, htSense).Contains(pt) && return(`top)
		wx.Rect(this.rcBox.GetLeft(), this.rcBox.GetBottom() - htSense / 2,
			this.rcBox.width, htSense).Contains(pt) && return(`bottom)
		nil
	}
	UpdateImageScreen() = {
		[wdClient, htClient] = this.GetClientSizeWH()
		this.imgScreen = image(`rgb, wdClient, htClient, `gray)
		img = this.imgOriginal.thumbnail(wdClient, htClient)
		[x, y] = [(wdClient - img.width) / 2, (htClient - img.height) / 2]
		this.imgScreen.paste(x, y, img)
		this.rcImg = wx.Rect(x, y, img.width, img.height)
		this.UpdateRectBox()
	}
	UpdateRectBox() = {
		xLeft = this.rcImg.x + this.rcImg.width * (this.faceRatio.center - RatioCursor / 2)
		xRight = this.rcImg.x + this.rcImg.width * (this.faceRatio.center + RatioCursor / 2)
		yTop = this.rcImg.y + this.rcImg.height * this.faceRatio.top
		yBottom = this.rcImg.y + this.rcImg.height * this.faceRatio.bottom
		this.rcBox = wx.Rect(xLeft, yTop, xRight - xLeft, yBottom - yTop)
	}
	// event handler
	OnSize(event:wx.SizeEvent) = {
		this.UpdateImageScreen()
		this.Refresh()
		event.Skip()
	}
	OnPaint(event:wx.PaintEvent) = {
		dc = wx.PaintDC(this)
		!this.imgScreen && this.UpdateImageScreen()
		dc.DrawBitmap(this.imgScreen, 0, 0, false)
		dc.SetBrush(wx.TRANSPARENT_BRUSH)
		dc.SetPen(this.penBox)
		xCenter = (this.rcBox.GetLeft() + this.rcBox.GetRight()) / 2
		dc.DrawLine(xCenter, this.rcBox.GetTop(), xCenter, this.rcBox.GetBottom())
		dc.DrawLine(this.rcBox.GetLeft(), this.rcBox.GetTop(),
						this.rcBox.GetRight(), this.rcBox.GetTop())
		dc.DrawLine(this.rcBox.GetLeft(), this.rcBox.GetBottom(),
						this.rcBox.GetRight(), this.rcBox.GetBottom())
		dc = nil
	}
	OnLeftDown(event:wx.MouseEvent) = {
		if (!this.grabTgt) {
			this.grabTgt = this.SenseBox(event.GetPosition())
		}
		!this.HasCapture() && this.CaptureMouse()
	}
	OnLeftUp(event:wx.MouseEvent) = {
		if (this.grabTgt) {
			this.grabTgt = nil
			this.productViewer.SetExtractedImage(this.imgOriginal, this.faceRatio)
			this.productViewer.UpdateContent()
		}
		this.HasCapture() && this.ReleaseMouse()
	}
	OnMotion(event:wx.MouseEvent) = {
		rtn = this.SenseBox(event.GetPosition())
		if (rtn == `center) {
			this.SetCursor(wx.StockCursor(wx.CURSOR_SIZEWE))
		} elsif (rtn in [`top, `bottom]) {
			this.SetCursor(wx.StockCursor(wx.CURSOR_SIZENS))
		} else {
			this.SetCursor(wx.StockCursor(wx.CURSOR_ARROW))
		}
		pt = event.GetPosition()
		if (this.grabTgt == `center) {
			this.faceRatio.center = (pt.x - this.rcImg.x) / this.rcImg.width
			if (this.faceRatio.center > 1 - RatioCursor / 2) {
				this.faceRatio.center = 1 - RatioCursor / 2
			} elsif (this.faceRatio.center < RatioCursor / 2) {
				this.faceRatio.center = RatioCursor / 2
			}
			this.UpdateRectBox()
			this.Refresh()
		} elsif (this.grabTgt == `top) {
			this.faceRatio.top = (pt.y - this.rcImg.y) / this.rcImg.height
			if (this.faceRatio.top < 0) {
				this.faceRatio.top = 0
			} elsif (this.faceRatio.top > this.faceRatio.bottom - .1) {
				this.faceRatio.top = this.faceRatio.bottom - .1
			}
			this.UpdateRectBox()
			this.Refresh()
		} elsif (this.grabTgt == `bottom) {
			this.faceRatio.bottom = (pt.y - this.rcImg.y) / this.rcImg.height
			if (this.faceRatio.bottom > 1) {
				this.faceRatio.bottom = 1
			} elsif (this.faceRatio.bottom < this.faceRatio.top + .1) {
				this.faceRatio.bottom = this.faceRatio.top + .1
			}
			this.UpdateRectBox()
			this.Refresh()
		}
	}
}

//-----------------------------------------------------------------------------
// Panel
//-----------------------------------------------------------------------------
Panel = class(wx.Panel) {
	__init__(parent:wx.Window) = {|parent|
		outerBox = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(outerBox)
		vbox = wx.BoxSizer(wx.VERTICAL)
		outerBox.Add(vbox, wx.SizerFlags(1).Expand())
		wx.StaticLine(this, wx.ID_ANY, style => wx.HORIZONTAL) {|ctrl|
			vbox.Add(ctrl, wx.SizerFlags().Expand())
		}
		productViewer = ProductViewer(this)
		extractor = Extractor(this, productViewer)
		wx.BoxSizer(wx.HORIZONTAL) {|hbox|
			vbox.Add(hbox, wx.SizerFlags().Expand())
			hbox.Add(extractor, wx.SizerFlags())
		}
		wx.StaticLine(this, wx.ID_ANY, style => wx.HORIZONTAL) {|ctrl|
			vbox.Add(ctrl, wx.SizerFlags().Expand())
		}
		vbox.Add(productViewer, wx.SizerFlags(1).Expand())
	}
}

//-----------------------------------------------------------------------------
// Frame
//-----------------------------------------------------------------------------
Frame = class(wx.Frame) {
	__init__(title:string, pos:wx.Point => wx.DefaultPosition,
			size:wx.Size => wx.Size(800, 700)) = {|nil, wx.ID_ANY, title, pos, size|
		this.SetIcon(wx.IconFromXPMData(resource.sample_xpm))
		wx.MenuBar() {|menuBar|
			this.SetMenuBar(menuBar)
			wx.Menu() {|menu|
				menuBar.Append(menu, '&File')
				//menu.Append(wx.ID_ANY, 'Save')
				//menu.Append(wx.ID_ANY, 'Load')
				//menu.Append(wx.ID_ANY, 'Exit')
			}
		}
		Panel(this)
	}
}

//-----------------------------------------------------------------------------
// App
//-----------------------------------------------------------------------------
App = class(wx.App) {
	OnInit() = {
		frame = Frame('gurashot')
		frame.Show()
		true
	}
}

wx.IMPLEMENT_APP(App)
