#!/usr/bin/env gura
import(wx)
import(jpeg)
import(cairo)
import(resource)

//-----------------------------------------------------------------------------
// Global Variables
//-----------------------------------------------------------------------------
[wdMgn, htMgn] = [10.0, 10.0]
[wdPaper, htPaper] = [127.0 - wdMgn, 89.0 - wdMgn]

//-----------------------------------------------------------------------------
// App
//-----------------------------------------------------------------------------
App = class(wx.App) {
	OnInit() = {
		frame = Frame('gurashot')
		frame.Show()
		true
	}
}

//-----------------------------------------------------------------------------
// ExtractRatio
//-----------------------------------------------------------------------------
ExtractRatio = struct(left:number, right:number, top:number, bottom:number)

//-----------------------------------------------------------------------------
// ComposeProduct
//-----------------------------------------------------------------------------
ComposeProduct(cr:cairo.context, imgExtract:image) = {
	cairo.pattern.create_for_surface(imgExtract) {|pattern|
		cr.save {
			scale = 40.0 / imgExtract.height
			cr.translate(0, 0)
			cr.scale(scale, scale)
			cr.set_source(pattern)
			cr.paint()
		}
		cr.save {
			cr.translate(127.0 / 2, 10)
			scale = 50.0 / imgExtract.height
			cr.scale(scale, scale)
			cr.set_source(pattern)
			cr.paint()
		}
	}
}

//-----------------------------------------------------------------------------
// ProductViewer
//-----------------------------------------------------------------------------
ProductViewer = class(wx.Panel) {
	// constructor
	__init__(parent:wx.Window) = {|parent|
		this.imgScreen = nil
		this.imgExtract = nil
		// bind event handlers
		this.Bind(wx.EVT_ERASE_BACKGROUND) { /* nothing to do */ }
		this.Bind(wx.EVT_SIZE) {|event| this.OnSize(event)}
		this.Bind(wx.EVT_PAINT) {|event| this.OnPaint(event)}
	}
	// methods
	SetExtractedImage(imgExtract:image) = {
		this.imgExtract = imgExtract
	}
	UpdateContent() = {
		!this.imgExtract && return
		[wdClient, htClient] = this.GetClientSizeWH()
		this.imgScreen.fill(`gray)
		wdImg = 600, htImg = int(wdImg * htPaper / wdPaper)
		img = image(`rgba, wdImg, htImg, `white)
		img.cairo {|cr|
			scale = wdImg / wdPaper
			cr.scale(scale, scale)
			ComposeProduct(cr, this.imgExtract)
		}
		//[x, y] = [(wdClient - img.width) / 2, (htClient - img.height) / 2]
		[x, y] = [0, 0]
		this.imgScreen.paste(x, y, img)
		this.Refresh()
	}
	// event handler
	OnSize(event:wx.SizeEvent) = {
		[wdClient, htClient] = this.GetClientSizeWH()
		this.imgScreen = image(`rgb, wdClient, htClient, `gray)
		this.UpdateContent()
		this.Refresh()
		event.Skip()
	}
	OnPaint(event:wx.PaintEvent) = {
		dc = wx.PaintDC(this)
		dc.DrawBitmap(this.imgScreen, 0, 0, false)
		dc = nil
	}
}

//-----------------------------------------------------------------------------
// Extractor
//-----------------------------------------------------------------------------
Extractor = class(wx.Panel) {
	// constructor
	__init__(parent:wx.Window, productViewer:ProductViewer) = {|parent, size => wx.Size(200, 200)|
		this.productViewer = productViewer
		this.grabTgt = nil
		this.extractRatio = ExtractRatio(.2, .8, .1, .6)
		this.imgOriginal = image('face.jpg')
		this.imgScreen = nil
		this.rcImg = wx.Rect(0, 0, 0, 0)
		this.rcBox = wx.Rect(0, 0, 0, 0)
		this.penBox = wx.Pen(wx.Colour(255, 0, 0), 2, wx.SHORT_DASH)
		this.productViewer.SetExtractedImage(this.DoExtract())
		// bind event handlers
		this.Bind(wx.EVT_ERASE_BACKGROUND) { /* nothing to do */ }
		this.Bind(wx.EVT_SIZE) {|event| this.OnSize(event)}
		this.Bind(wx.EVT_PAINT) {|event| this.OnPaint(event)}
		this.Bind(wx.EVT_LEFT_DOWN) {|event| this.OnLeftDown(event)}
		this.Bind(wx.EVT_LEFT_UP) {|event| this.OnLeftUp(event)}
		this.Bind(wx.EVT_MOTION) {|event| this.OnMotion(event)}
	}
	// methods
	DoExtract() = {
		xLeft = this.imgOriginal.width * this.extractRatio.left
		xRight = this.imgOriginal.width * this.extractRatio.right
		yTop = this.imgOriginal.height * this.extractRatio.top
		yBottom = this.imgOriginal.height * this.extractRatio.bottom
		this.imgOriginal.crop(xLeft, yTop, xRight - xLeft, yBottom - yTop)
	}
	SenseBox(pt:wx.Point) = {
		[wdSense, htSense] = [6, 6]
		wx.Rect(this.rcBox.GetLeft() - wdSense / 2, this.rcBox.GetTop(),
			wdSense, this.rcBox.height).Contains(pt) && return(`left)
		wx.Rect(this.rcBox.GetRight() - wdSense / 2, this.rcBox.GetTop(),
			wdSense, this.rcBox.height).Contains(pt) && return(`right)
		wx.Rect(this.rcBox.GetLeft(), this.rcBox.GetTop() - htSense / 2,
			this.rcBox.width, htSense).Contains(pt) && return(`top)
		wx.Rect(this.rcBox.GetLeft(), this.rcBox.GetBottom() - htSense / 2,
			this.rcBox.width, htSense).Contains(pt) && return(`bottom)
		nil
	}
	UpdateImageScreen() = {
		[wdClient, htClient] = this.GetClientSizeWH()
		this.imgScreen = image(`rgb, wdClient, htClient, `gray)
		img = this.imgOriginal.thumbnail(wdClient, htClient)
		[x, y] = [(wdClient - img.width) / 2, (htClient - img.height) / 2]
		this.imgScreen.paste(x, y, img)
		this.rcImg = wx.Rect(x, y, img.width, img.height)
		this.UpdateRectBox()
	}
	UpdateRectBox() = {
		xLeft = this.rcImg.x + this.rcImg.width * this.extractRatio.left
		xRight = this.rcImg.x + this.rcImg.width * this.extractRatio.right
		yTop = this.rcImg.y + this.rcImg.height * this.extractRatio.top
		yBottom = this.rcImg.y + this.rcImg.height * this.extractRatio.bottom
		this.rcBox = wx.Rect(xLeft, yTop, xRight - xLeft, yBottom - yTop)
	}
	// event handler
	OnSize(event:wx.SizeEvent) = {
		this.UpdateImageScreen()
		this.Refresh()
		event.Skip()
	}
	OnPaint(event:wx.PaintEvent) = {
		dc = wx.PaintDC(this)
		!this.imgScreen && this.UpdateImageScreen()
		dc.DrawBitmap(this.imgScreen, 0, 0, false)
		dc.SetBrush(wx.TRANSPARENT_BRUSH)
		dc.SetPen(this.penBox)
		dc.DrawRectangleRect(this.rcBox)
		dc = nil
	}
	OnLeftDown(event:wx.MouseEvent) = {
		if (!this.grabTgt) {
			this.grabTgt = this.SenseBox(event.GetPosition())
		}
		!this.HasCapture() && this.CaptureMouse()
	}
	OnLeftUp(event:wx.MouseEvent) = {
		if (this.grabTgt) {
			this.grabTgt = nil
			this.productViewer.SetExtractedImage(this.DoExtract())
			this.productViewer.UpdateContent()
		}
		this.HasCapture() && this.ReleaseMouse()
	}
	OnMotion(event:wx.MouseEvent) = {
		rtn = this.SenseBox(event.GetPosition())
		if (rtn in [`left, `right]) {
			this.SetCursor(wx.StockCursor(wx.CURSOR_SIZEWE))
		} elsif (rtn in [`top, `bottom]) {
			this.SetCursor(wx.StockCursor(wx.CURSOR_SIZENS))
		} else {
			this.SetCursor(wx.StockCursor(wx.CURSOR_ARROW))
		}
		pt = event.GetPosition()
		if (this.grabTgt == `left) {
			this.extractRatio.left = (pt.x - this.rcImg.x) / this.rcImg.width
			if (this.extractRatio.left < 0) {
				this.extractRatio.left = 0
			} elsif (this.extractRatio.left > this.extractRatio.right - .1) {
				this.extractRatio.left = this.extractRatio.right - .1
			}
			this.UpdateRectBox()
			this.Refresh()
		} elsif (this.grabTgt == `right) {
			this.extractRatio.right = (pt.x - this.rcImg.x) / this.rcImg.width
			if (this.extractRatio.right > 1) {
				this.extractRatio.right = 1
			} elsif (this.extractRatio.right < this.extractRatio.left + .1) {
				this.extractRatio.right = this.extractRatio.left + .1
			}
			this.UpdateRectBox()
			this.Refresh()
		} elsif (this.grabTgt == `top) {
			this.extractRatio.top = (pt.y - this.rcImg.y) / this.rcImg.height
			if (this.extractRatio.top < 0) {
				this.extractRatio.top = 0
			} elsif (this.extractRatio.top > this.extractRatio.bottom - .1) {
				this.extractRatio.top = this.extractRatio.bottom - .1
			}
			this.UpdateRectBox()
			this.Refresh()
		} elsif (this.grabTgt == `bottom) {
			this.extractRatio.bottom = (pt.y - this.rcImg.y) / this.rcImg.height
			if (this.extractRatio.bottom > 1) {
				this.extractRatio.bottom = 1
			} elsif (this.extractRatio.bottom < this.extractRatio.top + .1) {
				this.extractRatio.bottom = this.extractRatio.top + .1
			}
			this.UpdateRectBox()
			this.Refresh()
		}
	}
}

//-----------------------------------------------------------------------------
// Panel
//-----------------------------------------------------------------------------
Panel = class(wx.Panel) {
	__init__(parent:wx.Window) = {|parent|
		outerBox = wx.BoxSizer(wx.VERTICAL)
		this.SetSizer(outerBox)
		vbox = wx.BoxSizer(wx.VERTICAL)
		outerBox.Add(vbox, wx.SizerFlags(1).Expand())
		wx.StaticLine(this, wx.ID_ANY, style => wx.HORIZONTAL) {|ctrl|
			vbox.Add(ctrl, wx.SizerFlags().Expand())
		}
		productViewer = ProductViewer(this)
		extractor = Extractor(this, productViewer)
		wx.BoxSizer(wx.HORIZONTAL) {|hbox|
			vbox.Add(hbox, wx.SizerFlags().Expand())
			hbox.Add(extractor, wx.SizerFlags())
		}
		wx.StaticLine(this, wx.ID_ANY, style => wx.HORIZONTAL) {|ctrl|
			vbox.Add(ctrl, wx.SizerFlags().Expand())
		}
		vbox.Add(productViewer, wx.SizerFlags(1).Expand())
	}
}

//-----------------------------------------------------------------------------
// Frame
//-----------------------------------------------------------------------------
Frame = class(wx.Frame) {
	__init__(title:string, pos:wx.Point => wx.DefaultPosition,
			size:wx.Size => wx.Size(800, 600)) = {|nil, wx.ID_ANY, title, pos, size|
		this.SetIcon(wx.IconFromXPMData(resource.sample_xpm))
		wx.MenuBar() {|menuBar|
			this.SetMenuBar(menuBar)
			wx.Menu() {|menu|
				menuBar.Append(menu, '&File')
				//menu.Append(wx.ID_ANY, 'Save')
				//menu.Append(wx.ID_ANY, 'Load')
				//menu.Append(wx.ID_ANY, 'Exit')
			}
		}
		Panel(this)
	}
}

wx.IMPLEMENT_APP(App)
