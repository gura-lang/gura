#!/usr/bin/env gura
import(conio)
import(markdown):binary:mixin_type {*}

MakeAnchorNameDef(indices[]:number) = 'anchor-' + format('%d', indices).join('-')

Renderer@html = class {
	tagsInline = [
		'a', 'b', 'code', 'em', 'img', 'span', 'strike', 'strong', 'u'
	]
	__init__(out:stream:w, captionIndex:boolean) = {
		this.out = out
		this.indices = dim(6) {0}
		this.captionIndex = captionIndex
	}
	Render(doc:document):void = {
		this.RenderItem(doc.root)
	}
	RenderItem(item:item):map:void = {
		!this.OnRenderItemPre(item) && return
		if (item.type == `root) {
			this.RenderItem(item.children)
		} elsif (item.type == `h1) {
			this.out.print('<h1>')
			if (this.captionIndex) {
				this.indices[0] += 1
				this.indices[1..5] = 0
				this.out.printf('<span class="caption-index-1">%d</span>',
								this.indices[0])
			}
			this.out.printf('<a name="%s">', this.MakeAnchorName([this.indices[0]]))
			this.out.print('</a>')
			this.RenderItem(item.children)
			this.out.print('</h1>\n')
		} elsif (item.type == `h2) {
			this.out.print('<h2>')
			if (this.captionIndex) {
				this.indices[1] += 1
				this.indices[2..5] = 0
				this.out.printf('<span class="caption-index-2">%d.%d</span>',
								this.indices[0..1]*)
			}
			this.out.printf('<a name="%s">', this.MakeAnchorName(this.indices[0..1]))
			this.out.print('</a>')
			this.RenderItem(item.children)
			this.out.print('</h2>\n')
		} elsif (item.type == `h3) {
			this.out.print('<h3>')
			if (this.captionIndex) {
				this.indices[2] += 1
				this.indices[3..5] = 0
				this.out.printf('<span class="caption-index-3">%d.%d.%d</span>',
								this.indices[0..2]*)
			}
			this.out.printf('<a name="%s">', this.MakeAnchorName(this.indices[0..2]))
			this.out.print('</a>')
			this.RenderItem(item.children)
			this.out.print('</h3>\n')
		} elsif (item.type == `h4) {
			this.out.print('<h4>')
			if (this.captionIndex) {
				this.indices[3] += 1
				this.indices[4..5] = 0
				this.out.printf('<span class="caption-index-4">%d.%d.%d.%d</span>',
								this.indices[0..3]*)
			}
			this.out.printf('<a name="%s">', this.MakeAnchorName(this.indices[0..3]))
			this.out.print('</a>')
			this.RenderItem(item.children)
			this.out.print('</h4>\n')
		} elsif (item.type == `h5) {
			this.out.print('<h5>')
			if (this.captionIndex) {
				this.indices[4] += 1
				this.indices[5] = 0
				this.out.printf('<span class="caption-index-5">%d.%d.%d.%d.%d</span>',
								this.indices[0..4]*)
			}
			this.out.printf('<a name="%s">', this.MakeAnchorName(this.indices[0..4]))
			this.out.print('</a>')
			this.RenderItem(item.children)
			this.out.print('</h5>\n')
		} elsif (item.type == `h6) {
			this.out.print('<h6>')
			if (this.captionIndex) {
				this.indices[5] += 1
				this.out.printf('<span class="caption-index-6">%d.%d.%d.%d.%d.%d</span>',
								this.indices[0..5]*)
			}
			this.out.printf('<a name="%s">', this.MakeAnchorName(this.indices[0..5]))
			this.out.print('</a>')
			this.RenderItem(item.children)
			this.out.print('</h6>\n')
		} elsif (item.type == `p) {
			this.out.print('<p>\n')
			this.RenderItem(item.children)
			this.out.println()
			this.out.print('</p>\n')
		} elsif (item.type == `blockquote) {
			this.out.print('<blockquote>\n')
			this.RenderItem(item.children)
			this.out.println()
			this.out.print('</blockquote>\n')
		} elsif (item.type == `em) {
			this.out.print('<em>')
			this.RenderItem(item.children)
			this.out.print('</em>')
		} elsif (item.type == `strong) {
			this.out.print('<strong>')
			this.RenderItem(item.children)
			this.out.print('</strong>')
		} elsif (item.type == `strike) {
			this.out.print('<del>')
			this.RenderItem(item.children)
			this.out.print('</del>')
		} elsif (item.type == `codeblock) {
			this.out.print('<pre><code>')
			this.RenderItem(item.children)
			this.out.print('</code></pre>\n')
		} elsif (item.type == `ol) {
			this.out.print('<ol>\n')
			this.RenderItem(item.children)
			this.out.print('</ol>\n')
		} elsif (item.type == `ul) {
			this.out.print('<ul>\n')
			this.RenderItem(item.children)
			this.out.print('</ul>\n')
		} elsif (item.type == `li) {
			this.out.print('<li>')
			this.RenderItem(item.children)
			this.out.print('</li>\n')
		} elsif (item.type == `line) {
			this.RenderItem(item.children)
			this.out.print('\n')
		} elsif (item.type == `a) {
			this.out.print('<a')
			item.url && this.out.print(' href="', item.url.encodeuri(), '"')
			item.title && this.out.print(' title="', item.title.escapehtml(), '"')
			this.out.print('>')
			this.RenderItem(item.children)
			this.out.print('</a>')
		} elsif (item.type == `img) {
			this.out.print('<img')
			item.url && this.out.print(' src="', item.url.encodeuri(), '"')
			item.text && this.out.print(' alt="', item.text.escapehtml(), '"')
			item.title && this.out.print(' title="', item.title.escapehtml(), '"')
			this.out.print('>')
		} elsif (item.type == `text) {
			this.out.print(item.text.escapehtml())
		} elsif (item.type == `comment) {
			this.out.print(item.text)
		} elsif (item.type == `code) {
			this.out.print('<code>')
			this.out.print(item.text.escapehtml())
			this.out.print('</code>')
		} elsif (item.type == `entity) {
			this.out.print('&', item.text.escapehtml(), ';')
		} elsif (item.type == `tag) {
			inlineFlag = item.text in tagsInline
			this.out.print('<')
			this.out.print(item.text)
			item.attrs && this.out.print(' ', item.attrs)
			if (item.children) {
				this.out.print('>')
				!inlineFlag && this.out.println()
				item.children && this.RenderItem(item.children)
				this.out.print('</')
				this.out.print(item.text)
				this.out.print('>')
			} else {
				this.out.print(' />')
			}
			!inlineFlag && this.out.println()
		} elsif (item.type == `hr) {
			this.out.print('<hr />\n')
		} elsif (item.type == `br) {
			this.out.print('<br />\n')
		}
		this.OnRenderItemPost(item)
	}
	MakeAnchorName(indices[]:number) = MakeAnchorNameDef(indices)
	OnRenderItemPre(item:item) = true
	OnRenderItemPost(item:item):void = nil
}

Renderer@tex = class {
	__init__(out:stream:w, title:string, author:string, date:string) = {
		this.out = out
		this.title = title
		this.author = author
		this.date = date
		this.escapeTextFlag = true
	}
	Render(doc:document):void = {
		this.RenderItem(doc.root)
	}
	RenderItem(item:item):map:void = {
		if (item.type == `root) {
			_this = this
			this.out.print(R'''
			\documentclass{report}
			\title{${_this.title}}
			\author{${_this.author}}
			\date{${_this.date}}
			\usepackage{fancyvrb}

			\setlength{\textwidth}{145mm}
			\setlength{\oddsidemargin}{35mm}
			\addtolength{\oddsidemargin}{-1in}
			\setlength{\parindent}{0mm}

			\setlength{\textheight}{244mm}
			\setlength{\headheight}{0mm}
			\setlength{\headsep}{25mm}
			\setlength{\footskip}{15mm}
			\addtolength{\topmargin}{-1in}

			\begin{document}
			\maketitle
			\tableofcontents
			'''.embed())
			this.RenderItem(item.children)
			this.out.print(R'''
			\end{document}
			''')
		} elsif (item.type == `h1) {
			this.out.print(r'\chapter{')
			this.RenderItem(item.children)
			this.out.println(r'}')
		} elsif (item.type == `h2) {
			this.out.print(r'\section{')
			this.RenderItem(item.children)
			this.out.println(r'}')
		} elsif (item.type == `h3) {
			this.out.print(r'\subsection{')
			this.RenderItem(item.children)
			this.out.println(r'}')
		} elsif (item.type == `h4) {
			this.out.print(r'\subsubsection{')
			this.RenderItem(item.children)
			this.out.println(r'}')
		} elsif (item.type == `h5) {
			this.out.print(r'\paragraph{')
			this.RenderItem(item.children)
			this.out.println(r'}')
		} elsif (item.type == `h6) {
			this.out.print(r'\paragraph{')
			this.RenderItem(item.children)
			this.out.println(r'}')
		} elsif (item.type == `p) {
			this.RenderItem(item.children)
			this.out.println()
			this.out.println()
		} elsif (item.type == `blockquote) {
			this.out.println(r'\begin{quotation}')
			this.RenderItem(item.children)
			this.out.println(r'\end{quotation}')
		} elsif (item.type == `em) {
			this.out.print(r'{\it ')
			this.RenderItem(item.children)
			this.out.print(r'}')
		} elsif (item.type == `strong) {
			this.out.print(r'{\bf ')
			this.RenderItem(item.children)
			this.out.print(r'}')
		} elsif (item.type == `strike) {
			this.RenderItem(item.children)
		} elsif (item.type == `codeblock) {
			this.out.println(r'\begin{Verbatim}[frame=single,framesep=2mm,fontsize=\small]')
			this.escapeTextFlag = false
			this.RenderItem(item.children)
			this.escapeTextFlag = true
			this.out.println(r'\end{Verbatim}')
		} elsif (item.type == `ol) {
			this.out.println(r'\begin{enumerate}')
			this.RenderItem(item.children)
			this.out.println(r'\end{enumerate}')
		} elsif (item.type == `ul) {
			this.out.println(r'\begin{itemize}')
			this.RenderItem(item.children)
			this.out.println(r'\end{itemize}')
		} elsif (item.type == `li) {
			this.out.print(r'\item ')
			this.RenderItem(item.children)
		} elsif (item.type == `line) {
			this.RenderItem(item.children)
			this.out.println()
		} elsif (item.type == `a) {
			this.RenderItem(item.children)
		} elsif (item.type == `img) {
			
		} elsif (item.type == `text) {
			if (this.escapeTextFlag) {
				this.out.print(this.EscapeText(item.text))
			} else {
				this.out.print(item.text)
			}
		} elsif (item.type == `comment) {
			// nothing to do
		} elsif (item.type == `code) {
			this.out.print(r'{\tt ')
			this.out.print(this.EscapeText(item.text))
			this.out.print(r' }')
		} elsif (item.type == `entity) {
			if (item.text == 'hellip') {
				this.out.print(r'\textellipsis ')
			}
			//this.out.print(r'\u%d?', 169)
		} elsif (item.type == `tag) {
			tagName = item.text.lower()
			if (tagName == 'table') {
				this.RenderTable(item)
			} elsif (tagName == 'code') {
				this.out.print(r'{\tt ')
				item.children && this.RenderItem(item.children)
				this.out.print(r' }')
			} else {
				item.children && this.RenderItem(item.children)
			}
		} elsif (item.type == `hr) {
			
		} elsif (item.type == `br) {
			this.out.println(r'\newline')
		}
	}
	replaceTbl = [
		'\\', r'$\backslash$',
		'#', r'\#',
		'$', r'\$',
		'%', r'\%',
		'&', r'\&',
		'_', r'\_',
		'{', r'\{',
		'}', r'\}',
		'^', r'$\wedge$',
		'*', r'$\ast$'
	]
	EscapeText(text:string):static = text.replaces(replaceTbl)
	RenderTable(item:item) = {
		rows = item.children.each():xlist {|item|
			(item.type != `tag || item.text.lower() != 'tr') && continue
			headerFlag = false
			item.children.each():xlist {|item|
				(item.type != `tag) && continue
				tagName = item.text.lower()
				if (tagName == 'th') {
					headerFlag = true
				} elsif (tagName == 'td') {
					// nothing to do
				} else {
					continue
				}
				item
			}
		}
		alignChars = %{`left => 'l', `right => 'r', `center => 'c'}
		alignSpecifier = alignChars.get(rows[0]::align, 'l').join('|')
		this.out.print('\n\\vspace{3mm}\n\n')
		this.out.printf('\\begin{tabular}{|%s|} \\hline\n', alignSpecifier)
		rows.each {|cols|
			cols.each {|item, i|
				(i > 0) && this.out.print(' & ')
				if (item.text.lower() == 'th') {
					this.out.print(r'{\bf ')
					this.RenderItem(item)
					this.out.print(r' }')
				} else {
					this.RenderItem(item)
				}
			}
			this.out.println(r' \\ \hline')
		}
		this.out.println(r'\end{tabular}')
		this.out.print('\n\\vspace{3mm}\n\n')
	}
}

Renderer@rtf = class {
	__init__(out:stream:w) = {
		this.out = out
		this.quoteLevel = 0
		this.listLevel = 0
		this.indentPerQuote = 720
		this.indentPerList = 360
	}
	Render(doc:document):void = {
		this.RenderItem(doc.root)
	}
	RenderItem(item:item):map:void = {
		if (item.type == `root) {
			this.out.println(r'{\rtf')
			this.RenderItem(item.children)
			this.out.println('}')
		} elsif (item.type == `h1) {
			this.out.print(r'{\pard \ql \f0 \sa180 \li0 \fi0 \b \fs36 ')
			this.RenderItem(item.children)
			this.out.println(r'\par}')
		} elsif (item.type == `h2) {
			this.out.print(r'{\pard \ql \f0 \sa180 \li0 \fi0 \b \fs32 ')
			this.RenderItem(item.children)
			this.out.println(r'\par}')
		} elsif (item.type == `h3) {
			this.out.print(r'{\pard \ql \f0 \sa180 \li0 \fi0 \b \fs28 ')
			this.RenderItem(item.children)
			this.out.println(r'\par}')
		} elsif (item.type == `h4) {
			this.out.print(r'{\pard \ql \f0 \sa180 \li0 \fi0 \b \fs24 ')
			this.RenderItem(item.children)
			this.out.println(r'\par}')
		} elsif (item.type == `h5) {
			this.out.print(r'{\pard \ql \f0 \sa180 \li0 \fi0 \b \fs20 ')
			this.RenderItem(item.children)
			this.out.println(r'\par}')
		} elsif (item.type == `h6) {
			this.out.print(r'{\pard \ql \f0 \sa180 \li0 \fi0 \b \fs16 ')
			this.RenderItem(item.children)
			this.out.println(r'\par}')
		} elsif (item.type == `p) {
			this.out.printf(r'{\pard \ql \f0 \sa180 \li%d \fi0 ',
									this.quoteLevel * this.indentPerQuote)
			this.RenderItem(item.children)
			this.out.println()
			this.out.println(r'\par}')
		} elsif (item.type == `blockquote) {
			this.quoteLevel += 1
			this.RenderItem(item.children)
			this.out.println()
			this.quoteLevel -= 1
		} elsif (item.type == `em) {
			this.out.print(r'{\i ')
			this.RenderItem(item.children)
			this.out.print(r'}')
		} elsif (item.type == `strong) {
			this.out.print(r'{\b ')
			this.RenderItem(item.children)
			this.out.print(r'}')
		} elsif (item.type == `strike) {
			this.out.print(r'{\strike ')
			this.RenderItem(item.children)
			this.out.print(r'}')
		} elsif (item.type == `codeblock) {
			this.out.print(r'{\pard \ql \f0 \sa180 \li0 \fi0 \f1 ')
			item.children.each {|itemChild, i|
				(i > 0) && this.out.println(r'\line')
				this.RenderItem(itemChild.children)
			}
			this.out.println(r'\par}')
		} elsif (item.type == `ol) {
			this.RenderListItem(item)
		} elsif (item.type == `ul) {
			this.RenderListItem(item)
		} elsif (item.type == `li) {
			// nothing to do here
		} elsif (item.type == `line) {
			this.RenderItem(item.children)
			this.out.println(r'\line')
		} elsif (item.type == `a) {
			this.out.println(r'{\field{\*\fldinst{HYPERLINK "%s"}}{\fldrslt{\ul' % this.EscapeText(item.url))
			this.RenderItem(item.children)
			this.out.println(r'}}}')
		} elsif (item.type == `img) {
			//this.out.print('<img')
			//item.url && this.out.print(' src="', item.url.encodeuri(), '"')
			//item.text && this.out.print(' alt="', item.text.escapehtml(), '"')
			//item.title && this.out.print(' title="', item.title.escapehtml(), '"')
			//this.out.print('>')
		} elsif (item.type == `text) {
			this.out.print(this.EscapeText(item.text))
		} elsif (item.type == `comment) {
			this.out.print(item.text)
		} elsif (item.type == `code) {
			this.out.print(r'{\f1 ')
			this.out.print(this.EscapeText(item.text))
			this.out.print(r'}')
		} elsif (item.type == `entity) {
			this.out.printf(r'\u%d?', 169)
		} elsif (item.type == `tag) {
			
		} elsif (item.type == `hr) {
			this.out.println(r'{\pard \qc \f0 \sa180 \li0 \fi0 \emdash\emdash\emdash\emdash\emdash\par}')
		} elsif (item.type == `br) {
			this.out.println(r'\line')
		}
	}
	EscapeText(text:string):static = {
		text.replace('\\', '\\\\').replace('{', r'\{').replace('}', r'\}')
	}
	RenderListItem(item:item) = {
		parRenderFlag = false
		this.listLevel += 1
		item.children.each {|itemChild, i|
			(i > 0 && !parRenderFlag) && this.out.println(r'\par}')
			if (item.type == `ul) {
				this.out.print(r'{\pard \ql \f0 \sa0 \li%d \fi-360 \bullet \tx360\tab ' % \
							[this.indentPerList * this.listLevel])
			} else { // item.type == `ol
				this.out.print(r'{\pard \ql \f0 \sa0 \li%d \fi-360 %d. \tx360\tab ' % \
							[this.indentPerList * this.listLevel, i + 1])
			}
			parRenderFlag = false
			itemChild.children.each {|itemDescend, i|
				if (itemDescend.type == `ol || itemDescend.type == `ul) {
					this.out.println(r'\sa180\par}')
					parRenderFlag = true
				}
				this.RenderItem(itemDescend)
			}
			
		}
		!parRenderFlag && this.out.println(r'\sa180\par}')
		this.listLevel -= 1
	}
}

Renderer@console = class {
	__init__(colorFlag:boolean => true) = {
		[this.wdScreen, this.htScreen] = conio.getwinsize()
		if (this.wdScreen == 0) {
			this.wdScreen = 80
		}
		this.wdScreen -= 1
		if (this.htScreen == 0) {
			this.htScreen = 25
		}
		this.indexStack = []
		if (colorFlag) {
			this.setcolor = conio.setcolor
		} else {
			this.setcolor(color:color) {block} = block()
		}
	}
	Render(doc:document):void = {
		this.RenderItem(doc.root)
	}
	RenderItem(item:item):map:void = {
		if (item.type == `root) {
			this.RenderItem(item.children)
		} elsif (item.type == `h1) {
			println()
			this.setcolor(`green) {
				this.RenderItem(item.children)
			}
			println()
		} elsif (item.type == `h2) {
			println()
			this.setcolor(`green) {
				this.RenderItem(item.children)
			}
			println()
		} elsif (item.type == `h3) {
			println()
			this.setcolor(`green) {
				this.RenderItem(item.children)
			}
			println()
		} elsif (item.type == `h4) {
			println()
			this.setcolor(`green) {
				this.RenderItem(item.children)
			}
			println()
		} elsif (item.type == `h5) {
			println()
			this.setcolor(`green) {
				this.RenderItem(item.children)
			}
			println()
		} elsif (item.type == `h6) {
			println()
			this.setcolor(`green) {
				this.RenderItem(item.children)
			}
			println()
		} elsif (item.type == `p) {
			println()
			this.RenderItem(item.children)
			println()
		} elsif (item.type == `blockquote) {
			println()
			this.RenderItem(item.children)
			println()
		} elsif (item.type == `em) {
			this.setcolor(`red) {
				this.RenderItem(item.children)
			}
		} elsif (item.type == `strong) {
			this.setcolor(`red) {
				this.RenderItem(item.children)
			}
		} elsif (item.type == `strike) {
			this.setcolor(`gray) {
				this.RenderItem(item.children)
			}
		} elsif (item.type == `codeblock) {
			println()
			this.setcolor(`blue) {
				this.RenderItem(item.children)
			}
		} elsif (item.type == `ol) {
			println()
			this.indexStack.add(0)
			this.RenderItem(item.children)
			this.indexStack.erase(-1)
		} elsif (item.type == `ul) {
			println()
			this.indexStack.add(nil)
			this.RenderItem(item.children)
			this.indexStack.erase(-1)
		} elsif (item.type == `li) {
			(this.indexStack.len() > 0) && print('  ' * (this.indexStack.len() - 1))
			index = this.indexStack[-1]
			if (index) {
				printf('%d. ', index + 1)
				this.indexStack[-1] += 1
			} else {
				print('- ')
			}
			this.RenderItem(item.children)
			println()
		} elsif (item.type == `line) {
			print('|   ')
			this.RenderItem(item.children)
			println()
		} elsif (item.type == `a) {
			this.RenderItem(item.children)
		} elsif (item.type == `img) {
			print('[', item.text, ']')
		} elsif (item.type == `text) {
			print(item.text)
		} elsif (item.type == `comment) {
			print(item.text)
		} elsif (item.type == `code) {
			this.setcolor(`blue) {
				print(item.text)
			}
		} elsif (item.type == `entity) {
			this.setcolor(`black, `white) {
				print(item.text.escapehtml())
			}
		} elsif (item.type == `tag) {
			if (item.text.lower() == 'table') {
				this.RenderTable(item)
			} else {
				item.children && this.RenderItem(item.children)
			}
		} elsif (item.type == `hr) {
			println('--------')
		} elsif (item.type == `br) {
			println()
		}
	}
	RenderTable(item:item) = {
		rows = item.children.each():xlist {|item|
			(item.type != `tag || item.text.lower() != 'tr') && continue
			headerFlag = false
			cols = item.children.each():xlist {|item|
				(item.type != `tag) && continue
				tagName = item.text.lower()
				if (tagName == 'th') {
					headerFlag = true
				} elsif (tagName == 'td') {
					// nothing to do
				} else {
					continue
				}
				TableCol(JoinText(item), item.align)
			}
			TableRow(headerFlag, cols)
		}
		nCols = rows:*cols:*len().max()
		rows::cols = rows:*cols::align(nCols, TableCol('', `left)):list
		widths = repeat(nCols):list {|iCol|
			rows:*cols:*get(iCol):*text:*len().max()
		}
		wdAvailable = this.wdScreen + 1 - nCols // subtract width for vertical bar
		wdThreshold = int(wdAvailable / nCols)
		wdSum = widths.sum()
		if (wdSum > wdAvailable) {
			widthsShort = widths.filter(widths < wdThreshold)
			widthsShortSum = widthsShort.sum() || 0
			widthsLongSum = wdSum - widthsShortSum
			wdRest = wdAvailable - widthsShortSum
			if (wdRest < 0) {
				widthsWk = int(widths * wdAvailable / wdSum)
			} else {
				widthsWk = widths.each():list {|width|
					if (width < wdThreshold) {
						width
					} else {
						width * wdRest / widthsLongSum
					}
				}
			}
			if ((widthsWk > 0).and()) {
				widths = widthsWk
			}
		}
		separator = ('-' * widths).join('+')
		rows.each {|row|
			textsFolded = row.cols:*text.each():list {|text, i|
				text.split('\n'):*foldw(widths[i]).flat().each()
			}
			while ((texts = textsFolded:*next()).or()) {
				//println(format('%-*s', widths, texts || '').join('|'))
				println(this.MakeAlign(texts || '', widths, row.cols:*align).join('|'))
			}
			row.headerFlag && println(separator)
		}
	}
	MakeAlign(str:string, width:number, align:symbol):map:static = {
		if (align == `left) {
			str.align(width):left
		} elsif (align == `center) {
			str.align(width):center
		} elsif (align == `right) {
			str.align(width):right
		} else {	
			str.align(width):left
		}
	}
}


TableCol = struct(text:string, align:symbol)
TableRow = struct(headerFlag:boolean, cols[]:TableCol)

JoinText(item:item):map = {
	if (item.children) {
		JoinText(item.children).join()
	} else {
		item.text
	}
}

Renderer@toc = class {
	__init__(handler:function) = {
		this.handler = handler
		this.text = nil
		this.indices = dim(6) {0}
	}
	Render(doc:document):void = {
		this.RenderItem(doc.root)
	}
	RenderItem(item:item):map:void = {
		if (item.type == `root) {
			this.RenderItem(item.children)
		} elsif (item.type == `h1) {
			this.indices[0] += 1
			this.indices[1..5] = 0
			this.text = ''
			this.RenderItem(item.children)
			this.handler(0, this.indices, this.MakeAnchorName([this.indices[0]]), this.text)
			this.text = nil
		} elsif (item.type == `h2) {
			this.indices[1] += 1
			this.indices[2..5] = 0
			this.text = ''
			this.RenderItem(item.children)
			this.handler(1, this.indices, this.MakeAnchorName(this.indices[0..1]), this.text)
			this.text = nil
		} elsif (item.type == `h3) {
			this.indices[2] += 1
			this.indices[3..5] = 0
			this.text = ''
			this.RenderItem(item.children)
			this.handler(2, this.indices, this.MakeAnchorName(this.indices[0..2]), this.text)
			this.text = nil
		} elsif (item.type == `h4) {
			this.indices[3] += 1
			this.indices[4..5] = 0
			this.text = ''
			this.RenderItem(item.children)
			this.handler(3, this.indices, this.MakeAnchorName(this.indices[0..3]), this.text)
			this.text = nil
		} elsif (item.type == `h5) {
			this.indices[4] += 1
			this.indices[5] = 0
			this.text = ''
			this.RenderItem(item.children)
			this.handler(4, this.indices, this.MakeAnchorName(this.indices[0..4]), this.text)
			this.text = nil
		} elsif (item.type == `h6) {
			this.indices[5] += 1
			this.text = ''
			this.RenderItem(item.children)
			this.handler(5, this.indices, this.MakeAnchorName(this.indices[0..5]), this.text)
			this.text = nil
		} elsif (item.type == `p) {
			// nothing to do
		} elsif (item.type == `blockquote) {
			// nothing to do
		} elsif (item.type == `em) {
			this.RenderItem(item.children)
		} elsif (item.type == `strong) {
			this.RenderItem(item.children)
		} elsif (item.type == `strike) {
			//this.RenderItem(item.children)
		} elsif (item.type == `codeblock) {
			// nothing to do
		} elsif (item.type == `ol) {
			// nothing to do
		} elsif (item.type == `ul) {
			// nothing to do
		} elsif (item.type == `li) {
			// nothing to do
		} elsif (item.type == `line) {
			// nothing to do
		} elsif (item.type == `a) {
			this.RenderItem(item.children)
		} elsif (item.type == `img) {
			// nothing to do
		} elsif (item.type == `text) {
			if (this.text) {
				this.text += item.text
			}
		} elsif (item.type == `comment) {
			// nothing to do
		} elsif (item.type == `code) {
			if (this.text) {
				this.text += item.text
			}
		} elsif (item.type == `entity) {
			// nothing to do
		} elsif (item.type == `tag) {
			// nothing to do
		} elsif (item.type == `hr) {
			// nothing to do
		} elsif (item.type == `br) {
			// nothing to do
		}
	}
	MakeAnchorName(indices[]:number) = MakeAnchorNameDef(indices)
}

classref(`document).render@html(
		out?:stream:w, easyFormatFlag:boolean => true, captionIndex:boolean => false) = {
	out = out || sys.stdout
	easyFormatFlag && out.print(R'''
	<html>
	<head>
	</head>
	<body>
	''')
	Renderer@html(out, captionIndex).Render(this)
	easyFormatFlag && out.print(R'''
	</body>
	</html>
	''')
}

classref(`document).render@tex(title:string, author:string, date:string, out?:stream:w) = {
	out = out || sys.stdout
	Renderer@tex(out, title, author, date).Render(this)
}

classref(`document).render@rtf(out?:stream:w) = {
	out = out || sys.stdout
	Renderer@rtf(out).Render(this)
}

classref(`document).render@console(colorFlag:boolean => true) = {
	Renderer@console(colorFlag).Render(this)
} % { `en, 'markdown',
R'''
Renders the content of markdown document to the console.

In default, it uses colors to highlight items.
Specify the argument `colorFlag` with `false` to disable the coloring process.
'''
}

classref(`document).render@toc() {block} = {
	Renderer@toc(block).Render(this)
}

setpresenter {|title:string:nil, doc:document:nil|
	title && conio.setcolor(`green) {
		println(title)
	}
	doc && Renderer@console().Render(doc)
}
