#!/usr/bin/env gura
import(re)

tmplImplementation = R'''
// ${fi.moduleName}.${fi.funcNameGura}
Gura_DeclareFunction(${fi.funcNameGura})
{
	${if (fi.rtnInfo.tmplCaller)}
	SetFuncAttr(VTYPE_any, RSLTMODE_Normal, FLAG_Map);
	DeclareBlock(OCCUR_ZeroOrOnce);
	${else}
	SetFuncAttr(VTYPE_any, RSLTMODE_Void, FLAG_None);
	${end}
	${for (arg in fi.args)}
	DeclareArg(env, "${arg.name}", VTYPE_${arg.valType}, OCCUR_${arg.occurPattern}, ${('FLAG_' + arg.flags).join('|')});
	${end}
	AddHelp(Gura_Symbol(en), Help::FMT_markdown,
	"");
}

Gura_ImplementFunction(${fi.funcNameGura})
{
	${if (!fi.validFlag)}
#if 0
	${end}
	${for (arg in fi.args) {|idx|}}
	${arg.tmplGetter.render()}
	${end}
	${if (fi.body)}
${fi.body}
	${elsif (fi.rtnInfo.tmplCaller)}
	${fi.rtnInfo.tmplCaller.render()}
	${else}
	${fi.MakeCaller()};
	return Value::Null;
	${end}
	${if (!fi.validFlag)}
#endif
	SetError_NotImpFunction(sig, "${fi.funcNameGura}");
	return Value::Null;
	${end}
}
'''.template()

tmplAssignment = R'''
Gura_AssignFunction(${fi.funcNameGura});
'''.template()

ArgInfo = struct(name:string, valType:string:nil, occurPattern:string:nil,
				 tmplGetter:template:nil, flags[]:string)

ReturnInfo = struct(tmplCaller:template:nil)

TypeConv = struct(
		valType:string:nil, occurPattern:string:nil, flags[]:string,
		tmplGetter:string:nil, tmplCaller:string:nil)

FuncInfo = class {
	public {
		funcNameC, funcNameGura, args, rtnInfo, body
	}
	__init__(moduleName:string, decl:expr, typeConvDict:dict, funcNameTranslator:function):map = {
		if (decl.isassign()) {
			caller = decl.left
			if (!(decl.right.isvalue() && isstring(decl.right.value))) {
				this.PrintError(decl, 'assigned value must be a string')
				sys.exit(1)
			}
			body = decl.right.value
		} else {
			caller = decl
			body = nil
		}
		funcNameRaw:string = caller.car.symbol
		this.moduleName = moduleName
		this.funcNameC = funcNameRaw.replace('$', '')
		this.funcNameGura = funcNameTranslator(this.funcNameC)
		this.validFlag = !funcNameRaw.find('$')
		this.body = body
		rtnTypeRaw = caller.attrfront.join('.')
		if (rtnTypeRaw.isempty() && !this.validFlag) {
			rtnTypeRaw = 'void'
		}
		if (caller.attrs.find(`const)) {
			rtnTypeRaw += ':const'
		}
		typeConv = typeConvDict.get(rtnTypeRaw)
		if (!typeConv) {
			this.PrintError(decl,
							'proper conversion not found in table "%s"', rtnTypeRaw)
			sys.exit(1)
		}
		this.rtnInfo = ReturnInfo(typeConv.tmplCaller && typeConv.tmplCaller.template())
		this.args = caller.cdr.each():list {|arg|
			if (arg.isindexer()) {
				identifier = arg.car
				argTypeRaw = identifier.attrfront.join('.') + '[]'
			} else {
				identifier = arg
				argTypeRaw = identifier.attrfront.join('.')
			}
			if (identifier.attrs.find(`const)) {
				argTypeRaw += ':const'
			}
			typeConv = typeConvDict.get(argTypeRaw)
			if (!typeConv) {
				this.PrintError(decl,
								'proper conversion not found in table "%s"', argTypeRaw)
				sys.exit(1)
			}
			flags = []
			flags.append(typeConv.flags)
			if (identifier.attrs.find(`nil)) {
				flags.append('Nil')
			}
			if (flags.isempty()) {
				flags.append('None')
			}
			name = identifier.symbol
			ArgInfo(name, typeConv.valType, typeConv.occurPattern,
					typeConv.tmplGetter && typeConv.tmplGetter.template(), flags)
		}
	}
	MakeCaller() = {
		this.funcNameC + '(' + this.args:*name.join(', ') + ')'
	}	
	PrintError(decl:expr, fmt:string, args*) = {
		sys.stderr.printf('%s:%d: %s',
				  path.filename(decl.source), decl.lineno, format(fmt, args*))
	}
	RenderImplementation() = {
		fi = this
		tmplImplementation.render()
	}
	RenderAssignment() = {
		fi = this
		tmplAssignment.render()
	}
}

typeConvDict_BuiltIn = %{
	'' => TypeConv(
			nil, nil, [],
			nil,
			'')
	'void' => TypeConv(
			nil, nil, [],
			nil,
			nil)
	'bool' => TypeConv(
			'boolean', 'Once', [],
			'bool ${arg.name} = args.GetBoolean(${idx});',
			R'''
			bool _rtn = ${fi.MakeCaller()};
			ReturnValue(env, sig, args, Value(_rtn));''')
	'char' => TypeConv(
			'number', 'Once', [],
			'char ${arg.name} = args.GetChar(${idx});',
			R'''
			bool _rtn = ${fi.MakeCaller()};
			ReturnValue(env, sig, args, Value(_rtn));''')
	'short' => TypeConv(
			'number', 'Once', [],
			'short ${arg.name} = args.GetShort(${idx});',
			R'''
			short _rtn = ${fi.MakeCaller()};
			ReturnValue(env, sig, args, Value(_rtn));''')
	'int' => TypeConv(
			'number', 'Once', [],
			'int ${arg.name} = args.GetInt(${idx});',
			R'''
			int _rtn = ${fi.MakeCaller()};
			ReturnValue(env, sig, args, Value(_rtn));''')
	'long' => TypeConv(
			'number', 'Once', [],
			'long ${arg.name} = args.GetLong(${idx});',
			R'''
			long _rtn = ${fi.MakeCaller()};
			ReturnValue(env, sig, args, Value(_rtn));''')
	'unsigned_char' => TypeConv(
			'number', 'Once', [],
			'unsigned char ${arg.name} = args.GetUChar(${idx});',
			R'''
			unsigned char _rtn = ${fi.MakeCaller()};
			ReturnValue(env, sig, args, Value(_rtn));''')
	'unsigned_short' => TypeConv(
			'number', 'Once', [],
			'unsigned short ${arg.name} = args.GetUShort(${idx});',
			R'''
			unsigned short _rtn = ${fi.MakeCaller()};
			ReturnValue(env, sig, args, Value(_rtn));''')
	'unsigned_int' => TypeConv(
			'number', 'Once', [],
			'unsigned int ${arg.name} = args.GetUInt(${idx});',
			R'''
			unsigned int _rtn = ${fi.MakeCaller()};
			ReturnValue(env, sig, args, Value(_rtn));''')
	'unsigned_long' => TypeConv(
			'number', 'Once', [],
			'unsigned long ${arg.name} = args.GetULong(${idx});',
			R'''
			unsigned long _rtn = ${fi.MakeCaller()};
			ReturnValue(env, sig, args, Value(_rtn));''')
	'UChar' => TypeConv(
			'number', 'Once', [],
			'UChar ${arg.name} = args.GetUChar(${idx});',
			R'''
			UChar _rtn = ${fi.MakeCaller()};
			ReturnValue(env, sig, args, Value(_rtn));''')
	'UShort' => TypeConv(
			'number', 'Once', [],
			'UShort ${arg.name} = args.GetUShort(${idx});',
			R'''
			UShort _rtn = ${fi.MakeCaller()};
			ReturnValue(env, sig, args, Value(_rtn));''')
	'UInt' => TypeConv(
			'number', 'Once', [],
			'UInt ${arg.name} = args.GetUInt(${idx});',
			R'''
			UInt _rtn = ${fi.MakeCaller()};
			ReturnValue(env, sig, args, Value(_rtn));''')
	'ULong' => TypeConv(
			'number', 'Once', [],
			'ULong ${arg.name} = args.GetULong(${idx});',
			R'''
			ULong _rtn = ${fi.MakeCaller()};
			ReturnValue(env, sig, args, Value(_rtn));''')
	'Int64' => TypeConv(
			'number', 'Once', [],
			'Int64 ${arg.name} = static_cast<Int64>(args.GetDouble(${idx}));',
			R'''
			Int64 _rtn = ${fi.MakeCaller()};
			ReturnValue(env, sig, args, Value(_rtn));''')
	'UInt64' => TypeConv(
			'number', 'Once', [],
			'UInt64 ${arg.name} = static_cast<UInt64>(args.GetDouble(${idx}));',
			R'''
			UInt64 _rtn = ${fi.MakeCaller()};
			ReturnValue(env, sig, args, Value(_rtn));''')
	'float' => TypeConv(
			'number', 'Once', [],
			'float ${arg.name} = args.GetFloat(${idx});',
			R'''
			float _rtn = ${fi.MakeCaller()};
			ReturnValue(env, sig, args, Value(_rtn));''')
	'double' => TypeConv(
			'number', 'Once', [],
			'double ${arg.name} = args.GetDouble(${idx});',
			R'''
			double _rtn = ${fi.MakeCaller()};
			ReturnValue(env, sig, args, Value(_rtn));''')
	'char[]' => TypeConv(
			'number', 'Once', ['List'],
			'CArray<char> ${arg.name} = args.GetList(${idx});',
			nil)
	'short[]' => TypeConv(
			'number', 'Once', ['List'],
			'CArray<short> ${arg.name} = args.GetList(${idx});',
			nil)
	'int[]' => TypeConv(
			'number', 'Once', ['List'],
			'CArray<int> ${arg.name} = args.GetList(${idx});',
			nil)
	'long[]' => TypeConv(
			'number', 'Once', ['List'],
			'CArray<long> ${arg.name} = args.GetList(${idx});',
			nil)
	'unsigned_char[]' => TypeConv(
			'number', 'Once', ['List'],
			'CArray<unsigned char> ${arg.name} = args.GetList(${idx});',
			nil)
	'unsigned_short[]' => TypeConv(
			'number', 'Once', ['List'],
			'CArray<unsigned short> ${arg.name} = args.GetList(${idx});',
			nil)
	'unsigned_int[]' => TypeConv(
			'number', 'Once', ['List'],
			'CArray<unsigned int> ${arg.name} = args.GetList(${idx});',
			nil)
	'unsigned_long[]' => TypeConv(
			'number', 'Once', ['List'],
			'CArray<unsigned long> ${arg.name} = args.GetList(${idx});',
			nil)
	'number[]' => TypeConv(
			'number', 'Once', ['List'],
			'const ValueList & ${arg.name} = args.GetList(${idx});',
			nil)
	'char@' => TypeConv(
			'string', 'Once', [],
			'char *${arg.name} = args.GetString(${idx});',
			R'''
			char *_rtn = ${fi.MakeCaller()};
			ReturnValue(env, sig, args, Value(_rtn));''')
	'char@:const' => TypeConv(
			'string', 'Once', [],
			'const char *${arg.name} = args.GetString(${idx});',
			R'''
			const char *_rtn = ${fi.MakeCaller()};
			ReturnValue(env, sig, args, Value(_rtn));''')
	'symbol@' => TypeConv(
			'symbol', 'Once', [],
			'const Symbol *${arg.name} = args.GetSymbol(${idx});',
			R'''
			const Symbol *_rtn = ${fi.MakeCaller()};
			ReturnValue(env, sig, args, Value(_rtn));''')
	'symbol@:const' => TypeConv(
			'symbol', 'Once', [],
			'const Symbol *${arg.name} = args.GetSymbol(${idx});',
			R'''
			const Symbol *_rtn = ${fi.MakeCaller()};
			ReturnValue(env, sig, args, Value(_rtn));''')
	'binary' => TypeConv(
			'binary', 'Once', [],
			'const Binary &${arg.name} = Object_binary::GetObject(args, ${idx})->GetBinary();',
			nil)
	'stream' => TypeConv(
			'stream', 'Once', [],
			'Stream &${arg.name} = Object_stream::GetObject(args, ${idx})->GetStream();',
			nil)
	'image' => TypeConv(
			'image', 'Once', [],
			'Image *${arg.name} = Object_image::GetObject(args, ${idx})->GetImage();',
			nil)
}

Renderer = class {
	__init__(moduleName:string, decls:iterator, typeConvDict:dict, funcNameTranslator:function) = {
		typeConvDictWork = %{}
		typeConvDictWork.store(typeConvDict_BuiltIn)
		typeConvDictWork.store(typeConvDict)
		this.funcInfos = FuncInfo(moduleName, decls, typeConvDictWork, funcNameTranslator):list
	}
	RenderImplementation() = {
		this.funcInfos:*RenderImplementation() + '\n'
	}
	RenderAssignment() = {
		this.funcInfos:*RenderAssignment()
	}
}
