#!/usr/bin/env gura
import(argopt)

argopt.Parser {|p|
	p.addFlag('verbose',	'v', 'verbose mode')
	p.addFlag('debug',		'd', 'build module with debug information')
	p.addFlag('rebuild',	'r', 'clean output files before building')
	p.addParam('exclude',	'x', 'specify module to exclude', 'NAME', '')
	try {
		[cfg, argv] = p.parse(sys.argv)
	} catch {|e|
		sys.stderr.println(e.text)
		sys.exit(1)
	}
	command = if (argv.len() == 0) {
		`build
	} elsif (argv[0] == 'help') {
		sys.stderr.printf('usage: %s [options] command\n', path.filename(sys.argv[0]))
		sys.stderr.println('command:')
		sys.stderr.println(' clean    clean output files')
		sys.stderr.println(' install  install produced files to root directory')
		sys.stderr.println('options:')
		sys.stderr.println(' ', p.formatHelp())
		sys.exit(1)
	} elsif (argv[0] == 'clean') {
		`clean
	} elsif (argv[0] == 'install') {
		`install
	} else {
		sys.stderr.println('unknown command: ', argv[0])
		sys.exit(1)
	}
}
DirInfo = struct(dirName:string, platforms[]:symbol)
dirInfos = @(DirInfo) {
	['module-sample',		[`mswin, `linux]]
	['module-csv',			[`mswin, `linux]]
	['module-re',			[`mswin, `linux]]
	['module-sqlite3',		[`mswin, `linux]]
	['module-xml',			[`mswin, `linux]]
	['module-yaml',			[`mswin, `linux]]
//	['module-mysql',		[`mswin, `linux]]
//	['module-postgresql',	[`mswin, `linux]]
	['module-sdl',			[`mswin, `linux]]
	['module-cairo',		[`mswin, `linux]]
	['module-opengl',		[`mswin, `linux]]
	['module-glu',			[`mswin, `linux]]
	['module-bmp',			[`mswin, `linux]]
	['module-ppm',			[`mswin, `linux]]
	['module-xpm',			[`mswin, `linux]]
	['module-msico',		[`mswin, `linux]]
	['module-jpeg',			[`mswin, `linux]]
	['module-png',			[`mswin, `linux]]
	['module-gif',			[`mswin, `linux]]
	['module-tiff',			[`mswin, `linux]]
	['module-freetype',		[`mswin, `linux]]
	['module-tcl',			[`mswin, `linux]]
	['module-hash',			[`mswin, `linux]]
	['module-gzip',			[`mswin, `linux]]
	['module-bzip2',		[`mswin, `linux]]
	['module-zip',			[`mswin, `linux]]
	['module-tar',			[`mswin, `linux]]
	['module-http',			[`mswin, `linux]]
	['module-curl',			[`mswin, `linux]]
	['module-wx',			[`mswin, `linux]]
	['module-guri',			[`mswin, `linux]]
	['module-midi',			[`mswin, `linux]]
	['module-wav',			[`mswin, `linux]]
	['module-markdown',		[`mswin, `linux]]
	['module-uuid',			[`mswin]]
	['module-canvas',		[`mswin]]
	['module-mswin',		[`mswin]]
}
dirNamesExcept = 'module-' + cfg['exclude'].split(',')
dirNames = dirInfos.filter(dirInfos:*platforms:*iscontain(sys.platform))::dirName
dirNamePre = path.dirname(sys.argv[0])
dirNamesError = []
debFiles = []
rpmFiles = []
for (dirName in dirNames) {
	if (dirName in dirNamesExcept) {
		printf('[%s]', dirName)
		conio.setcolor(`bright_red) {
			printf('...skipped\n')
		}
		continue
	}
	printf('[%s]\n', dirName)
	if (!dirNamePre.isempty()) {
		pathName = path.join(dirNamePre, dirName)
	}
	argvOrg = sys.argv
	sys.argv = ['build.gura']
	if (command == `build) {
		// nothing to do
	} elsif (command == `clean) {
		sys.argv.append('clean')
	} elsif (command == `install) {
		sys.argv.append('install-root')
	} else {
		// nothing to do
	}
	cfg['verbose'] && sys.argv.append('--verbose')
	cfg['debug'] && sys.argv.append('--debug')
	cfg['rebuild'] && sys.argv.append('--rebuild')
	sys.argv.append('--basedir=' + path.join(fs.getcwd(), dirName))
	fs.chdir(pathName) {
		scope {
			builder = open('build.gura').parse().eval()
			builder.errorFlag && dirNamesError.append(dirName)
			builder.debFiles && debFiles.append(builder.debFiles)
			builder.rpmFiles && rpmFiles.append(builder.rpmFiles)
		}
		sys.argv = argvOrg
	}
}
if (sys.argv.len() < 2 || command != `install) {
	if (!dirNamesError.isempty()) {
		println('**** error in ', dirNamesError)
	}
	if (!debFiles.isempty()) {
		fileName = 'install-deb.sh'
		open(fileName, 'w').println('apt-get install -y ', set(debFiles))
		fs.chmod('a+x', fileName)
		println(fileName, ' was created. Execute this to install guest Debian packages.')
	}
	if (!rpmFiles.isempty()) {
		fileName = 'install-rpm.sh'
		open(fileName, 'w').println('yum install -y ', set(rpmFiles))
		fs.chmod('a+x', fileName)
		println(fileName, ' was created. Execute this to install guest RPM packages.')
	}
	!dirNamesError.isempty() && sys.exit(1);
}
