#!/usr/bin/env gura
// This Gura script has been ported from the original above by Yutaka SAITO.
import(wx)
import(resource)
import(colrdlgg)
import(dirdlgg)
import(filedlgg)
import(fontdlgg)

// Menu IDs
[
	DIALOGS_CHOOSE_COLOUR,
	DIALOGS_CHOOSE_COLOUR_GENERIC,
	DIALOGS_CHOOSE_FONT,
	DIALOGS_CHOOSE_FONT_GENERIC,
	DIALOGS_MESSAGE_BOX,
	DIALOGS_SINGLE_CHOICE,
	DIALOGS_MULTI_CHOICE,
	DIALOGS_TEXT_ENTRY,
	DIALOGS_PASSWORD_ENTRY,
	DIALOGS_FILE_OPEN,
	DIALOGS_FILE_OPEN2,
	DIALOGS_FILES_OPEN,
	DIALOGS_FILE_SAVE,
	DIALOGS_FILE_OPEN_GENERIC,
	DIALOGS_FILES_OPEN_GENERIC,
	DIALOGS_FILE_SAVE_GENERIC,
	DIALOGS_DIR_CHOOSE,
	DIALOGS_DIRNEW_CHOOSE,
	DIALOGS_GENERIC_DIR_CHOOSE,
	DIALOGS_TIP,
	DIALOGS_NUM_ENTRY,
	DIALOGS_LOG_DIALOG,
	DIALOGS_MODAL,
	DIALOGS_MODELESS,
	DIALOGS_CENTRE_SCREEN,
	DIALOGS_CENTRE_PARENT,
	DIALOGS_MODELESS_BTN,
	DIALOGS_PROGRESS,
	DIALOGS_ABOUTDLG_SIMPLE,
	DIALOGS_ABOUTDLG_FANCY,
	DIALOGS_ABOUTDLG_FULL,
	DIALOGS_ABOUTDLG_CUSTOM,
	DIALOGS_BUSYINFO,
	DIALOGS_FIND,
	DIALOGS_REPLACE,
	DIALOGS_REQUEST,
	DIALOGS_PROPERTY_SHEET,
	DIALOGS_PROPERTY_SHEET_TOOLBOOK,
	DIALOGS_PROPERTY_SHEET_BUTTONTOOLBOOK
] = wx.ID_HIGHEST..

myCanvas = nil

MyApp = class(wx.App) {
	OnInit() = {
		wx.InitAllImageHandlers();
		self.m_canvasTextColour = wx.BLACK;
		self.m_canvasFont = wx.NORMAL_FONT;
		// Create the main frame window
		frame = MyFrame(nil, 'wxWidgets dialogs example');
		// Make a menubar
		menuDlg = wx.Menu()
		menuDlg.Append(DIALOGS_MESSAGE_BOX, '&Message box\tCtrl-M');
		choices_menu = wx.Menu()
		choices_menu.Append(DIALOGS_CHOOSE_COLOUR, '&Choose colour');
		choices_menu.Append(DIALOGS_CHOOSE_FONT, 'Choose &font');
		choices_menu.Append(DIALOGS_SINGLE_CHOICE, '&Single choice\tCtrl-C');
		choices_menu.Append(DIALOGS_MULTI_CHOICE, 'M&ultiple choice\tCtrl-U');
		choices_menu.AppendSeparator();
		choices_menu.Append(DIALOGS_CHOOSE_COLOUR_GENERIC, '&Choose colour (generic)');
		choices_menu.Append(DIALOGS_CHOOSE_FONT_GENERIC, 'Choose &font (generic)');
		menuDlg.AppendMenu(wx.ID_ANY, '&Choices and selectors', choices_menu);
		entry_menu = wx.Menu()
		entry_menu.Append(DIALOGS_TEXT_ENTRY, 'Text &entry\tCtrl-E');
		entry_menu.Append(DIALOGS_PASSWORD_ENTRY, '&Password entry\tCtrl-P');
		entry_menu.Append(DIALOGS_NUM_ENTRY, '&Numeric entry\tCtrl-N');
		menuDlg.AppendMenu(wx.ID_ANY, '&Entry dialogs', entry_menu);
		filedlg_menu = wx.Menu()
		filedlg_menu.Append(DIALOGS_FILE_OPEN, '&Open file\tCtrl-O');
		filedlg_menu.Append(DIALOGS_FILE_OPEN2, '&Second open file\tCtrl-2');
		filedlg_menu.Append(DIALOGS_FILES_OPEN, 'Open &files\tCtrl-Q');
		filedlg_menu.Append(DIALOGS_FILE_SAVE, 'Sa&ve file\tCtrl-S');
		filedlg_menu.AppendSeparator();
		filedlg_menu.Append(DIALOGS_FILE_OPEN_GENERIC, '&Open file (generic)');
		filedlg_menu.Append(DIALOGS_FILES_OPEN_GENERIC, 'Open &files (generic)');
		filedlg_menu.Append(DIALOGS_FILE_SAVE_GENERIC, 'Sa&ve file (generic)');
		menuDlg.AppendMenu(wx.ID_ANY, '&File operations', filedlg_menu);
		dir_menu = wx.Menu()
		dir_menu.Append(DIALOGS_DIR_CHOOSE, '&Choose a directory\tCtrl-D');
		dir_menu.Append(DIALOGS_DIRNEW_CHOOSE, 'Choose a directory (with \"Ne&w\" button)\tShift-Ctrl-D');
		menuDlg.AppendMenu(wx.ID_ANY, '&Directory operations', dir_menu);
		dir_menu.AppendSeparator();
		dir_menu.Append(DIALOGS_GENERIC_DIR_CHOOSE, '&Choose a directory (generic)');
		info_menu = wx.Menu()
		info_menu.Append(DIALOGS_TIP, '&Tip of the day\tCtrl-T');
		info_menu.Append(DIALOGS_PROGRESS, 'Pro&gress dialog\tCtrl-G');
		info_menu.Append(DIALOGS_BUSYINFO, '&Busy info dialog\tCtrl-B');
		info_menu.Append(DIALOGS_LOG_DIALOG, '&Log dialog\tCtrl-L');
		menuDlg.AppendMenu(wx.ID_ANY, '&Informative dialogs', info_menu);
		find_menu = wx.Menu()
		find_menu.AppendCheckItem(DIALOGS_FIND, '&Find dialog\tCtrl-F');
		find_menu.AppendCheckItem(DIALOGS_REPLACE, 'Find and &replace dialog\tShift-Ctrl-F');
		menuDlg.AppendMenu(wx.ID_ANY, '&Searching', find_menu);
		dialogs_menu = wx.Menu()
		dialogs_menu.Append(DIALOGS_MODAL, '&Modal dialog\tCtrl-W');
		dialogs_menu.AppendCheckItem(DIALOGS_MODELESS, 'Mode&less dialog\tCtrl-Z');
		dialogs_menu.Append(DIALOGS_CENTRE_SCREEN, 'Centered on &screen\tShift-Ctrl-1');
		dialogs_menu.Append(DIALOGS_CENTRE_PARENT, 'Centered on &parent\tShift-Ctrl-2');
		menuDlg.AppendMenu(wx.ID_ANY, '&Generic dialogs', dialogs_menu);
		sheet_menu = wx.Menu()
		sheet_menu.Append(DIALOGS_PROPERTY_SHEET, '&Standard property sheet\tShift-Ctrl-P');
		sheet_menu.Append(DIALOGS_PROPERTY_SHEET_TOOLBOOK, '&Toolbook sheet\tShift-Ctrl-T');
		menuDlg.AppendMenu(wx.ID_ANY, '&Property sheets', sheet_menu);
		menuDlg.Append(DIALOGS_REQUEST, '&Request user attention\tCtrl-R');
		menuDlg.AppendSeparator();
		menuDlg.Append(wx.ID_EXIT, 'E&xit\tAlt-X');
		menuHelp = wx.Menu()
		menuHelp.Append(DIALOGS_ABOUTDLG_SIMPLE, '&About (simple)...\tF1');
		menuHelp.Append(DIALOGS_ABOUTDLG_FANCY, 'About (&fancy)...\tShift-F1');
		menuHelp.Append(DIALOGS_ABOUTDLG_FULL, 'About (f&ull)...\tCtrl-F1');
		menuHelp.Append(DIALOGS_ABOUTDLG_CUSTOM, 'About (&custom)...\tCtrl-Shift-F1');
		menubar = wx.MenuBar()
		menubar.Append(menuDlg, '&Dialogs');
		menubar.Append(menuHelp, '&Help');
		frame.SetMenuBar(menubar);
		myCanvas:extern = MyCanvas(frame);
		myCanvas.SetBackgroundColour(wx.WHITE);
		frame.Centre(wx.BOTH);
		// Show the frame
		frame.Show(true);
		self.SetTopWindow(frame);
		true;
	}
}

/*
// a trivial example of a custom dialog class
MyAboutDialog = class(wx.GenericAboutDialog) {
	__init__(info:wx.AboutDialogInfo) = {
		self.Create(info);
	}
	// add some custom controls
	DoAddCustomControls() = {
		self.AddControl(wx.StaticLine(self), wx.SizerFlags().Expand())
		self.AddText('Some custom text')
		self.AddControl(wx.StaticLine(self), wx.SizerFlags().Expand());
	}
};
*/

MyFrame = class(wx.Frame) {
	__init__(parent:wx.Window:nil, title:string) = {|parent, wx.ID_ANY, title|
		self.EventTable()
		self.m_findData = wx.FindReplaceData()
		self.m_clrData = wx.ColourData()
		self.SetIcon(wx.IconFromXPMData(resource.sample_xpm))
		self.m_dialog = nil
		self.m_dlgFind = self.m_dlgReplace = nil
		self.m_clrData.SetChooseFull(true)
		repeat(16) {|i|
			self.m_clrData.SetCustomColour(
				i,
				wx.Colour(
					i * 16,
					i * 16,
					i * 16
				)
			);
		}
		self.CreateStatusBar();
	}
	EventTable() = {
		self.Bind(wx.EVT_MENU, DIALOGS_MESSAGE_BOX) {|event| self.MessageBox(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_CHOOSE_COLOUR) {|event| self.ChooseColour(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_CHOOSE_FONT) {|event| self.ChooseFont(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_LOG_DIALOG) {|event| self.LogDialog(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_TEXT_ENTRY) {|event| self.TextEntry(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_PASSWORD_ENTRY) {|event| self.PasswordEntry(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_NUM_ENTRY) {|event| self.NumericEntry(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_SINGLE_CHOICE) {|event| self.SingleChoice(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_MULTI_CHOICE) {|event| self.MultiChoice(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_FILE_OPEN) {|event| self.FileOpen(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_FILE_OPEN2) {|event| self.FileOpen2(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_FILES_OPEN) {|event| self.FilesOpen(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_FILE_SAVE) {|event| self.FileSave(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_FILE_OPEN_GENERIC) {|event| self.FileOpenGeneric(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_FILES_OPEN_GENERIC) {|event| self.FilesOpenGeneric(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_FILE_SAVE_GENERIC) {|event| self.FileSaveGeneric(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_DIR_CHOOSE) {|event| self.DirChoose(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_DIRNEW_CHOOSE) {|event| self.DirChooseNew(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_MODAL) {|event| self.ModalDlg(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_MODELESS) {|event| self.ModelessDlg(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_CENTRE_SCREEN) {|event| self.DlgCenteredScreen(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_CENTRE_PARENT) {|event| self.DlgCenteredParent(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_TIP) {|event| self.ShowTip(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_CHOOSE_FONT_GENERIC) {|event| self.ChooseFontGeneric(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_GENERIC_DIR_CHOOSE) {|event| self.GenericDirChoose(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_CHOOSE_COLOUR_GENERIC) {|event| self.ChooseColourGeneric(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_PROGRESS) {|event| self.ShowProgress(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_ABOUTDLG_SIMPLE) {|event| self.ShowSimpleAboutDialog(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_ABOUTDLG_FANCY) {|event| self.ShowFancyAboutDialog(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_ABOUTDLG_FULL) {|event| self.ShowFullAboutDialog(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_ABOUTDLG_CUSTOM) {|event| self.ShowCustomAboutDialog(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_BUSYINFO) {|event| self.ShowBusyInfo(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_FIND) {|event| self.ShowFindDialog(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_REPLACE) {|event| self.ShowReplaceDialog(event) }
		self.Bind(wx.EVT_FIND) {|event| self.OnFindDialog(event) }
		self.Bind(wx.EVT_FIND_NEXT) {|event| self.OnFindDialog(event) }
		self.Bind(wx.EVT_FIND_REPLACE) {|event| self.OnFindDialog(event) }
		self.Bind(wx.EVT_FIND_REPLACE_ALL) {|event| self.OnFindDialog(event) }
		self.Bind(wx.EVT_FIND_CLOSE) {|event| self.OnFindDialog(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_PROPERTY_SHEET) {|event| self.OnPropertySheet(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_PROPERTY_SHEET_TOOLBOOK) {|event| self.OnPropertySheet(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_PROPERTY_SHEET_BUTTONTOOLBOOK) {|event| self.OnPropertySheet(event) }
		self.Bind(wx.EVT_MENU, DIALOGS_REQUEST) {|event| self.OnRequestUserAttention(event) }
		self.Bind(wx.EVT_MENU, wx.ID_EXIT) {|event| self.OnExit(event) }
	}
	ChooseColour(event:wx.CommandEvent) = {
		self.m_clrData.SetColour(myCanvas.GetBackgroundColour());
		dialog = wx.ColourDialog(self, self.m_clrData);
		dialog.SetTitle('Choose the background colour');
		if (dialog.ShowModal() == wx.ID_OK) {
			self.m_clrData = dialog.GetColourData();
			myCanvas.SetBackgroundColour(self.m_clrData.GetColour());
			myCanvas.ClearBackground();
			myCanvas.Refresh();
		}
	}
	ChooseColourGeneric(event:wx.CommandEvent) = {
		self.m_clrData.SetColour(myCanvas.GetBackgroundColour());
		//FIXME:TODO:This has no effect...
		self.m_clrData.SetChooseFull(true);
		repeat(16) {|i|
			colour = wx.Colour(
				i * 16,
				i * 16,
				i * 16
			)
			self.m_clrData.SetCustomColour(i, colour);
		}
		dialog = wx.GenericColourDialog(self, self.m_clrData);
		if (dialog.ShowModal() == wx.ID_OK) {
			self.m_clrData = dialog.GetColourData();
			myCanvas.SetBackgroundColour(self.m_clrData.GetColour());
			myCanvas.ClearBackground();
			myCanvas.Refresh();
		}
		dialog.Destroy();
	}
	ChooseFont(event:wx.CommandEvent) = {
		data = wx.FontData()
		data.SetInitialFont(wx.GetApp().m_canvasFont);
		data.SetColour(wx.GetApp().m_canvasTextColour);
		// you might also do this:
		//
		//  wxFontDialog dialog;
		//  if ( !dialog.Create(this, data) { ... error ... }
		//
		dialog = wx.FontDialog(self, data);
		if (dialog.ShowModal() == wx.ID_OK) {
			retData = dialog.GetFontData();
			wx.GetApp().m_canvasFont = retData.GetChosenFont();
			wx.GetApp().m_canvasTextColour = retData.GetColour();
			myCanvas.Refresh();
		}
		//else: cancelled by the user, don't change the font
	}
	ChooseFontGeneric(event:wx.CommandEvent) = {
		data = wx.FontData()
		data.SetInitialFont(wx.GetApp().m_canvasFont);
		data.SetColour(wx.GetApp().m_canvasTextColour);
		dialog = wx.GenericFontDialog(self, data);
		if (dialog.ShowModal() == wx.ID_OK) {
			retData = dialog.GetFontData();
			wx.GetApp().m_canvasFont = retData.GetChosenFont();
			wx.GetApp().m_canvasTextColour = retData.GetColour();
			myCanvas.Refresh();
		}
		dialog.Destroy();
	}
	LogDialog(event:wx.CommandEvent) = {
		// calling wxYield() (as ~wxBusyCursor does) shouldn't result in messages
		// being flushed -- test it
		wx.BusyCursor {
			wx.LogMessage('This is some message - everything is ok so far.');
			wx.LogMessage('Another message...\n... this one is on multiple lines');
			wx.LogWarning('And then something went wrong!');
			// and if ~wxBusyCursor doesn't do it, then call it manually
			wx.Yield();
		}
		wx.LogError('Intermediary error handler decided to abort.');
		wx.LogError('The top level caller detected an unrecoverable error.');
		wx.Log.FlushActive();
		wx.LogMessage('And this is the same dialog but with only one message.');
	}
	MessageBox(event:wx.CommandEvent) = {
		dialog = wx.MessageDialog(nil, 'This is a message box\nA long, long string to test out the message box properly',
			'Message box text', wx.NO_DEFAULT | wx.YES_NO | wx.CANCEL | wx.ICON_INFORMATION);
		ret = dialog.ShowModal()
		if (ret == wx.ID_YES) {
			wx.LogStatus('You pressed "Yes"');
		} elsif(ret == wx.ID_NO) {
			wx.LogStatus('You pressed "No"');
		} elsif(ret == wx.ID_CANCEL) {
			wx.LogStatus('You pressed "Cancel"');
		} else {
			wx.LogError('Unexpected wxMessageDialog return code!');
		}
	}
	NumericEntry(event:wx.CommandEvent) = {
		res = wx.GetNumberFromUser('This is some text, actually a lot of text.\n' \
									'Even two rows of text.',
									'Enter a number:', 'Numeric input test',
									50, 0, 100, self);
		if (res == -1) {
			msg = 'Invalid number entered or dialog cancelled.';
			icon = wx.ICON_HAND;
		} else {
			msg = format("You've entered %lu", res);
			icon = wx.ICON_INFORMATION;
		}
		wx.MessageBox(msg, 'Numeric test result', wx.OK | icon, self);
	}
	PasswordEntry(event:wx.CommandEvent) = {
		pwd = wx.GetPasswordFromUser('Enter password:',
									'Password entry dialog',
									wx.EmptyString,
									self);
		if (!pwd.isempty()) {
			wx.MessageBox(format("Your password is '%s'", pwd),
						'Got password', wx.OK | wx.ICON_INFORMATION, self);
		}
	}
	TextEntry(event:wx.CommandEvent) = {
		dialog = wx.TextEntryDialog(self,
								'This is a small sample\n' \
								'A long, long string to test out the text entrybox',
								'Please enter a string',
								'Default value',
								wx.OK | wx.CANCEL);
		if (dialog.ShowModal() == wx.ID_OK) {
			wx.MessageBox(dialog.GetValue(), 'Got string', wx.OK | wx.ICON_INFORMATION, self);
		}
	}
	SingleChoice(event:wx.CommandEvent) = {
		choices = ['One', 'Two', 'Three', 'Four', 'Five']
		dialog = wx.SingleChoiceDialog(self,
									'This is a small sample\n' \
									'A single-choice convenience dialog',
									'Please select a value',
									choices);
		dialog.SetSelection(2);
		if (dialog.ShowModal() == wx.ID_OK) {
			dialog2 = wx.MessageDialog(self, dialog.GetStringSelection(), 'Got string');
			dialog2.ShowModal();
		}
	}
	MultiChoice(event:wx.CommandEvent) = {
		choices = [
			'One', 'Two', 'Three', 'Four', 'Five',
			'Six', 'Seven', 'Eight', 'Nine', 'Ten',
			'Eleven', 'Twelve', 'Seventeen',
		]
		selections = wx.GetMultipleChoices('This is a small sample\n' \
										'A multi-choice convenience dialog',
										'Please select a value',
										choices,
										self);
		if (!selections.isempty()) {
			msg = format('You selected %u items:\n', selections.len());
			msg += format('\t%u: %u (%s)\n',
							0.., selections,
							choices[selections]).join()
			wx.LogMessage(msg);
		}
		//else: cancelled or nothing selected
	}
	FileOpen(event:wx.CommandEvent) = {
		dialog = wx.FileDialog(
						self,
						'Testing open file dialog',
						wx.EmptyString,
						wx.EmptyString,
						'C++ files (*.cpp;*.h)|*.cpp;*.h'
					);
		dialog.CentreOnParent();
		dialog.SetDirectory(wx.GetHomeDir());
		if (dialog.ShowModal() == wx.ID_OK) {
			info = format('Full file name: %s\n' \
						'Path: %s\n' \
						'Name: %s',
						dialog.GetPath(),
						dialog.GetDirectory(),
						dialog.GetFilename());
			dialog2 = wx.MessageDialog(self, info, 'Selected file');
			dialog2.ShowModal();
		}
	}
	s_extDef = ''
	FileOpen2(event:wx.CommandEvent) = {
		res = wx.FileSelector(
							'Select the file to load',
							wx.EmptyString, wx.EmptyString,
							s_extDef,
							format(
								'Waveform (*.wav)|*.wav|Plain text (*.txt)|*.txt|All files (%s)|%s',
								wx.FileSelectorDefaultWildcardStr,
								wx.FileSelectorDefaultWildcardStr
							),
							wx.FD_OPEN | wx.FD_CHANGE_DIR | wx.FD_PREVIEW,
							self
						);
		if (res.isempty()) {
			return;
		}
		// it is just a sample, would use wxSplitPath in real program
		s_extDef = path.splitext(res)[1]
		wx.LogMessage("You selected the file '%s', remembered extension '%s'",
					res, s_extDef);
	}
	FilesOpen(event:wx.CommandEvent) = {
		wildcards = format(
						'All files (%s)|%s|C++ files (*.cpp;*.h)|*.cpp;*.h',
						wx.FileSelectorDefaultWildcardStr,
						wx.FileSelectorDefaultWildcardStr
					);
		dialog = wx.FileDialog(self, 'Testing open multiple file dialog',
						wx.EmptyString, wx.EmptyString, wildcards,
						wx.FD_OPEN | wx.FD_MULTIPLE);
		if (dialog.ShowModal() == wx.ID_OK) {
			paths = dialog.GetPaths();
			filenames = dialog.GetFilenames();
			count = paths.len();
			msg = format('File %d: %s (%s)\n', 0.., paths, filenames).join()
			s = format('Filter index: %d', dialog.GetFilterIndex());
			msg += s;
			dialog2 = wx.MessageDialog(self, msg, 'Selected files');
			dialog2.ShowModal();
		}
	}
	FileSave(event:wx.CommandEvent) = {
		dialog = wx.FileDialog(self,
							'Testing save file dialog',
							wx.EmptyString,
							'myletter.doc',
							'Text files (*.txt)|*.txt|Document files (*.doc)|*.doc',
							wx.FD_SAVE | wx.FD_OVERWRITE_PROMPT);
		dialog.SetFilterIndex(1);
		if (dialog.ShowModal() == wx.ID_OK) {
			wx.LogMessage('%s, filter %d',
						dialog.GetPath(), dialog.GetFilterIndex());
		}
	}
	FileOpenGeneric(event:wx.CommandEvent) = {
		dialog = wx.GenericFileDialog(self,
	  						'Testing open file dialog',
							wx.EmptyString,
							wx.EmptyString,
							'C++ files (*.cpp;*.h)|*.cpp;*.h'
						 );
		dialog.SetDirectory(wx.GetHomeDir());
		if (dialog.ShowModal() == wx.ID_OK) {
			info = format('Full file name: %s\n' \
						'Path: %s\n' \
						'Name: %s',
						dialog.GetPath(),
						dialog.GetDirectory(),
						dialog.GetFilename());
			dialog2 = wx.MessageDialog(self, info, 'Selected file');
			dialog2.ShowModal();
		}
	}
	FilesOpenGeneric(event:wx.CommandEvent) = {
		// On PocketPC you can disable OK-only dialogs policy using system option
		buttons = wx.SystemOptions.GetOptionInt('wince.dialog.real-ok-cancel');
		wx.SystemOptions.SetOption('wince.dialog.real-ok-cancel', 1);
		wildcards = 'All files (*.*)|*.*|C++ files (*.cpp;*.h)|*.cpp;*.h';
		dialog = wx.GenericFileDialog(self, 'Testing open multiple file dialog',
							wx.EmptyString, wx.EmptyString, wildcards,
							wx.FD_MULTIPLE);
		if (dialog.ShowModal() == wx.ID_OK) {
			paths = dialog.GetPaths();
			filenames = dialog.GetFilenames();
			msg = format('File %d: %s (%s)\n', 0.., paths, filenames).join()
			s = format('Filter index: %d', dialog.GetFilterIndex());
			msg += s;
			dialog2 = wx.MessageDialog(self, msg, 'Selected files');
			dialog2.ShowModal();
		}
		// restore system option
		wx.SystemOptions.SetOption('wince.dialog.real-ok-cancel', buttons);
	}
	FileSaveGeneric(event:wx.CommandEvent) = {
		dialog = wx.GenericFileDialog(self,
							'Testing save file dialog',
							wx.EmptyString,
							'myletter.doc',
							'Text files (*.txt)|*.txt|Document files (*.doc)|*.doc',
							wx.FD_SAVE | wx.FD_OVERWRITE_PROMPT);
		dialog.SetFilterIndex(1);
		if (dialog.ShowModal() == wx.ID_OK) {
			wx.LogMessage('%s, filter %d',
						 dialog.GetPath(), dialog.GetFilterIndex());
		}
	}
	DoDirChoose(style:number) = {
		// pass some initial dir to wxDirDialog
		dirHome = wx.GetHomeDir();
		dialog = wx.DirDialog(self, 'Testing directory picker', dirHome, style);
		if (dialog.ShowModal() == wx.ID_OK) {
			wx.LogMessage('Selected path: %s', dialog.GetPath());
		}
	}
	DirChoose(event:wx.CommandEvent) = {
		self.DoDirChoose(wx.DD_DEFAULT_STYLE | wx.DD_DIR_MUST_EXIST);
	}
	DirChooseNew(event:wx.CommandEvent) = {
		self.DoDirChoose(wx.DD_DEFAULT_STYLE & ~wx.DD_DIR_MUST_EXIST);
	}
	GenericDirChoose(event:wx.CommandEvent) = {
		// pass some initial dir to wxDirDialog
		dirHome = wx.GetHomeDir();
		dialog = wx.GenericDirDialog(self, 'Testing generic directory picker', dirHome);
		if (dialog.ShowModal() == wx.ID_OK) {
			dialog2 = wx.MessageDialog(self, dialog.GetPath(), 'Selected path');
			dialog2.ShowModal();
		}
	}
	ModalDlg(event:wx.CommandEvent) = {
		dlg = MyModalDialog(self);
		dlg.ShowModal();
	}
	ModelessDlg(event:wx.CommandEvent) = {
		show = self.GetMenuBar().IsChecked(event.GetId());
		if (show) {
			if (!self.m_dialog) {
				self.m_dialog = MyModelessDialog(self);
			}
			self.m_dialog.Show(true);
		} else { // hide
			// If m_dialog is NULL, then possibly the system
			// didn't report the checked menu item status correctly.
			// It should be true just after the menu item was selected,
			// if there was no modeless dialog yet.
			wx.ASSERT(self.m_dialog != nil);
			if (self.m_dialog) {
				self.m_dialog.Hide();
			}
		}
	}
	DlgCenteredScreen(event:wx.CommandEvent) = {
		dlg = wx.Dialog(self, wx.ID_ANY, 'Dialog centered on screen',
					wx.DefaultPosition, wx.Size(200, 100));
		wx.Button(dlg, wx.ID_OK, 'Close').Centre();
		dlg.CentreOnScreen();
		dlg.ShowModal();
	}
	DlgCenteredParent(event:wx.CommandEvent) = {
		dlg = wx.Dialog(self, wx.ID_ANY, 'Dialog centered on parent',
					 wx.DefaultPosition, wx.Size(200, 100));
		wx.Button(dlg, wx.ID_OK, 'Close').Centre();
		dlg.CentreOnParent();
		dlg.ShowModal();
	}
	s_index = -1
	ShowTip(event:wx.CommandEvent) = {
		if (s_index == -1) {
			// this is completely bogus, we don't know how many lines are there
			// in the file, but who cares, it's a demo only...
			s_index = rand(5);
		}
		tipProvider = wx.CreateFileTipProvider('tips.txt', s_index);
		showAtStartup = wx.ShowTip(self, tipProvider);
		if (showAtStartup) {
			wx.MessageBox('Will show tips on startup', 'Tips dialog',
						wx.OK | wx.ICON_INFORMATION, self);
		}
		s_index = tipProvider.GetCurrentTip();
		tipProvider = nil
	}
	OnPropertySheet(event:wx.CommandEvent) = {
		dialog = SettingsDialog(self, event.GetId());
		dialog.ShowModal();
	}
	OnRequestUserAttention(event:wx.CommandEvent) = {
		wx.LogStatus('Sleeping for 3 seconds to allow you to switch to another window');
		wx.Sleep(3);
		self.RequestUserAttention(wx.USER_ATTENTION_ERROR);
	}
	OnExit(event:wx.CommandEvent) = {
		self.Close(true)
	}
	max = 100
	ShowProgress(event:wx.CommandEvent) = {
		dialog = wx.ProgressDialog('Progress dialog example',
								'An informative message',
								self.max,	// range
								self,		// parent
								wx.PD_CAN_ABORT | \
								wx.PD_CAN_SKIP | \
								wx.PD_APP_MODAL | \
								/* wxPD_AUTO_HIDE | -- try this as well */ \
								wx.PD_ELAPSED_TIME | \
								wx.PD_ESTIMATED_TIME | \
								wx.PD_REMAINING_TIME \
								| wx.PD_SMOOTH // - makes indeterminate mode bar on WinXP very small
							);
		cont = true;
		repeat(self.max) {|i|
			wx.MilliSleep(200);
			// test both modes of wxProgressDialog behaviour: start in
			// indeterminate mode but switch to the determinate one later
			determinate = (i > self.max / 2);
			if (i == self.max) {
				msg = "That's all, folks!";
			} elsif ( !determinate ) {
				msg = 'Testing indeterminate mode';
			} elsif ( determinate ) {
				msg = 'Now in standard determinate mode';
			}
			// will be set to true if "Skip" button was pressed
			skip = false;
			if (determinate) {
				[cont, skip] = dialog.Update(i, msg);
			} else {
				[cont, skip] = dialog.Pulse(msg);
			}
			// each skip will move progress about quarter forward
			if (skip) {
				i += self.max / 4;
			}
			if (!cont) {
				if (wx.MessageBox('Do you really want to cancel?',
								  'Progress dialog question',  // caption
								  wx.YES_NO | wx.ICON_QUESTION) == wx.YES) {
					break;
				}
				cont = true;
				dialog.Resume();
			}
		}
		if (!cont) {
			wx.LogStatus('Progress dialog aborted!');
		} else {
			wx.LogStatus('Countdown from %d finished', self.max);
		}
	}
	ShowSimpleAboutDialog(event:wx.CommandEvent) = {
		info = wx.AboutDialogInfo()
		InitAboutInfoMinimal(info);
		wx.AboutBox(info);
	}
	ShowFancyAboutDialog(event:wx.CommandEvent) = {
		info = wx.AboutDialogInfo()
		InitAboutInfoWebsite(info);
		wx.AboutBox(info);
	}
	ShowFullAboutDialog(event:wx.CommandEvent) = {
		info = wx.AboutDialogInfo()
		InitAboutInfoAll(info);
		wx.AboutBox(info);
	}
	ShowCustomAboutDialog(event:wx.CommandEvent) = {
		info = wx.AboutDialogInfo()
		InitAboutInfoAll(info);
		dlg = MyAboutDialog(info);
		dlg.ShowModal();
	}
	ShowBusyInfo(event:wx.CommandEvent) = {
		disableAll = wx.WindowDisabler()
		info = wx.BusyInfo('Working, please wait...', self);
		repeat(18) {|i|
			//wx.Usleep(100);
			wx.GetApp().Yield();
		}
		wx.Sleep(2);
		//wx.WakeUpIdle();
	}
	ShowReplaceDialog(event:wx.CommandEvent) = {
		if (self.m_dlgReplace) {
			self.m_dlgReplace.Destroy()
			self.m_dlgReplace = nil
		} else {
			self.m_dlgReplace = wx.FindReplaceDialog(
								self,
								self.m_findData,
								'Find and replace dialog',
								wx.FR_REPLACEDIALOG
							);
			self.m_dlgReplace.Show(true);
		}
	}
	ShowFindDialog(event:wx.CommandEvent) = {
		if (self.m_dlgFind) {
			self.m_dlgFind.Destroy()
			self.m_dlgFind = nil
		} else {
			self.m_dlgFind = wx.FindReplaceDialog(
								self,
								self.m_findData,
								'Find dialog',
								// just for testing
								wx.FR_NOWHOLEWORD
							);
			self.m_dlgFind.Show(true);
		}
	}
	OnFindDialog(event:wx.FindDialogEvent) = {
		type = event.GetEventType();
		if (type == wx.EVT_FIND.GetEventType() || \
					type == wx.EVT_FIND_NEXT.GetEventType()) {
			wx.LogMessage("Find %s'%s' (flags: %s)",
						cond(type == wx.EVT_FIND_NEXT.GetEventType(), 'next ', ''),
						event.GetFindString(),
						DecodeFindDialogEventFlags(event.GetFlags()));
		} elsif (type == wx.EVT_FIND_REPLACE.GetEventType() || \
					type == wx.EVT_FIND_REPLACE_ALL.GetEventType()) {
			wx.LogMessage("Replace %s'%s' with '%s' (flags: %s)",
						cond(type == wx.EVT_FIND_REPLACE_ALL.GetEventType(), 'all ', ''),
						event.GetFindString(),
						event.GetReplaceString(),
						DecodeFindDialogEventFlags(event.GetFlags()));
		} elsif (type == wx.EVT_FIND_CLOSE.GetEventType()) {
			dlg = event.GetDialog();
			if (dlg == self.m_dlgFind ) {
				txt = 'Find';
				idMenu = DIALOGS_FIND;
				self.m_dlgFind = nil;
			} elsif (dlg == self.m_dlgReplace) {
				txt = 'Replace';
				idMenu = DIALOGS_REPLACE;
				self.m_dlgReplace = nil;
			} else {
				txt = 'Unknown';
				idMenu = wx.ID_ANY;
				wx.FAIL_MSG('unexpected event');
			}
			wx.LogMessage('%s dialog is being closed.', txt);
			if (idMenu != wx.ID_ANY) {
				self.GetMenuBar().Check(idMenu, false);
			}
			dlg.Destroy();
		} else {
			wx.LogError('Unknown find dialog event!');
		}
	}
}

// ----------------------------------------------------------------------------
// MyCanvas
// ----------------------------------------------------------------------------

MyCanvas = class(wx.ScrolledWindow) {
	__init__(parent:wx.Window) = {|parent, wx.ID_ANY, wx.DefaultPosition, wx.DefaultSize, wx.NO_FULL_REPAINT_ON_RESIZE|
		self.EventTable()
	}
	EventTable() = {
		//self.Bind(wx.EVT_PAINT) {|event| self.OnPaint(event) }
	}
	OnPaint(event:wx.PaintEvent) = {
		wx.PaintDC(self) {|dc|
			dc.SetFont(wx.GetApp().m_canvasFont);
			dc.SetTextForeground(wx.GetApp().m_canvasTextColour);
			dc.SetBackgroundMode(wx.TRANSPARENT);
			dc.DrawText('wxWidgets common dialogs' \
						' test application', 10, 10)
		}
	}
}

// ----------------------------------------------------------------------------
// MyModelessDialog
// ----------------------------------------------------------------------------

MyModelessDialog = class(wx.Dialog) {
	__init__(parent:wx.Window) = {
		self.EventTable()
		sizerTop = wx.BoxSizer(wx.VERTICAL);
		btn = wx.Button(self, DIALOGS_MODELESS_BTN, 'Press me');
		check = wx.CheckBox(self, wx.ID_ANY, 'Should be disabled');
		check.Disable();
		sizerTop.Add(btn, wx.SizerFlags(1).Expand().Border(wx.ALL, 5))
		sizerTop.Add(check, wx.SizerFlags(1).Expand().Border(wx.ALL, 5))
		self.SetSizer(sizerTop);
		sizerTop.SetSizeHints(self);
		sizerTop.Fit(self);
	}
	EventTable() = {
		self.Bind(wx.EVT_BUTTON, DIALOGS_MODELESS_BTN) {|event| self.OnButton(event) }
		self.Bind(wx.EVT_CLOSE) {|event| self.OnClose(event) }
	}
	OnButton(event:wx.CommandEvent) = {
		wx.MessageBox('Button pressed in modeless dialog', 'Info',
						wx.OK | wx.ICON_INFORMATION, self);
	}
	OnClose(event:wx.CloseEvent) = {
		if (event.CanVeto()) {
			wx.MessageBox('Use the menu item to close this dialog',
						'Modeless dialog',
						wx.OK | wx.ICON_INFORMATION, self);
			event.Veto();
		}
	}
}

// ----------------------------------------------------------------------------
// MyModalDialog
// ----------------------------------------------------------------------------

MyModalDialog = class(wx.Dialog) {
	__init__(parent:wx.Window) = {
		self.EventTable()
		sizerTop = wx.BoxSizer(wx.HORIZONTAL);
		self.m_btnModal = wx.Button(self, wx.ID_ANY, '&Modal dialog...');
		self.m_btnModeless = wx.Button(self, wx.ID_ANY, 'Mode&less dialog');
		self.m_btnDelete = wx.Button(self, wx.ID_ANY, '&Delete button');
		wxButton *btnOk = wx.Button(self, wx.ID_CANCEL, '&Close');
		sizerTop.Add(self.m_btnModal, wx.SizerFlags(0).Align(wx.ALIGN_CENTER).Border(wx.ALL, 5));
		sizerTop.Add(self.m_btnModeless, wx.SizerFlags(0).Align(wx.ALIGN_CENTER).Border(wx.ALL, 5));
		sizerTop.Add(self.m_btnDelete, wx.SizerFlags(0).Align(wx.ALIGN_CENTER).Border(wx.ALL, 5));
		sizerTop.Add(btnOk, wx.SizerFlags(0).Align(wx.ALIGN_CENTER).Border(wx.ALL, 5));
		self.SetSizer(sizerTop);
		sizerTop.SetSizeHints(self);
		sizerTop.Fit(self);
		self.m_btnModal.SetFocus();
		self.m_btnModal.SetDefault();
	}
	EventTable() = {
		self.Bind(wx.EVT_BUTTON) {|event| self.OnButton(event) }
	}
	OnButton(event:wx.CommandEvent) = {
		if (event.GetEventObject() == self.m_btnDelete) {
			self.m_btnModal.Destroy()
			self.m_btnModal = nil
			self.m_btnDelete.Disable();
		} elsif (event.GetEventObject() == self.m_btnModal) {
			wx.GetTextFromUser('Dummy prompt',
							'Modal dialog called from dialog',
							wx.EmptyString, self);
		} elsif (event.GetEventObject() == self.m_btnModeless) {
			MyModelessDialog(self).Show();
		} else {
			event.Skip();
		}
	}
}

// ----------------------------------------------------------------------------
// SettingsDialog
// ----------------------------------------------------------------------------

SettingsDialog = class(wx.PropertySheetDialog) {
	[
		ID_SHOW_TOOLTIPS,
		ID_AUTO_SAVE,
		ID_AUTO_SAVE_MINS,
		ID_LOAD_LAST_PROJECT,
		ID_APPLY_SETTINGS_TO,
		ID_BACKGROUND_STYLE,
		ID_FONT_SIZE
	] = 100..
	__init__(parent:wx.Window, dialogType:number) = \
			{|parent, wx.ID_ANY, 'Preferences', wx.DefaultPosition, wx.DefaultSize,
			  (wx.DEFAULT_DIALOG_STYLE | wx.RESIZE_BORDER)|
		self.SetExtraStyle(wx.DIALOG_EX_CONTEXTHELP | wx.WS_EX_VALIDATE_RECURSIVELY);
		tabImage1 = -1;
		tabImage2 = -1;
		useToolBook = (dialogType == DIALOGS_PROPERTY_SHEET_TOOLBOOK || dialogType == DIALOGS_PROPERTY_SHEET_BUTTONTOOLBOOK);
		resizeBorder = wx.RESIZE_BORDER;
		if (useToolBook) {
			resizeBorder = 0;
			tabImage1 = 0;
			tabImage2 = 1;
			sheetStyle = wx.PROPSHEET_SHRINKTOFIT;
			if (dialogType == DIALOGS_PROPERTY_SHEET_BUTTONTOOLBOOK) {
				sheetStyle |= wx.PROPSHEET_BUTTONTOOLBOOK;
			} else {
				sheetStyle |= wx.PROPSHEET_TOOLBOOK;
			}
			self.SetSheetStyle(sheetStyle);
			self.SetSheetInnerBorder(0);
			self.SetSheetOuterBorder(0);
			// create a dummy image list with a few icons
			imageSize = wx.Size(32, 32);
			self.m_imageList = wx.ImageList(imageSize.GetWidth(), imageSize.GetHeight());
			self.m_imageList.Add(wx.ArtProvider.GetIcon(wx.ART_INFORMATION, wx.ART_OTHER, imageSize));
			self.m_imageList.Add(wx.ArtProvider.GetIcon(wx.ART_QUESTION, wx.ART_OTHER, imageSize));
			self.m_imageList.Add(wx.ArtProvider.GetIcon(wx.ART_WARNING, wx.ART_OTHER, imageSize));
			self.m_imageList.Add(wx.ArtProvider.GetIcon(wx.ART_ERROR, wx.ART_OTHER, imageSize));
		} else {
			self.m_imageList = nil
		}
		// If using a toolbook, also follow Mac style and don't create buttons
		if (!useToolBook) {
			self.CreateButtons(wx.OK | wx.CANCEL | wx.HELP)
		}
		notebook = self.GetBookCtrl();
		notebook.SetImageList(self.m_imageList);
		generalSettings = self.CreateGeneralSettingsPage(notebook);
		aestheticSettings = self.CreateAestheticSettingsPage(notebook);
		notebook.AddPage(generalSettings, 'General', true, tabImage1);
		notebook.AddPage(aestheticSettings, 'Aesthetics', false, tabImage2);
		self.LayoutDialog();
	}
	CreateGeneralSettingsPage(parent:wx.Window) = {
		panel = wx.Panel(parent, wx.ID_ANY);
		topSizer = wx.BoxSizer(wx.VERTICAL);
		item0 = wx.BoxSizer(wx.VERTICAL);
		//// LOAD LAST FILE
		itemSizer3 = wx.BoxSizer(wx.HORIZONTAL);
		checkBox3 = wx.CheckBox(panel, ID_LOAD_LAST_PROJECT, '&Load last project on startup', wx.DefaultPosition, wx.DefaultSize);
		itemSizer3.Add(checkBox3, wx.SizerFlags(0).Align(wx.ALIGN_CENTER_VERTICAL).Border(wx.ALL, 5));
		item0.Add(itemSizer3, wx.SizerFlags(0).Expand().Border(wx.ALL, 0));
		//// AUTOSAVE
		autoSaveLabel = '&Auto-save every';
		minsLabel = 'mins';
		itemSizer12 = wx.BoxSizer(wx.HORIZONTAL);
		checkBox12 = wx.CheckBox(panel, ID_AUTO_SAVE, autoSaveLabel, wx.DefaultPosition, wx.DefaultSize);
		spinCtrl12 = wx.SpinCtrl(panel, ID_AUTO_SAVE_MINS, wx.EmptyString,
			wx.DefaultPosition, wx.Size(40, wx.DefaultCoord), wx.SP_ARROW_KEYS, 1, 60, 1);
		itemSizer12.Add(checkBox12, wx.SizerFlags(0).Align(wx.ALIGN_CENTER_VERTICAL).Border(wx.ALL, 5));
		itemSizer12.Add(spinCtrl12, wx.SizerFlags(0).Align(wx.ALIGN_CENTER_VERTICAL).Border(wx.ALL, 5));
		itemSizer12.Add(wx.StaticText(panel, wx.ID_STATIC, minsLabel), wx.SizerFlags(0).Align(wx.ALIGN_CENTER_VERTICAL).Border(wx.ALL, 5));
		item0.Add(itemSizer12, wx.SizerFlags(0).Expand().Border(wx.ALL, 0));
		//// TOOLTIPS
		itemSizer8 = wx.BoxSizer(wx.HORIZONTAL);
		checkBox6 = wx.CheckBox(panel, ID_SHOW_TOOLTIPS, 'Show &tooltips', wx.DefaultPosition, wx.DefaultSize);
		itemSizer8.Add(checkBox6, wx.SizerFlags(0).Align(wx.ALIGN_CENTER_VERTICAL).Border(wx.ALL, 5));
		item0.Add(itemSizer8, wx.SizerFlags(0).Expand().Border(wx.ALL, 0));
		topSizer.Add(item0, wx.SizerFlags(1).Expand().Align(wx.ALIGN_CENTRE).Border(wx.ALL, 5))
		panel.SetSizer(topSizer);
		topSizer.Fit(panel);
		panel;
	}
	CreateAestheticSettingsPage(parent:wx.Window) = {
		panel = wx.Panel(parent, wx.ID_ANY);
		topSizer = wx.BoxSizer(wx.VERTICAL);
		item0 = wx.BoxSizer(wx.VERTICAL);
		//// PROJECT OR GLOBAL
		globalOrProjectChoices = [
			'&New projects',
			'&This project'
		]
		projectOrGlobal = wx.RadioBox(panel, ID_APPLY_SETTINGS_TO, '&Apply settings to:',
			wx.DefaultPosition, wx.DefaultSize, globalOrProjectChoices);
		item0.Add(projectOrGlobal, wx.SizerFlags(0).Expand().Border(wx.ALL, 5));
		projectOrGlobal.SetSelection(0);
		//// BACKGROUND STYLE
		backgroundStyleChoices = [
			'Colour',
			'Image'
		]
		staticBox3 = wx.StaticBox(panel, wx.ID_ANY, 'Background style:');
		styleSizer = wx.StaticBoxSizer(staticBox3, wx.VERTICAL);
		item0.Add(styleSizer, wx.SizerFlags(0).Expand().Border(wx.ALL, 5));
		itemSizer2 = wx.BoxSizer(wx.HORIZONTAL);
		choice2 = wx.Choice(panel, ID_BACKGROUND_STYLE, wx.DefaultPosition, wx.DefaultSize, backgroundStyleChoices);
		itemSizer2.Add(wx.StaticText(panel, wx.ID_ANY, '&Window:'), wx.SizerFlags(0).Align(wx.ALIGN_CENTER_VERTICAL).Border(wx.ALL, 5));
		itemSizer2.AddSpacer(5, 5, 1, wx.ALL, 0);
		itemSizer2.Add(choice2, wx.SizerFlags(0).Align(wx.ALIGN_CENTER_VERTICAL).Border(wx.ALL, 5));
		styleSizer.Add(itemSizer2, wx.SizerFlags(0).Expand().Border(wx.ALL, 5));
		//// FONT SIZE SELECTION
		staticBox1 = wx.StaticBox(panel, wx.ID_ANY, 'Tile font size:');
		itemSizer5 = wx.StaticBoxSizer(staticBox1, wx.HORIZONTAL);
		spinCtrl = wx.SpinCtrl(panel, ID_FONT_SIZE, wx.EmptyString, wx.DefaultPosition,
			wx.Size(80, wx.DefaultCoord));
		itemSizer5.Add(spinCtrl, wx.SizerFlags(0).Align(wx.ALIGN_CENTER_HORIZONTAL | wx.ALIGN_CENTER_VERTICAL).Border(wx.ALL, 5));
		item0.Add(itemSizer5, wx.SizerFlags(0).Expand().Border(wx.LEFT | wx.RIGHT, 5));
		topSizer.Add(item0, wx.SizerFlags(1).Expand().Align(wx.ALIGN_CENTRE).Border(wx.ALL, 5));
		topSizer.AddSpacer(5);
		panel.SetSizer(topSizer);
		topSizer.Fit(panel);
		panel;
	}
};

// ----------------------------------------------------------------------------
// utility functions
// ----------------------------------------------------------------------------

InitAboutInfoMinimal(info:wx.AboutDialogInfo) = {
	info.SetName('Dialogs Sample');
	info.SetVersion(wx.VERSION_NUM_DOT_STRING);
	info.SetDescription('This sample shows different wxWidgets dialogs');
	info.SetCopyright('(C) 1998-2006 wxWidgets dev team');
	info.AddDeveloper('Vadim Zeitlin');
}

InitAboutInfoWebsite(info:wx.AboutDialogInfo) = {
	InitAboutInfoMinimal(info);
	info.SetWebSite('http://www.wxwidgets.org/', 'wxWidgets web site');
}

InitAboutInfoAll(info:wx.AboutDialogInfo) = {
	InitAboutInfoMinimal(info);
	// we can add a second developer
	info.AddDeveloper('A.N. Other');
	// or we can add several persons at once like this
	docwriters = [
		'First D. Writer',
		'Second One',
	]
	info.SetDocWriters(docwriters)
	info.SetLicence(R'''
				wxWindows Library Licence, Version 3.1
				======================================

  Copyright (c) 1998-2005 Julian Smart, Robert Roebling et al

  Everyone is permitted to copy and distribute verbatim copies
  of this licence document, but changing it is not allowed.

					   WXWINDOWS LIBRARY LICENCE
	 TERMS AND CONDITIONS FOR COPYING, DISTRIBUTION AND MODIFICATION

					...and so on and so forth...
'''
	)
	info.AddTranslator('Wun Ngo Wen (Martian)');
}

DecodeFindDialogEventFlags(flags:number) = {
	cond((flags & wx.FR_DOWN) != 0, 'down', 'up') + ', ' \
	+ cond((flags & wx.FR_WHOLEWORD) != 0, 'whole words only, ', '') \
	+ cond((flags & wx.FR_MATCHCASE) != 0, '', 'not ') \
	+ 'case sensitive'
}

wx.IMPLEMENT_APP(MyApp)
