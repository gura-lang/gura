#!/usr/bin/env gura
import(re)

Entry = struct(pattern:re.pattern, func:function)

Tokenizer = class {
	__init__() = {
		this.mode = `start
		this.entriesDict = %{}
	}
	SetRule() {`block} = {
		this.entriesDict[`start] = this.ParseBlock(block)
	}
	SetMode(mode:symbol) = {
		this.mode = mode
	}
	Tokenize(stream:stream) = {
		raw = 0
		readlines(stream) {|lineBuff|
			col = 0
			while (col < lineBuff.len()) {
				this.match = nil
				for (entry in this.entriesDict[this.mode]) {
					if (this.match = entry.pattern.match(lineBuff, col)) {
						entry.func(this)
						col = this.match.end(0)
						break
					}
				}
				if (!this.match) {
					raise(ValueError,
						"unrecognizable token at %d:%d" % [raw + 1, col + 1])
					break
				}
			}
			raw += 1
		}
	}
	ParseBlock(block:expr) = {
		block.each():xlist {|expr|
			if (!expr.isdictassign()) {
				raise(SyntaxError, "invalid element")
			} elsif (expr.left().isquote()) {
				exprLeft = expr.left().unquote()
				this.entriesDict[exprLeft.getsymbol()] = this.ParseBlock(expr.right())
				nil
			} else {
				Entry(re.pattern('^' + expr.left().getstring()), expr.right().tofunction())
			}
		}
	}
}
