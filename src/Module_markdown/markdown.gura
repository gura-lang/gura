#!/usr/bin/env gura
import(markdown):binary:mixin_type {*}

Renderer_html = class {
	__init__(out?:stream:w) = {
		this.out = out || sys.stdout
	}
	Output(doc:document):void = {
		this.OutputItem(doc.root)
	}
	OutputItem(item:item):map:void = {
		if (item.type == `root) {
			this.out.print('<html>\n')
			this.out.print('<body>\n')
			this.OutputItem(item.children)
			this.out.print('</body>\n')
			this.out.print('</html>\n')
		} elsif (item.type == `h1) {
			this.out.print('<h1>')
			this.OutputItem(item.children)
			this.out.print('</h1>\n')
		} elsif (item.type == `h2) {
			this.out.print('<h2>')
			this.OutputItem(item.children)
			this.out.print('</h2>\n')
		} elsif (item.type == `h3) {
			this.out.print('<h3>')
			this.OutputItem(item.children)
			this.out.print('</h3>\n')
		} elsif (item.type == `h4) {
			this.out.print('<h4>')
			this.OutputItem(item.children)
			this.out.print('</h4>\n')
		} elsif (item.type == `h5) {
			this.out.print('<h5>')
			this.OutputItem(item.children)
			this.out.print('</h5>\n')
		} elsif (item.type == `h6) {
			this.out.print('<h6>')
			this.OutputItem(item.children)
			this.out.print('</h6>\n')
		} elsif (item.type == `p) {
			this.out.print('<p>\n')
			this.OutputItem(item.children)
			this.out.println()
			this.out.print('</p>\n')
		} elsif (item.type == `blockquote) {
			this.out.print('<blockquote>\n')
			this.OutputItem(item.children)
			this.out.println()
			this.out.print('</blockquote>\n')
		} elsif (item.type == `em) {
			this.out.print('<em>')
			this.OutputItem(item.children)
			this.out.print('</em>')
		} elsif (item.type == `strong) {
			this.out.print('<strong>')
			this.OutputItem(item.children)
			this.out.print('</strong>')
		} elsif (item.type == `codeblock) {
			this.out.print('<pre><code>')
			this.OutputItem(item.children)
			this.out.print('</code></pre>\n')
		} elsif (item.type == `ol) {
			this.out.print('<ol>\n')
			this.OutputItem(item.children)
			this.out.print('</ol>\n')
		} elsif (item.type == `ul) {
			this.out.print('<ul>\n')
			this.OutputItem(item.children)
			this.out.print('</ul>\n')
		} elsif (item.type == `li) {
			this.out.print('<li>')
			this.OutputItem(item.children)
			this.out.print('</li>\n')
		} elsif (item.type == `line) {
			this.OutputItem(item.children)
			this.out.print('\n')
		} elsif (item.type == `a) {
			this.out.print('<a')
			item.url && this.out.print(' href="', item.url.encodeuri(), '"')
			item.title && this.out.print(' title="', item.title.escapehtml(), '"')
			this.out.print('>')
			this.OutputItem(item.children)
			this.out.print('</a>')
		} elsif (item.type == `img) {
			this.out.print('<img')
			item.url && this.out.print(' src="', item.url.encodeuri(), '"')
			item.text && this.out.print(' alt="', item.text.escapehtml(), '"')
			item.title && this.out.print(' title="', item.title.escapehtml(), '"')
			this.out.print('>')
		} elsif (item.type == `text) {
			this.out.print(item.text.escapehtml())
		} elsif (item.type == `code) {
			this.out.print('<code>')
			this.out.print(item.text.escapehtml())
			this.out.print('</code>')
		} elsif (item.type == `entity) {
			this.out.print('&', item.text.escapehtml(), ';')
		} elsif (item.type == `hr) {
			this.out.print('<hr />\n')
		} elsif (item.type == `br) {
			this.out.print('<br />\n')
		}
	}
}

Renderer_rtf = class {
	__init__(out?:stream:w) = {
		this.out = out || sys.stdout
		this.quoteLevel = 0
		this.indentPerQuote = 720
	}
	Output(doc:document):void = {
		this.OutputItem(doc.root)
	}
	OutputItem(item:item):map:void = {
		if (item.type == `root) {
			this.out.println(r'{\rtf')
			this.OutputItem(item.children)
			this.out.println('}')
		} elsif (item.type == `h1) {
			this.out.print(r'{\pard \ql \f0 \sa180 \li0 \fi0 \b \fs36 ')
			this.OutputItem(item.children)
			this.out.println(r'\par}')
		} elsif (item.type == `h2) {
			this.out.print(r'{\pard \ql \f0 \sa180 \li0 \fi0 \b \fs32 ')
			this.OutputItem(item.children)
			this.out.println(r'\par}')
		} elsif (item.type == `h3) {
			this.out.print(r'{\pard \ql \f0 \sa180 \li0 \fi0 \b \fs28 ')
			this.OutputItem(item.children)
			this.out.println(r'\par}')
		} elsif (item.type == `h4) {
			this.out.print(r'{\pard \ql \f0 \sa180 \li0 \fi0 \b \fs24 ')
			this.OutputItem(item.children)
			this.out.println(r'\par}')
		} elsif (item.type == `h5) {
			this.out.print(r'{\pard \ql \f0 \sa180 \li0 \fi0 \b \fs20 ')
			this.OutputItem(item.children)
			this.out.println(r'\par}')
		} elsif (item.type == `h6) {
			this.out.print(r'{\pard \ql \f0 \sa180 \li0 \fi0 \b \fs16 ')
			this.OutputItem(item.children)
			this.out.println(r'\par}')
		} elsif (item.type == `p) {
			this.out.printf(r'{\pard \ql \f0 \sa180 \li%d \fi0 ',
									this.quoteLevel * this.indentPerQuote)
			this.OutputItem(item.children)
			this.out.println()
			this.out.println(r'\par}')
		} elsif (item.type == `blockquote) {
			this.quoteLevel += 1
			this.OutputItem(item.children)
			this.out.println()
			this.quoteLevel -= 1
		} elsif (item.type == `em) {
			this.out.print(r'{\i ')
			this.OutputItem(item.children)
			this.out.print(r'}')
		} elsif (item.type == `strong) {
			this.out.print(r'{\b ')
			this.OutputItem(item.children)
			this.out.print(r'}')
		} elsif (item.type == `codeblock) {
			this.out.print(r'{\pard \ql \f0 \sa180 \li0 \fi0 \f1 ')
			item.children.each {|itemChild, i|
				(i > 0) && this.out.println(r'\line')
				this.OutputItem(itemChild.children)
			}
			this.out.println(r'\par}')
		} elsif (item.type == `ol) {
			this.OutputItem(item.children)
		} elsif (item.type == `ul) {
			this.OutputItem(item.children)
		} elsif (item.type == `li) {
			this.out.print(r'{\pard \ql \f0 \sa0 \li360 \fi-360 \bullet \tx360\tab ')
			this.OutputItem(item.children)
			this.out.println(r'\par}')
		} elsif (item.type == `line) {
			this.OutputItem(item.children)
			this.out.println(r'\line')
		} elsif (item.type == `a) {
			this.out.println(r'{\field{\*\fldinst{HYPERLINK "%s"}}{\fldrslt{\ul' % this.EscapeText(item.url))
			this.OutputItem(item.children)
			this.out.println(r'}}}')
		} elsif (item.type == `img) {
			//this.out.print('<img')
			//item.url && this.out.print(' src="', item.url.encodeuri(), '"')
			//item.text && this.out.print(' alt="', item.text.escapehtml(), '"')
			//item.title && this.out.print(' title="', item.title.escapehtml(), '"')
			//this.out.print('>')
		} elsif (item.type == `text) {
			this.out.print(this.EscapeText(item.text))
		} elsif (item.type == `code) {
			this.out.print(r'{\f1 ')
			this.out.print(this.EscapeText(item.text))
			this.out.print(r'}')
		} elsif (item.type == `entity) {
			this.out.print(r'\u%d?', 169)
		} elsif (item.type == `hr) {
			this.out.println(r'{\pard \qc \f0 \sa180 \li0 \fi0 \emdash\emdash\emdash\emdash\emdash\par}')
		} elsif (item.type == `br) {
			this.out.println(r'\line')
		}
	}
	EscapeText(text:string):static = {
		text.replace('\\', '\\\\').replace('{', r'\{').replace('}', r'\}')
	}
}

Renderer_console = class {
	__init__() = {
	}
	Output(doc:document):void = {
		this.OutputItem(doc.root)
	}
	OutputItem(item:item):map:void = {
		if (item.type == `root) {
			this.OutputItem(item.children)
		} elsif (item.type == `h1) {
			println()
			conio.setcolor(`green) {
				this.OutputItem(item.children)
			}
			println()
		} elsif (item.type == `h2) {
			println()
			conio.setcolor(`green) {
				this.OutputItem(item.children)
			}
			println()
		} elsif (item.type == `h3) {
			println()
			conio.setcolor(`green) {
				this.OutputItem(item.children)
			}
			println()
		} elsif (item.type == `h4) {
			println()
			conio.setcolor(`green) {
				this.OutputItem(item.children)
			}
			println()
		} elsif (item.type == `h5) {
			println()
			conio.setcolor(`green) {
				this.OutputItem(item.children)
			}
			println()
		} elsif (item.type == `h6) {
			println()
			conio.setcolor(`green) {
				this.OutputItem(item.children)
			}
			println()
		} elsif (item.type == `p) {
			println()
			this.OutputItem(item.children)
			println()
		} elsif (item.type == `blockquote) {
			println()
			this.OutputItem(item.children)
			println()
		} elsif (item.type == `em) {
			conio.setcolor(`red) {
				this.OutputItem(item.children)
			}
		} elsif (item.type == `strong) {
			conio.setcolor(`red) {
				this.OutputItem(item.children)
			}
		} elsif (item.type == `codeblock) {
			println()
			conio.setcolor(`blue) {
				this.OutputItem(item.children)
			}
		} elsif (item.type == `ol) {
			println()
			this.OutputItem(item.children)
		} elsif (item.type == `ul) {
			println()
			this.OutputItem(item.children)
		} elsif (item.type == `li) {
			print('- ')
			this.OutputItem(item.children)
			println()
		} elsif (item.type == `line) {
			print('|   ')
			this.OutputItem(item.children)
			println()
		} elsif (item.type == `a) {
			this.OutputItem(item.children)
		} elsif (item.type == `img) {
			print('[', item.text, ']')
		} elsif (item.type == `text) {
			print(item.text)
		} elsif (item.type == `code) {
			conio.setcolor(`blue) {
				print(item.text)
			}
		} elsif (item.type == `entity) {
			conio.setcolor(`black, `white) {
				print(item.text.escapehtml())
			}
		} elsif (item.type == `hr) {
			println('--------')
		} elsif (item.type == `br) {
			println()
		}
	}
}

operator(`<<).assign(`document, `function) {|doc, func|
	doc << '**`' + func.format + '`**'
	doc << func.gethelp().text
}

classref(`document).output_html(out?:stream:w) = {
	Renderer_html(out).Output(this)
}

classref(`document).output_rtf(out?:stream:w) = {
	Renderer_rtf(out).Output(this)
}

classref(`document).output_console() = {
	Renderer_console().Output(this)
}

setpresenter {|title:string:nil, doc:document:nil|
	title && conio.setcolor(`green) {
		println(title)
	}
	doc && Renderer_console().Output(doc)
}
