#!/bin/usr/env gura

if (sys.argv.len() < 1) {
	sys.stderr.println('module name needs to be specified')
	sys.exit(1)
}
fields = path.filename(sys.argv[0]).split('.'):list
dotName = fields.join('.')
ubarName = fields.join('_')
baseName = fields[-1]
fileBuild = 'build.gura'
fileTest = 'test-' + ubarName + '.gura'
fileAfx = 'stdafx.h'
fileHdr = 'module-' + ubarName + '.h'
fileSrc = 'module-' + ubarName + '.cpp'
incOnceMacroName = '__GURA_MODULE_' + ubarName.upper()  + '_H__'
incOnceMacroNameAfx = '__GURA_MODULE_' + ubarName.upper()  + '_STDAFX_H__'
if (path.exists([fileBuild, fileTest, fileAfx, fileHdr, fileSrc]).or()) {
	println('file already exists')
	sys.exit(1)
}

//======== Generate Build Script ========
R'''
#!/usr/bin/env gura
import(modbuild)

builder = modbuild.Builder()
builder.build('${dotName}', ['module-${ubarName}.cpp'])
'''.template(open(fileBuild, 'w').addcr(false))
fs.chmod('a+x', fileBuild)

//======== Generate Test Script ========
R'''
#!/usr/bin/env gura
import(${dotName})

println(${dotName}.test(3, 4))
'''.template(open(fileTest, 'w').addcr(false))
fs.chmod('a+x', fileTest)

//======== Generate Precompiled Header File ========
R'''
#ifndef ${incOnceMacroNameAfx}
#define ${incOnceMacroNameAfx}
#include "${fileHdr}"
#endif
'''.template(open(fileAfx, 'w').addcr(false))

//======== Generate Header File ========
R'''
//-----------------------------------------------------------------------------
// Gura ${dotName} module
//-----------------------------------------------------------------------------
#ifndef ${incOnceMacroName}
#define ${incOnceMacroName}
#include <gura.h>

Gura_BeginModuleHeader(${ubarName})
Gura_EndModuleHeader(${ubarName})

#endif
'''.template(open(fileHdr, 'w').addcr(false))

//======== Generate Source File ========
R'''
//-----------------------------------------------------------------------------
// Gura ${dotName} module
//-----------------------------------------------------------------------------
#include "${fileAfx}"

Gura_BeginModuleBody(${ubarName})

//-----------------------------------------------------------------------------
// Gura module functions: ${dotName}
//-----------------------------------------------------------------------------
// ${dotName}.test(num1:number, num2:number)
Gura_DeclareFunction(test)
{
	SetMode(RSLTMODE_Normal, FLAG_None);
	DeclareArg(env, "num1", VTYPE_number);
	DeclareArg(env, "num2", VTYPE_number);
	AddHelp(Gura_Symbol(en), Help::FMT_markdown,
	"This function adds two numbers and returns the result."
	);
}

Gura_ImplementFunction(test)
{
	return Value(args.GetNumber(0) + args.GetNumber(1));
}

//-----------------------------------------------------------------------------
// Module entry
//-----------------------------------------------------------------------------
Gura_ModuleEntry()
{
	// function assignment
	Gura_AssignFunction(test);
}

Gura_ModuleTerminate()
{
}

Gura_EndModuleBody(${ubarName}, ${baseName})

Gura_RegisterModule(${ubarName})
'''.template(open(fileSrc, 'w').addcr(false))

R'''
Following files were generated.
- Build Script: ${fileBuild}
- Test Script:  ${fileTest}
- Source Files: ${fileAfx}, ${fileHdr}, ${fileSrc}
'''.template(sys.stdout)
sys.exit(0)
