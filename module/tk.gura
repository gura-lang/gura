#!/usr/bin/env gura
//-----------------------------------------------------------------------------
// Gura tk module
// Tcl/Tk manual: http://www.tcl.tk/man/tcl8.5/
// TkDocs: http://www.tkdocs.com/index.html
//-----------------------------------------------------------------------------
import(tcl)

_interp = tcl.interp()
_tclDumpFlag = false

//-----------------------------------------------------------------------------
// Helper functions
//-----------------------------------------------------------------------------
printArg(args+) = {
	args = args.each():list {|arg|
		if (islist(arg)) {
			arg.tostring()
		} else {
			arg = arg.tostring()
			if (arg.find(' ')) {
				'{' + arg + '}'
			} else {
				arg
			}
		}
	}
	println('[', args.join(' '), ']')
}

// args opts
tclEval(args*, opts%) = {
	optsList = []
	opts.items() {|item|
		optsList.add('-' + item[0].tostring())
		optsList.add(item[1])
	}
	_tclDumpFlag && printArg(args*, optsList*)
	_interp.eval(args*, optsList*)
}

// cmds args opts
genFunction(cmds+:string) = {
	func(args*, opts%):map = {
		tclEval(cmds*, args*, opts%)
	}
	func.name = cmds.join('$')
	func
}

// cmds path args opts
genMethod(cmds+:string) = {
	func(args*, opts%):map = {
		tclEval(cmds*, this, args*, opts%)
	}
	func.name = cmds.join('$')
	func
}

// cmds path args opts
genCommand(cmds+:string) = {
	func(args*, opts%):map {block?} = {
		tclEval(cmds*, this, args*, opts%)
		block && block(this)
		this
	}
	func.symbol = cmds.join('$')
	func
}

// cmds pathChild args opts
genWidgetCreator(constructor:function, cmds+:string) = {
	func(args*, opts%):map {block?} = {
		pathChild = this.makePathChild()
		tclEval(cmds*, pathChild, args*, opts%)
		w = constructor(pathChild)
		block && block(w)
		w
	}
	func.name = cmds.join('$')
	func
}

// path cmds args opts
genWidgetMethod(cmds+:string) = {
	func(args*, opts%):map = {
		tclEval(this, cmds*, args*, opts%)
	}
	func.name = cmds.join('$')
	func
}

// container cmds id args opts
genItemMethod(container, cmds+:string) = {
	func(args*, opts%):map = {
		tclEval(container, cmds*, this, args*, opts%)
	}
	func.name = cmds.join('$')
	func
}

// container 'addtag' tag searchProc args opts
genTagMethod(container, searchProc:string) = {
	func(args*, opts%):map = {
		tclEval(container, 'addtag', this, searchProc, args*, opts%)
	}
	func.name = ['addtag', searchProc].join('$')
	func
}

makeBindScript(func:function) = {
	args = func.decls:*symbol
	cmdName = _interp.command(func)
	elems = [cmdName]
	elems.append('%' + bindSubstDict.gets(args, args:*tostring()))
	elems.join(' ')
}

bindSubstDict = %{
	`serial				=> '#'
	`above				=> 'a'
	`numbuttons			=> 'b'
	`count				=> 'c'
	`detail				=> 'd'
	`user_data			=> 'd'
	`focus				=> 'f'
	`height				=> 'h'
	`widget_num			=> 'i'
	`keycode			=> 'k'
	`mode				=> 'm'
	`override_redirect	=> 'o'
	`place				=> 'p'
	`state				=> 's'
	`time				=> 't'
	`width				=> 'w'
	`x					=> 'x'
	`y					=> 'y'
	`char				=> 'A'
	`border_width		=> 'B'
	`delta				=> 'D'
	`send_event			=> 'E'
	`keysym				=> 'K'
	`keysym_num			=> 'N'
	`root				=> 'R'
	`subwidget			=> 'S'
	`type				=> 'T'
	`widget				=> 'W'
	`x_root				=> 'X'
	`y_root				=> 'Y'
	// for validcommand
	`action				=> 'd'
	`index				=> 'i'
	`wouldbe			=> 'P'
	`current			=> 's'
	`edited				=> 'S'
	`validate			=> 'V'
}

//-----------------------------------------------------------------------------
// Widget
//-----------------------------------------------------------------------------
Widget = class {
	__init__(path:string):map = {
		this._path = path
		this._cntChildren = 0
	}
	__str__() = format('<widget:%s>', this.__tclname__())
	__getprop__(symbol:symbol) = {
		method = this.genDynamicMethod(symbol)
		this.setprop!(symbol, method)
		method
	}
	__setitem__(option:string, value) = {
		tclEval(this, 'configure', '-' + option, value)
	}
	__getitem__(option:string) = {
		tclEval(this, 'cget', '-' + option)
	}
	__tclname__() = this._path
	genDynamicMethod(symbol:symbol) = {
		str = symbol.tostring()
		if (str.startswith('wm$')) {
			method = genMethod('wm', str.mid(3))
		} elsif (str.startswith('winfo$')) {
			method = genMethod('winfo', str.mid(6))
		} elsif (str.startswith('grid$')) {
			method = genCommand('grid', str.mid(5))
		} elsif (str.startswith('place$')) {
			method = genCommand('place', str.mid(6))
		} elsif (str.startswith('tk$')) {
			method = genCommand('tk_' + str.mid(3))
		} elsif (this.widgetCreatorDict.haskey(symbol)) {
			[cmd, constructor] = this.widgetCreatorDict[symbol]
			method = genWidgetCreator(eval(constructor), cmd)
		} else {
			method = genWidgetMethod(str.split('$'):list*)
		}
		method
	}
	makePathChild() = {
		idx = this._cntChildren
		this._cntChildren += 1
		if (this._path == '.') {
			format('.c%d', idx)
		} else {
			format('%s.c%d', this._path, idx)
		}
	}
	widgetCreatorDict = %{
		`Button				=> ['button',			`Widget]
		`Canvas				=> ['canvas',			`Canvas]
		`Checkbutton		=> ['checkbutton',		`Widget]
		`Entry				=> ['entry',			`Widget]
		`Frame				=> ['frame',			`Widget]
		`Label				=> ['label',			`Widget]
		`Labelframe			=> ['labelframe',		`Widget]
		`Listbox			=> ['listbox',			`Widget]
		`Menu				=> ['menu',				`Menu]
		`Menubutton			=> ['menubutton',		`Widget]
		`Message			=> ['message',			`Widget]
		`Panedwindow		=> ['panedwindow',		`Widget]
		`Place				=> ['place',			`Widget]
		`Radiobutton		=> ['radiobutton',		`Widget]
		`Scrollbar			=> ['scrollbar',		`Widget]
		`Spinbox			=> ['spinbox',			`Widget]
		`Text				=> ['text',				`Text]
		`Toplevel			=> ['toplevel',			`Widget]
		`ttk$Button			=> ['ttk::button',		`Widget]
		`ttk$Checkbutton	=> ['ttk::checkbutton',	`Widget]
		`ttk$Combobox		=> ['ttk::combobox',	`Widget]
		`ttk$Entry			=> ['ttk::entry',		`Widget]
		`ttk$Frame			=> ['ttk::frame',		`Widget]
		`ttk$Intro			=> ['ttk::intro',		`Widget]
		`ttk$Label			=> ['ttk::label',		`Widget]
		`ttk$Labelframe		=> ['ttk::labelframe',	`Widget]
		`ttk$Menubutton		=> ['ttk::menubutton',	`Widget]
		`ttk$Notebook		=> ['ttk::notebook',	`Notebook]
		`ttk$Panedwindow	=> ['ttk::panedwindow',	`Widget]
		`ttk$Progressbar	=> ['ttk::progressbar',	`Widget]
		`ttk$Radiobutton	=> ['ttk::radiobutton',	`Widget]
		`ttk$Scale			=> ['ttk::scale',		`Widget]
		`ttk$Scrollbar		=> ['ttk::scrollbar',	`Widget]
		`ttk$Separator		=> ['ttk::separator',	`Widget]
		`ttk$Sizegrip		=> ['ttk::sizegrip',	`Widget]
		`ttk$Spinbox		=> ['ttk::spinbox',		`Widget]
		`ttk$Style			=> ['ttk::style',		`Widget]
		`ttk$Treeview		=> ['ttk::treeview',	`Treeview]
		`ttk$Widget			=> ['ttk::widget',		`Widget]
		`ttk$Image			=> ['ttk::image',		`Widget]
		`ttk$Vsapi			=> ['ttk::vsapi',		`Widget]
	}
	winfo$children() = {
		Widget(tclEval('winfo', 'children', this))
	}
	bind(trigger, func?:function) {block?} = {
		func = func || block
		if (issymbol(trigger)) {
			// `command, `xscrollcommand, `yscrollcommand, `validatecommand
			script = makeBindScript(func)
			tclEval(this, 'configure', '-' + trigger.tostring(), script)
		} elsif (isstring(trigger)) {
			script = makeBindScript(func)
			tclEval('bind', this, trigger, script)
		} else {
			raise(error.TypeError, 'symbol or string should specified for binding')
		}
	}
	assoc(trigger:symbol, func?:function) {block?} = {
		func = func || block
		tclEval(this, 'configure', '-' + trigger.tostring(), func)
	}
	destroy				= genCommand('destroy')
	focus				= genCommand('focus')
	grid				= genCommand('grid')
	lower				= genCommand('lower')
	pack				= genCommand('pack')
	place				= genCommand('place')
	raise				= genCommand('raise')
	tk$ChooseColor		= genFunction('tk_chooseColor')
	tk$ChooseDirectory	= genFunction('tk_chooseDirectory')
	tk$Dialog(title:string, text:string,
				bitmap:string => '', default:number => -1, strings[]:string => []) = {
		pathChild = this.makePathChild()
		tclEval('tk_dialog', pathChild, title, text, bitmap, default, strings*)
	}
	tk$GetOpenFile		= genFunction('tk_getOpenFile')
	tk$GetSaveFile		= genFunction('tk_getSaveFile')
	tk$MessageBox		= genFunction('tk_messageBox')
	bindafter(msec:number) {block} = {
		timer = _interp.timer()
		timer.start(msec, 0, 1) { block() }
		timer
	}
	bindtimer(msec:number, args*) {block} = {
		timer = _interp.timer()
		timer.start(msec, args*) { block(timer) }
		timer
	}
}

Mainwindow = class(Widget) {
	__init__() = {|'.'|
	}
}
mainwindow = Mainwindow

//-----------------------------------------------------------------------------
// Menu
//-----------------------------------------------------------------------------
Menu = class(Widget) {
	__init__(path:string) = {|path|
	}
	Separator(opts%)	= this.add('separator', opts%)
	Cascade(opts%) {block?} = {
		m = this.Menu(tearoff => false)
		opts[`menu] = m
		this.add('cascade', opts%)
		block && block(m)
		m
	}
	Command(opts%) {block?} = {
		if (block) { opts[`command] = block }
		this.add('command', opts%)
	}
	Checkbutton(opts%) {block?} = {
		if (block) { opts[`command] = block }
		this.add('checkbutton', opts%)
	}
	Radiobutton(opts%) {block?} = {
		if (block) { opts[`command] = block }
		this.add('radiobutton', opts%)
	}
	popup(x:number, y:number) = {
		tclEval('tk_popup', this, x, y)
	}
}

//-----------------------------------------------------------------------------
// Canvas
//-----------------------------------------------------------------------------
// path cmds args opts
genCanvasItemCreator(cmds+:string) = {
	func(args*, opts%):map {block?} = {
		id = tclEval(this, 'create', cmds*, args*, opts%)
		item = CanvasItem(this, id)
		block && block(item)
		item
	}
	func.name = cmds[0]
	func
}

Canvas = class(Widget) {
	__init__(path:string) = {|path|
		this._cntTags = 0
	}
	__getprop__(symbol:symbol) = {
		str = symbol.tostring()
		if (this.itemCreatorDict.haskey(symbol)) {
			method = genCanvasItemCreator(this.itemCreatorDict[symbol])
		} else {
			method = this.genDynamicMethod(symbol)
		}
		this.setprop!(symbol, method)
		method
	}
	itemCreatorDict = %{
		`Arc		=> 'arc'
		`Bitmap		=> 'bitmap'
		`Image		=> 'image'
		`Line		=> 'line'
		`Polygon	=> 'polygon'
		`Rectangle	=> 'rectangle'
		`Text		=> 'text'
		`Window		=> 'window'
	}
	Tag() {block?} = {
		tagName = 'tag' + this._cntTags
		this._cntTags += 1
		tag = CanvasTag(this, tagName)
		block && block(tag)
		tag
	}
}

//-----------------------------------------------------------------------------
// CanvasItem
//-----------------------------------------------------------------------------
CanvasItem = class {
	__init__(canvas:Canvas, tagOrId) = {
		this._canvas = canvas
		this._tagOrId = tagOrId
	}
	__getprop__(symbol:symbol) = {
		method = this.genDynamicMethod(symbol)
		this.setprop!(symbol, method)
		method
	}
	__setitem__(option:string, value) = {
		tclEval(this._canvas, 'itemconfigure', this, '-' + option, value)
	}
	__getitem__(option:string) = {
		tclEval(this._canvas, 'itemcget', this, '-' + option)
	}
	__tclname__() = this._tagOrId
	__str__() = format('<canvasitem:%s>', this.__tclname__())
	genDynamicMethod(symbol:symbol) = {
		if (this.methodDict.haskey(symbol)) {
			method = genItemMethod(this._canvas, this.methodDict[symbol])
		} else {
			method = genItemMethod(this._canvas, symbol)
		}
		method
	}
	bbox() = {
		rtn = tclEval(this._canvas, 'bbox', this)
		if (isstring(rtn)) {
			rtn = rtn.split(' ')
		}
		rtn::tonumber()
	}
	bind(sequence:string, func?:function) {block?} = {
		func = func || block
		script = makeBindScript(func)
		tclEval(this._canvas, 'bind', this, sequence, script)
	}
	methodDict = %{
		`configure	=> 'itemconfigure'
		`cget		=> 'itemcget'
	}
}

//-----------------------------------------------------------------------------
// CanvasTag
//-----------------------------------------------------------------------------
CanvasTag = class(CanvasItem) {
	__init__(canvas:Canvas, tag) = {|canvas, tag|}
	__getprop__(symbol:symbol) = {
		if (this.searchProcDict.haskey(symbol)) {
			method = genTagMethod(this._canvas, this.searchProcDict[symbol])
		} else {
			method = this.genDynamicMethod(symbol)
		}
		this.setprop!(symbol, method)
		method
	}
	searchProcDict = %{
		`add_above			=> 'above'
		`add_all			=> 'all'
		`add_below			=> 'below'
		`add_closest		=> 'closest'
		`add_enclosed		=> 'enclosed'
		`add_overlapping	=> 'overlapping'
		`add_withtag		=> 'withtag'
	}
}

//-----------------------------------------------------------------------------
// Text
//-----------------------------------------------------------------------------
Text = class(Widget) {
	__init__(path:string) = {|path|
		this._cntTags = 0
	}
	MakeNewTag() = {
		tagName = 'tag' + this._cntTags
		this._cntTags += 1
		TextTag(this, tagName)
	}
	Insert(args+) {block?} = {
		if (args[0].istype(`TextTag)) {
			tag = args.shift()
		} else {
			tag = this.MakeNewTag()
		}
		[index:string, chars:string] = args
		tclEval(this, 'insert', index, chars, tag)
		block && block(tag)
		tag
	}
	Replace(args+) {block?} = {
		if (args[0].istype(`TextTag)) {
			tag = args.shift()
		} else {
			tag = this.MakeNewTag()
		}
		[index1:string, index2:string, chars:string] = args
		tclEval(this, 'replace', index1, index2, chars, tag)
		block && block(tag)
		tag
	}
	Tag(args*):map {block?} = {
		if (args.isempty()) {
			tag = this.MakeNewTag()
		} else {
			if (args[0].istype(`TextTag)) {
				tag = args.shift()
			} else {
				tag = this.MakeNewTag()
			}
			tclEval(this, 'tag', 'add', tag, args*)
		}
		block && block(tag)
		tag
	}
}

//-----------------------------------------------------------------------------
// TextTag
//-----------------------------------------------------------------------------
TextTag = class {
	__init__(text:Text, tagName:string) = {
		this._text = text
		this._tagName = tagName
	}
	__getprop__(symbol:symbol) = {
		method = genItemMethod(this._text, 'tag', symbol)
		this.setprop!(symbol, method)
		method
	}
	__tclname__() = this._tagName
	__str__() = format('<texttag:%s>', this.__tclname__())
	bind(sequence:string, func?:function) {block?} = {
		func = func || block
		script = makeBindScript(func)
		tclEval(this._text, 'tag', 'bind', this, sequence, script)
	}
}

//-----------------------------------------------------------------------------
// Notebook
//-----------------------------------------------------------------------------
Notebook = class(Widget) {
	__init__(path:string) = {|path|
		this._cntTags = 0
	}
	enableTraversal() = {
		tclEval('ttk::notebook::enableTraversal', this)
	}
}

//-----------------------------------------------------------------------------
// Treeview
//-----------------------------------------------------------------------------
// path cmds args opts
Treeview = class(Widget) {
	__init__(path:string) = {|path|
		this._cntTags = 0
		this._itemDict = %{}
	}
	Item(index?:number, opts%) {block?} = {
		id = tclEval(this, 'insert', '', index || 'end', opts%)
		item = TreeviewItem(this, id)
		block && block(item)
		this._itemDict[id] = item
		item
	}
	Children() = {
		ids = tclEval(this, 'children', '')
		ids.isempty() && return
		TreeviewItem(this, ids)
	}
	Selection() = {
		ids = tclEval(this, 'selection')
		ids.isempty() && return
		this._itemDict.gets(ids)
	}
	Tag() {block?} = {
		tagName = 'tag' + this._cntTags
		this._cntTags += 1
		tag = TreeviewTag(this, tagName)
		block && block(tag)
		tag
	}
}

//-----------------------------------------------------------------------------
// TreeviewItem
//-----------------------------------------------------------------------------
TreeviewItem = class {
	__init__(treeview:Treeview, id):map = {
		this._treeview = treeview
		this._id = id
	}
	__getprop__(symbol:symbol) = {
		method = genItemMethod(this._treeview, symbol)
		this.setprop!(symbol, method)
		method
	}
	__setitem__(option:string, value) = {
		tclEval(this._treeview, 'item', this, '-' + option, value)
	}
	__getitem__(option:string) = {
		tclEval(this._treeview, 'item', this, '-' + option)
	}
	__tclname__() = this._id
	__str__() = format('<treeviewitem:%s>', this.__tclname__())
	Item(index?:number, opts%) {block?} = {
		id = tclEval(this._treeview, 'insert', this, index || 'end', opts%)
		item = TreeviewItem(this._treeview, id)
		block && block(item)
		this._treeview._itemDict[id] = item
		item
	}
	Children() = {
		ids = tclEval(this._treeview, 'children', this)
		ids.isempty() && return
		TreeviewItem(this._treeview, ids)
	}
	Parent() = {
		id = tclEval(this._treeview, 'parent', this)
		id.isempty() && return
		TreeviewItem(this._treeview, id)
	}
	Next() = {
		id = tclEval(this._treeview, 'next', this)
		id.isempty() && return
		TreeviewItem(this._treeview, id)
	}
	Prev() = {
		id = tclEval(this._treeview, 'prev', this)
		id.isempty() && return
		TreeviewItem(this._treeview, id)
	}
	configure(opts%) = {
		tclEval(this._treeview, 'item', this, opts%)
	}
	cget(option:string) = {
		tclEval(this._treeview, 'item', this, '-' + option)
	}
}

//-----------------------------------------------------------------------------
// TreeviewTag
//-----------------------------------------------------------------------------
TreeviewTag = class {
	__init__(treeview:Treeview, tagName:string):map = {
		this._treeview = treeview
		this._tagName = tagName
	}
	__getprop__(symbol:symbol) = {
		method = genItemMethod(this._treeview, 'tag', symbol)
		this.setprop!(symbol, method)
		method
	}
	__setitem__(option:string, value) = {
		tclEval(this._treeview, 'tag', 'configure', this, '-' + option, value)
	}
	__getitem__(option:string) = {
		tclEval(this._treeview, 'tag', 'configure', this, '-' + option)
	}
	__tclname__() = this._tagName
	__str__() = format('<treeviewtag:%s>', this.__tclname__())
}

//-----------------------------------------------------------------------------
// FontT
//-----------------------------------------------------------------------------
FontT = class {
	__init__(fontname:string) = {
		this._fontname = fontname
	}
	__str__() = format('<font:%s>', this.__tclname__())
	__tclname__() = this._fontname
	configure	= genMethod('font', 'configure')
	delete		= genMethod('font', 'delete')
	measure		= genMethod('font', 'measure')
	actual(args*, opts%) {`block?} = {
		rtn = tclEval('font', 'actual', this._fontname, args*, opts%)
		rtn = rtn.fold(2):list {|item|
			[tosymbol(item[0].replace('-', '')), item[1]]
		}
		opts = dict(rtn)
		block && opts.store {|block|}
		opts
	}
	metrics(args*, opts%) = {
		rtn = tclEval('font', 'metrics', this._fontname, args*, opts%)
		if (isstring(rtn)) {
			rtn = rtn.split(' ')
		}
		rtn = rtn.fold(2):list {|item|
			[tosymbol(item[0].replace('-', '')), item[1].tonumber()]
		}
		dict(rtn)
	}
	customize(opts%) = Font(this.actual().store(opts)%)
	families(args*, opts%):static = tclEval('font', 'families', args*, opts%)
	names(args*, opts%):static = tclEval('font', 'names', args*, opts%)
}

Font(opts%)				= FontT(tclEval('font', 'create', opts%))

DefaultFont:public		= FontT('TkDefaultFont')
TextFont:public			= FontT('TkTextFont')
FixedFont:public		= FontT('TkFixedFont')
MenuFont:public			= FontT('TkMenuFont')
HeadingFont:public		= FontT('TkHeadingFont')
CaptionFont:public		= FontT('TkCaptionFont')
SmallCaptionFont:public	= FontT('TkSmallCaptionFont')
IconFont:public			= FontT('TkIconFont')
TooltipFont:public		= FontT('TkTooltipFont')

//-----------------------------------------------------------------------------
// Image
//-----------------------------------------------------------------------------
Image = class {
	public {
		_imagename
	}
	__init__(type:string, opts%) = {
		this._imagename = tclEval('image', 'create', type, opts%)
	}
	// Although you may think it would be convenient that destructor of Image
	// automatically calls "image delete" Tcl command, it doesn't work correctly.
	// Tcl/Tk doesn't increment a reference counter of image objects even when
	// they are passed to a canvas object.
	__str__() = format('<image:%s>', this.__tclname__())
	__getprop__(symbol:symbol) = {
		method = genWidgetMethod(symbol)
		this.setprop!(symbol, method)
		method
	}
	__tclname__() = this._imagename
	delete	= genMethod('image', 'delete')
	height	= genMethod('image', 'height')
	inuse	= genMethod('image', 'inuse')
	type	= genMethod('image', 'type')
	width	= genMethod('image', 'width')
	names():static = tclEval('image', 'names').split(' '):list
	types():static = tclEval('image', 'types').split(' '):list
}

Bitmap(opts%) = Image('bitmap', opts%)
Photo(opts%) = Image('photo', opts%)

// expansion to image class
classref(`root.image) {|ref|
	ref.to_tk() = {
		imgTk = tk.Photo()
		this.writetcl(_interp, imgTk._imagename)
		imgTk
	}
}

//-----------------------------------------------------------------------------
// other functions
//-----------------------------------------------------------------------------
bell					= genFunction('bell')
bind					= genFunction('bind')
bindtags				= genFunction('bindtags')
tkerror					= genFunction('tkerror')
update					= genFunction('update')

winfo$atom				= genFunction('winfo', 'atom')
winfo$atomname			= genFunction('winfo', 'atomname')
winfo$containing		= genFunction('winfo', 'containing')
winfo$interps			= genFunction('winfo', 'interps')
winfo$pathname			= genFunction('winfo', 'pathname')

variable:public			= _interp.variable

tclLibrary:public		= variable(varName => 'tcl_library')
tclVersion:public		= variable(varName => 'tcl_version')
tclPatchLevel:public	= variable(varName => 'tcl_patchLevel')

tkLibrary:public		= variable(varName => 'tk_library')
tkVersion:public		= variable(varName => 'tk_version')
tkPatchLevel:public		= variable(varName => 'tk_patchLevel')
tkStrictMotif:public	= variable(varName => 'tk_strictMotif')
tkTextRedraw:public		= variable(varName => 'tk_textRedraw')
tkTextRelayout:public	= variable(varName => 'tk_textRelayout')

tk$FocusFollowsMouse	= genFunction('tk_focusFollowsMouse')
tk$SetPalette			= genFunction('tk_setPalette')
tk$bisque				= genFunction('tk_bisque')

tkwait$variable			= genFunction('tkwait', 'variable')
tkwait$visibility		= genFunction('tkwait', 'visibility')
tkwait$window			= genFunction('tkwait', 'window')

mainloop				= tcl.Tk_MainLoop

tclDump(flag:boolean => true) = { _tclDumpFlag:extern = flag }
