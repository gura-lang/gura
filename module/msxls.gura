#!/usr/bin/env gura
#------------------------------------------------------------------------------
# MS-Excel simple interface
#------------------------------------------------------------------------------
import(mswin)

progid = 'Excel.Application'

reader(filename:string, sheetindex => 1) = {
	app = mswin.ole(progid)
	wb = app.Workbooks.Open(path.absname(filename))
	ws = wb.WorkSheets(sheetindex)
	lastCell = ws.Range('A1').SpecialCells(app.xlCellTypeLastCell)
	[nCols, nRows] = [lastCell.Column, lastCell.Row]
	iter = for (iRow in 1..nRows):iter {
		for (iCol in 1..nCols):list {
			ws.Cells(iRow, iCol).Value
		}
	}
	iter
}

writer = class {
	__init__(filename:string, sheetindex => 1, growvertFlag => true) = {
		this.filename = filename
		this.growvertFlag = growvertFlag
		this.app = mswin.ole(progid)
		this.wb = this.app.Workbooks.Add()
		this.ws = this.wb.WorkSheets(sheetindex)
		this.start(1, 1)
	}
	__del__() = {
		this.close()
	}
	start(iRow:number => 1, iCol:number => 1) = {
		[this.iRow, this.iCol] = [iRow, iCol]
		this.nEntries = 0
	}
	start.help = 'Set start point to write values.'
	write(fields+):map = {
		if (this.growvertFlag) {
			for (field in fields) {|idx|
				this.ws.Cells(this.iRow + this.nEntries, this.iCol + idx).Value = field
			}
		} else {
			for (field in fields) {|idx|
				this.ws.Cells(this.iRow + idx, this.iCol + this.nEntries).Value = field
			}
		}
		this.nEntries += 1
	}
	write.help = 'Append value of fields into Excel sheet.'
	clear() = {
		lastCell = this.ws.Range('A1').SpecialCells(this.app.xlCellTypeLastCell)
		this.ws.Range(this.ws.Cells(1, 1), lastCell).ClearContents()
		this.nEntries = 0
	}
	clear.help = 'Clear contents of Excel sheet.'
	autofit() = {
		lastCell = ws.Range('A1').SpecialCells(app.xlCellTypeLastCell)
		for (iCol in 1..lastCell.Column) {
			this.ws.Columns(iCol).EntireColumn().AutoFit()
		}
	}
	autofit.help = 'Fits column width by calculating maximum width of each column.'
	close() = {
		this.wb.SaveAs(path.absname(this.filename))
		this.wb.Close(0)
		this.app.Quit()
	}
	close.help = 'Save file and exit Excel application.'
}
