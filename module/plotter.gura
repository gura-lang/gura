#!/usr/bin/env gura
import(cairo)

//-----------------------------------------------------------------------------
// Coord
//-----------------------------------------------------------------------------
Coord = struct(x:number, y:number):map

//-----------------------------------------------------------------------------
// Plot
//-----------------------------------------------------------------------------
Plot = struct(coords[]:Coord, markerStyle?:symbol, markerSize?:number, lineStyle?:symbol)

//-----------------------------------------------------------------------------
// Graph
//-----------------------------------------------------------------------------
Graph = class {
	public {
		xMin, xMax
		yMin, yMax
		cntSamples,
		markerStyleDefault,
		markerSizeDefault,
		lineStyleDefault,
		margine,
	}
	__init__() = {
		this.fontName = if (sys.platform == `mswin) {
			'Sans'
		} elsif (sys.platform == `linux) {
			'Sans'
		} elsif (sys.platform == `darwin) {
			'Hiragino Kaku Gothic Pro'
		} else {
			'Sans'
		}
		this.funcs = []
		this.plots = []
		[this.xMin, this.xMax] = [-10, 10]
		[this.yMin, this.yMax] = [-10, 10]
		this.cntSamples = 100
		this.markerStyleDefault = `square
		this.markerSizeDefault = 10
		this.lineStyleDefault = `none
		this.margine = 10
		this.dict@drawMarker = %{
			`pixel			=> this.drawMarker_pixel
			`square			=> this.drawMarker_square
			`point			=> this.drawMarker_point
			`circle			=> this.drawMarker_circle
			`triangle_down	=> this.drawMarker_triangle_down
			`triangle_up	=> this.drawMarker_triangle_up
			`triangle_left	=> this.drawMarker_triangle_left
			`triangle_right	=> this.drawMarker_triangle_right
			`tri_down		=> this.drawMarker_tri_down
			`tri_up			=> this.drawMarker_tri_up
			`tri_left		=> this.drawMarker_tri_left
			`tri_right		=> this.drawMarker_tri_right
			`x				=> this.drawMarker_x
		}
	}
	coordX(x:number):map = {
		this.xAxis + (x - this.xMin) * this.wdAxis / (this.xMax - this.xMin)
	}
	coordY(y:number):map = {
		this.yAxis + this.htAxis - (y - this.yMin) * this.htAxis / (this.yMax - this.yMin)
	}
	coord(x:number, y:number) = [coordX(x), coordY(y)]
	calcStep(tMin:number, tMax:number) = {
		tRange = tMax - tMin
		nScales = [1, 2, 5, 10, 20, 50, 100, 200, 500, 1000]
		tStepBase = 10 ** (int(math.log10(tRange)) - 2)
		tSteps = tStepBase * nScales
		(tSteps.filter(tRange / tSteps >= 10):list).last()
	}
	drawHline(cr:cairo.context, x:number, y:number, width:number):map:void = {
		cr.move_to(x, y).rel_line_to(width, 0)
	}
	drawVline(cr:cairo.context, x:number, y:number, height:number):map:void = {
		cr.move_to(x, y).rel_line_to(0, height)
	}
	drawMarker(cr:cairo.context, x:number, y:number, markerStyle?:symbol, markerSize?:number):map:void = {
		markerStyle = markerStyle || this.markerStyleDefault
		markerSize = markerSize || this.markerSizeDefault
		this.dict@drawMarker[markerStyle](cr, x, y, markerSize)
	}
	drawMarker_pixel(cr:cairo.context, x:number, y:number, markerSize:number):map:void = {
		cr.save {
			cr.rectangle(x - markerSize / 2, y - markerSize / 2, markerSize, markerSize)
			cr.fill()
		}
	}
	drawMarker_square(cr:cairo.context, x:number, y:number, markerSize:number):map:void = {
		cr.save {
			cr.rectangle(x - markerSize / 2, y - markerSize / 2, markerSize, markerSize)
			cr.set_source_rgb(1, 1, 1)
			cr.fill_preserve()
			cr.set_source_rgb(0, 0, 0)
			cr.set_line_width(1)
			cr.stroke()
		}
	}
	drawMarker_point(cr:cairo.context, x:number, y:number, markerSize:number):map:void = {
		cr.save {
			cr.arc(x, y, markerSize / 2, 0, math.pi * 2)
			cr.fill()
		}
	}
	drawMarker_circle(cr:cairo.context, x:number, y:number, markerSize:number):map:void = {
		cr.save {
			cr.arc(x, y, markerSize / 2, 0, math.pi * 2)
			cr.set_source_rgb(1, 1, 1)
			cr.fill_preserve()
			cr.set_source_rgb(0, 0, 0)
			cr.set_line_width(1)
			cr.stroke()
		}
	}
	makePath_triangle_down(cr:cairo.context, x:number, y:number, markerSize:number):map:void = {
		markerSizeHalf = markerSize / 2
		markerSizeTan = markerSize * 0.577	// 1 / sqrt(3)
		cr.move_to(x, y + markerSizeHalf)
		cr.line_to(x - markerSizeTan, y - markerSizeHalf)
		cr.line_to(x + markerSizeTan, y - markerSizeHalf)
		cr.line_to(x, y + markerSizeHalf)
	}
	makePath_triangle_up(cr:cairo.context, x:number, y:number, markerSize:number):map:void = {
		markerSizeHalf = markerSize / 2
		markerSizeTan = markerSize * 0.577	// 1 / sqrt(3)
		cr.move_to(x, y - markerSizeHalf)
		cr.line_to(x - markerSizeTan, y + markerSizeHalf)
		cr.line_to(x + markerSizeTan, y + markerSizeHalf)
		cr.line_to(x, y - markerSizeHalf)
	}
	makePath_triangle_left(cr:cairo.context, x:number, y:number, markerSize:number):map:void = {
		markerSizeHalf = markerSize / 2
		markerSizeTan = markerSize * 0.577	// 1 / sqrt(3)
		cr.move_to(x - markerSizeHalf, y)
		cr.line_to(x + markerSizeHalf, y - markerSizeTan)
		cr.line_to(x + markerSizeHalf, y + markerSizeTan)
		cr.line_to(x - markerSizeHalf, y)
	}
	makePath_triangle_right(cr:cairo.context, x:number, y:number, markerSize:number):map:void = {
		markerSizeHalf = markerSize / 2
		markerSizeTan = markerSize * 0.577	// 1 / sqrt(3)
		cr.move_to(x + markerSizeHalf, y)
		cr.line_to(x - markerSizeHalf, y - markerSizeTan)
		cr.line_to(x - markerSizeHalf, y + markerSizeTan)
		cr.line_to(x + markerSizeHalf, y)
	}
	drawMarker_triangle_down(cr:cairo.context, x:number, y:number, markerSize:number):map:void = {
		cr.save {
			makePath_triangle_down(cr, x, y, markerSize)
			cr.fill()
		}
	}
	drawMarker_triangle_up(cr:cairo.context, x:number, y:number, markerSize:number):map:void = {
		cr.save {
			makePath_triangle_up(cr, x, y, markerSize)
			cr.fill()
		}
	}
	drawMarker_triangle_left(cr:cairo.context, x:number, y:number, markerSize:number):map:void = {
		cr.save {
			makePath_triangle_left(cr, x, y, markerSize)
			cr.fill()
		}
	}
	drawMarker_triangle_right(cr:cairo.context, x:number, y:number, markerSize:number):map:void = {
		cr.save {
			makePath_triangle_right(cr, x, y, markerSize)
			cr.fill()
		}
	}
	drawMarker_tri_down(cr:cairo.context, x:number, y:number, markerSize:number):map:void = {
		cr.save {
			makePath_triangle_down(cr, x, y, markerSize)
			cr.set_source_rgb(1, 1, 1)
			cr.fill_preserve()
			cr.set_source_rgb(0, 0, 0)
			cr.set_line_width(1)
			cr.stroke()
		}
	}
	drawMarker_tri_up(cr:cairo.context, x:number, y:number, markerSize:number):map:void = {
		cr.save {
			makePath_triangle_up(cr, x, y, markerSize)
			cr.set_source_rgb(1, 1, 1)
			cr.fill_preserve()
			cr.set_source_rgb(0, 0, 0)
			cr.set_line_width(1)
			cr.stroke()
		}
	}
	drawMarker_tri_left(cr:cairo.context, x:number, y:number, markerSize:number):map:void = {
		cr.save {
			makePath_triangle_left(cr, x, y, markerSize)
			cr.set_source_rgb(1, 1, 1)
			cr.fill_preserve()
			cr.set_source_rgb(0, 0, 0)
			cr.set_line_width(1)
			cr.stroke()
		}
	}
	drawMarker_tri_right(cr:cairo.context, x:number, y:number, markerSize:number):map:void = {
		cr.save {
			makePath_triangle_right(cr, x, y, markerSize)
			cr.set_source_rgb(1, 1, 1)
			cr.fill_preserve()
			cr.set_source_rgb(0, 0, 0)
			cr.set_line_width(1)
			cr.stroke()
		}
	}
	drawMarker_x(cr:cairo.context, x:number, y:number, markerSize:number):map:void = {
		markerSizeHalf = markerSize / 2
		cr.save {
			cr.move_to(x - markerSizeHalf, y - markerSizeHalf)
			cr.line_to(x + markerSizeHalf, y + markerSizeHalf)
			cr.move_to(x + markerSizeHalf, y - markerSizeHalf)
			cr.line_to(x - markerSizeHalf, y + markerSizeHalf)
			cr.set_source_rgb(0, 0, 0)
			cr.set_line_width(1)
			cr.stroke()
		}
	}
	drawTextOnN(cr:cairo.context, x:number, y:number, text:string):map:void = {
		extText = cr.text_extents(text)
		cr.move_to(x - extText.x_advance / 2, y + extText.height)
		cr.show_text(text)
	}
	drawTextOnE(cr:cairo.context, x:number, y:number, text:string):map:void = {
		extText = cr.text_extents(text)
		cr.move_to(x - extText.x_advance, y + extText.height / 2)
		cr.show_text(text)
	}
	draw(cr:cairo.context, x?:number, y?:number, width?:number, height?:number) = {
		x = x || this.margine
		y = y || this.margine
		width = width || (cr.width - x - this.margine)
		height = height || (cr.height - y - this.margine)
		[wdGraph, htGraph] = [width, height]
		[xGraph, yGraph] = [x, y]
		[this.wdAxis, this.htAxis] = [wdGraph, htGraph]
		[this.xAxis, this.yAxis] = [xGraph, yGraph]
		this.drawAxis(cr)
		cr.save {
			this.clipAxis(cr)
			this.drawFuncs(cr)
			this.drawPlots(cr)
		}
	}
	clipAxis(cr:cairo.context) = {
		cr.rectangle(this.xAxis, this.yAxis, this.wdAxis, this.htAxis)
		cr.clip()
	}
	drawAxis(cr:cairo.context) = {
		tStep = this.calcStep(this.xMin, this.xMax)
		xCoords = (int(this.xMin / tStep) + (0 .. int((this.xMax - this.xMin) / tStep))) * tStep
		tStep = this.calcStep(this.yMin, this.yMax)
		yCoords = (int(this.yMin / tStep) + (0 .. int((this.yMax - this.yMin) / tStep))) * tStep
		cr.set_line_width(1)
		cr.save {
			cr.set_antialias(cairo.ANTIALIAS_NONE)
			cr.rectangle(this.xAxis, this.yAxis, this.wdAxis, this.htAxis)
			cr.stroke()
			this.clipAxis(cr)
			cr.set_antialias(cairo.ANTIALIAS_NONE)
			this.drawVline(cr, this.coordX(0), this.yAxis, this.htAxis)
			this.drawHline(cr, this.xAxis, this.coordY(0), this.wdAxis)
			cr.set_dash([], 0)
			cr.stroke()
			this.drawVline(cr, this.coordX(xCoords), this.yAxis, this.htAxis)
			this.drawHline(cr, this.xAxis, this.coordY(yCoords), this.wdAxis)
			cr.set_dash([2, 2], 0)
			cr.stroke()
		}
		cr.save {
			cr.select_font_face(this.fontName, cairo.FONT_SLANT_NORMAL, cairo.FONT_WEIGHT_NORMAL)
			cr.set_font_size(15)
			this.drawTextOnN(cr, this.coordX(xCoords), this.coordY(0) + 4, format('%.1f', xCoords))
			this.drawTextOnE(cr, this.coordX(0) - 4, this.coordY(yCoords), format('%.1f', yCoords))
		}
	}
	drawFuncs(cr:cairo.context) = {
		cr.save {
			x = interval(this.xMin, this.xMax, this.cntSamples)
			y = interval(this.yMin, this.yMax, this.cntSamples)
			cr.set_antialias(cairo.ANTIALIAS_DEFAULT)
			for (func in this.funcs) {
				syms = [func.decls:*symbol]
				if (syms.len() != 1) {
					raise(error.ValueError, 'wrong number of arguments')
				}
				sym = syms.first()
				if (sym == `y) {
					cr.line_to(this.coordX(func(y)), this.coordY(y))
				} else {
					cr.line_to(this.coordX(x), this.coordY(func(x)))
				}
				cr.stroke()
			}
		}
	}
	drawPlots(cr:cairo.context) = {
		cr.save {
			this.plots.each {|plot, i|
				lineStyle = plot.lineStyle || this.lineStyleDefault
				if (lineStyle != `none) {
					cr.line_to(this.coordX(plot.coords:*x), this.coordY(plot.coords:*y))
					cr.stroke()
				}
				plot.coords.each {|coord|
					[x, y] = [this.coordX(coord.x), this.coordY(coord.y)]
					this.drawMarker(cr, x, y, plot.markerStyle, plot.markerSize)
				}
			}
		}
	}
	plot(coords[]:Coord, markerStyle?:symbol, markerSize?:number, lineStyle?:symbol) = {
		this.plots.add(Plot(coords, markerStyle, markerSize, lineStyle))
	}
	func(func:function) = {
		this.funcs.add(func)
	}
}


main() = {
	MakeRandCoords(xCenter:number, yCenter:number, xRange:number, yRange:number) = {
		Coord(xCenter + rands(nil, 10) * xRange - xRange / 2, yCenter + rands(nil, 10) * yRange - yRange / 2)
	}
	image(`rgba, 600, 600, `white) {|img|
		Graph() {|g|
			//g.func(function(x):map {math.sin(x)})
			//g.func(function(x):map {math.cos(x)})
			g.func(math.sin)
			g.func(math.cos)
			g.plot(MakeRandCoords(5, 5, 5, 5), `circle, 10, `solid)
			g.plot(MakeRandCoords(5, -5, 5, 5), `point, 10)
			symbols = [
				`pixel, `square, `point, `circle
				`triangle_down, `triangle_up, `triangle_left, `triangle_right
				`tri_down, `tri_up, `tri_left, `tri_right
				`x
			]
			for (x in interval(g.xMin, g.xMax, 30):open, symbol in symbols) {
				g.plot([Coord(x, 4)], symbol)
			}
			g.draw(img)
		}
		img.show(680, 680)
	}
}

if (__name__ == '__main__') {
	import(wx.show)
	main()
}
