import(sdl)

App = class {
	MainLoop(width:number, height:number, flags:number) = {
		sdl.Init(sdl.INIT_VIDEO)
		self.OnInit()
		bpp = sdl.GetVideoInfo().vfmt.BitsPerPixel
		screen = sdl.SetVideoMode(width, height, bpp, flags)
		openglFlag = ((flags | sdl.OPENGL) != 0)
		if (openglFlag) {
			sdl.GL_SetAttribute * [
				[sdl.GL_RED_SIZE, 5], [sdl.GL_GREEN_SIZE, 5], [sdl.GL_BLUE_SIZE, 5],
				[sdl.GL_DEPTH_SIZE, 32], [sdl.GL_DOUBLEBUFFER, 0]
			]
		}
		self.OnResize(width, height)
		repeat {
			event = sdl.PollEvent()
			if (!event) {
				// nothing to do
			} elsif (event.type == sdl.VIDEOEXPOSE) {
				self.OnDraw()
				openglFlag && self.SwapBuffers()
			} elsif (event.type == sdl.VIDEORESIZE) {
				self.OnResize(event.w, event.h)
				self.OnDraw()
				openglFlag && self.SwapBuffers()
			} elsif (event.type == sdl.QUIT) {
				break
			} elsif (event.type == sdl.KEYDOWN) {
				if (event.sym == sdl.K_ESCAPE) {
					break
				}
				self.OnKeyDown(event)
			} elsif (event.type == sdl.KEYUP) {
				self.OnKeyUp(event)
			} elsif (event.type == sdl.MOUSEMOTION) {
				self.OnMouseMotion(event)
			} elsif (event.type == sdl.MOUSEBUTTONDOWN) {
				self.OnMouseButtonDown(event)
			} elsif (event.type == sdl.MOUSEBUTTONUP) {
				self.OnMouseButtonUp(event)
			}
		}
	}
	SwapBuffers() = sdl.GL_SwapBuffers()
	SetCaption(caption:string) = sdl.WM_SetCaption(caption, '')
	OnInit() = {}
	OnResize(width:number, height:number) = {}
	OnDraw() = {}
	OnKeyDown(event:sdl.Event) = {}
	OnKeyUp(event:sdl.Event) = {}
	OnMouseMotion(event:sdl.Event) = {}
	OnMouseButtonDown(event:sdl.Event) = {}
	OnMouseButtonUp(event:sdl.Event) = {}
}

if (__name__ == '__main__') {
	import(opengl) {*}
	AppEx = class(App) {
		OnInit() = {
			self.SetCaption('selutil test')
		}
		OnResize(width:number, height:number) = {}
		OnDraw() = {
			glClearColor(0, 0, 1, 1)
			glClear(GL_COLOR_BUFFER_BIT)
			glFlush()
		}
		OnKeyDown(event:sdl.Event) = {
			println(event)
		}
		OnKeyUp(event:sdl.Event) = {
			println(event)
		}
		OnMouseMotion(event:sdl.Event) = {
			println(event)
		}
		OnMouseButtonDown(event:sdl.Event) = {
			println(event)
		}
		OnMouseButtonUp(event:sdl.Event) = {
			println(event)
		}
	}
	app = AppEx()
	app.MainLoop(400, 300, sdl.OPENGL | sdl.RESIZABLE)
	//sdl.Quit()
}
