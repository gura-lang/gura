#!/usr/bin/env gura
//-----------------------------------------------------------------------------
// argopt.gura
// This module provides a parser of argument options.
// example)
//   import(argopt)
// 	 argopt.Parser {|p|
//       p.addParam('text', 't')
//       p.addFlag('bold', 'b')
//       [cfg, argv] = p.parse(sys.argv)
//   }
//-----------------------------------------------------------------------------
Entry = struct(type:symbol, longName:string, shortName:string:nil,
							help:string:nil, helpValue:string:nil, defValue) {
	makeHelpIndex(longNameFlag:boolean, shortNameFlag:boolean) = {
		str = ''
		if (!shortNameFlag) {
			strSep = ''
		} elsif (this.shortName) {
			str += '-' + this.shortName
			strSep = ', '
		} else {
			str += '  '
			strSep = '  '
		}
		if (longNameFlag && this.longName) {
			str += strSep
			str += '--'
			str += this.longName
		}
		if (this.type == `flag) {
			// nothing to do
		} else {
			if (longNameFlag && this.longName) {
				str += '='
			} elsif (shortNameFlag && this.shortName) {
				str += ' '
			}
			str += this.helpValue || 'X'
		}
		str
	}
}

Parser = class {
	__init__() = {
		this.entries = []
	} %% {`en, R'''
	Create an `argopt.Parser` instance.
	'''}
	parse(argv[]:string) = {
		cfg = %{}
		cfg[this.entries:*longName] = this.entries:*defValue
		argvRest = []
		i = argv.offset(1)
		while (arg = i.next()) {
			[entry, value] = [nil, nil]
			if (arg.startswith('--')) {
				if (arg.len() == 2) {
					argvRest.add(arg)
					continue
				}
				fields = arg.mid(2).split('='):list
				if (fields.len() == 1) {
					longName = fields[0]
				} else {
					[longName, value] = fields
				}
				entry = this.entries.find(this.entries:*longName == longName)
				!entry && raise(error.ArgumentError, 'unknown option --' + longName)
			} elsif (arg.startswith('-')) {
				if (arg.len() == 1) {
					argvRest.add(arg)
					continue
				}
				shortName = arg.mid(1, 1)
				if (arg.len() > 2) {
					raise(error.ArgumentError, 'unknown option -' + arg.mid(1))
				}
				entry = this.entries.find(this.entries:*shortName == shortName)
				!entry && raise(error.ArgumentError, 'unknown option -' + shortName)
				if (entry.type != `flag && !value) {
					value = i.next()
				}
			} else {
				argvRest.add(arg)
				continue
			}
			if (entry.type == `flag) {
				if (!value) {
					value = true
				} else {
					str = value.lower()
					if (str in ['t', 'true', 'on']) {
						value = true
					} elsif (str in ['f', 'false', 'off', 'nil']) {
						value = false
					} else {
						raise(error.ArgumentError, 'invalid value for flag: ' + value)
					}
				}
				cfg[entry.longName] = value
			} elsif (value) {
				cfg[entry.longName] = value
			} else {
				raise(error.ArgumentError, 'value is necessary')
			}
		}
		[cfg, argvRest]
	} %% {`en, R'''
	Parses an argument list which is usually the value of `sys.argv` given by `sys` module.

	It returns the result in a format `[cfg, argv]` where `cfg` is a `dict` instance
	containing parameter values and `argv` a list of arguments that have not been
	parsed as options.
	'''}
	addParam(longName:string, shortName?:string, help?:string, helpValue?:string, defValue?:string) = {
		this.entries.add(Entry(`param, longName, shortName, help, helpValue, defValue))
	} %% {`en, R'''
	Adds an option that comes with a value like `--foo=value`
	where `foo` is a long name for the option.

	The argument `longName` specifies a long option name
	that follows after two hyphens in the command line.
	This name is also used as a key when you look for a value in the dictionary `cfg` returned
	by `argopt.Parser#parse()`.

	The argument `shortName` specifies a short option name that usually consists of a single character.
	If it exists, you can specify the option by a character followed by one hyphen like `-f value`
	where `f` is the short name.
	
	The argument `help` and `helpValue` are used in a option parameter help
	created by `argopt.Parse#formatHelp()`.
	The string for `help` specifies a help text for the option while `helpValue` is a string
	printed after the equal character in the option presentation.
	If the argument `helpValue` is not specified, a string `X` is printed instead.

	The argument `defValue` specifies a default value that would be used
	when the option is not specified in the command line.
	'''}
	addFlag(longName:string, shortName?:string, help?:string) = {
		this.entries.add(Entry(`flag, longName, shortName, help, nil, false))
	} %% {`en, R'''
	Adds an option that represents a boolean state.
	It comes like `--foo` where `foo` is a long name for the option.

	The argument `longName` specifies a long option name
	that follows after two hyphens in the command line.
	This name is also used as a key when you look for a value in the dictionary `cfg` returned
	by `argopt.Parser#parse()`.

	The argument `shortName` specifies a short option name that usually consists of a single character.
	If it exists, you can specify the option by a character followed by one hyphen like `-f`
	where `f` is the short name.

	The argument `help` is used in a option parameter help
	created by `argopt.Parse#formatHelp()`.
	The string for `help` specifies a help text for the option.
	'''}
	formatHelp(longNameFlag:boolean => true, shortNameFlag:boolean => true):[linefeed] = {
		helpIndices = this.entries:*makeHelpIndex(longNameFlag, shortNameFlag)
		str = format('%-*s  %s%s', helpIndices:*len().max(), helpIndices,
					this.entries:*help, cond(__arg__.isset(`linefeed), '\n', ''))
	} %% {`en, R'''
	Creates an iterator of strings that contain help text for each option.

	If the argument `longNameFlag` is `true`, the help text would contain long names.

	If the argument `shortNameFlag` is `true`, the help text would contain short names.

	In default, each string doesn't contain a line feed at the end.
	To add a line feed, specify an attribute `:linefeed`.

	Below is an example of showing help:

	    argopt.Parser {|p|
	        p.addParam('text', 't', 'text value', 'txt')
	        p.addFlag('flag1', 'f', 'flag option #1')
	        p.addFlag('flag2', 'g', 'flag option #2')
	        println(p.formatHelp())
	    }

	The result comes as below:

	    -t, --text=txt  text value
	    -f, --flag1     flag option #1
	    -g, --flag2     flag option #2
	'''}
}
