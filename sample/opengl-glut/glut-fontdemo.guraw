#!/usr/bin/env gura
// This Gura program has been ported from the original C code below:
// https://www.opengl.org/archives/resources/code/samples/glut_examples/examples/fontdemo.c

/* Copyright (c) Mark J. Kilgard, 1994. */

/* This program is freely distributable without licensing fees 
   and is provided without guarantee or warrantee expressed or 
   implied. This program is -not- in the public domain. */

import(opengl) {*}
import(glu) {*}
import(glut) {*}

bitmap_output(x:number, y:number, string:string, font:glut.Font):void = {
	glRasterPos2f(x, y);
	glutBitmapCharacter(font, string.each():utf8);
}

stroke_output(x:number, y:number, fmt:string, args*) = {
	buffer = format(fmt, args*)
	glPushMatrix {
		glTranslatef(x, y, 0);
		glScalef(0.005, 0.005, 0.005);
		glutStrokeCharacter(GLUT_STROKE_ROMAN, buffer.each():utf8);
	}
}

display() = {
	glClear(GL_COLOR_BUFFER_BIT);
	bitmap_output(40, 35, "This is written in a GLUT bitmap font.",
				  GLUT_BITMAP_TIMES_ROMAN_24);
	bitmap_output(30, 210, "More bitmap text is a fixed 9 by 15 font.",
				  GLUT_BITMAP_9_BY_15);
	bitmap_output(70, 240, "                Helvetica is yet another bitmap font.",
				  GLUT_BITMAP_HELVETICA_18);
	glMatrixMode(GL_PROJECTION);
	glPushMatrix {
		glLoadIdentity();
		gluPerspective(40.0, 1.0, 0.1, 20.0);
		glMatrixMode(GL_MODELVIEW);
		glPushMatrix {
			gluLookAt(0.0, 0.0, 4.0,		/* eye is at (0,0,30) */
					  0.0, 0.0, 0.0,		/* center is at (0,0,0) */
					  0.0, 1.0, 0.);		/* up is in postivie Y direction */
			glPushMatrix {
				glTranslatef(0, 0, -4);
				glRotatef(50, 0, 1, 0);
				stroke_output(-2.5, 1.1, "  This is written in a");
				stroke_output(-2.5, 0, " GLUT stroke font.");
				stroke_output(-2.5, -1.1, "using 3D perspective.");
			}
			glMatrixMode(GL_MODELVIEW);
		}
		glMatrixMode(GL_PROJECTION);
	}
	glMatrixMode(GL_MODELVIEW);
	glFlush();
}

reshape(w:number, h:number) = {
	glViewport(0, 0, w, h);
	glMatrixMode(GL_PROJECTION);
	glLoadIdentity();
	gluOrtho2D(0, w, 0, h);
	glScalef(1, -1, 1);
	glTranslatef(0, -h, 0);
	glMatrixMode(GL_MODELVIEW);
}

main() = {
	glutInit(sys.argv);
	glutInitDisplayMode(GLUT_SINGLE | GLUT_RGB);
	glutInitWindowSize(465, 250);
	glutCreateWindow("GLUT bitmap & stroke font example");
	glClearColor(1.0, 1.0, 1.0, 1.0);
	glColor3f(0, 0, 0);
	glLineWidth(3.0);
	glutDisplayFunc(display);
	glutReshapeFunc(reshape);
	glutMainLoop();
}

main()
