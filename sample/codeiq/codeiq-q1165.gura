#!/usr/bin/env gura
// Answer to the question: https://codeiq.jp/ace/joboffer_apli/q1165
import(csv)

Record = class {
	__init__(name:string, scoreJapanese:number, scoreMathematics:number,
			 scoreEnglish:number, scoreSociety:number, scoreScience:number) = {
		this.name:public = name
		this.scoreJapanese:public = scoreJapanese
		this.scoreMathematics:public = scoreMathematics
		this.scoreEnglish:public = scoreEnglish
		this.scoreSociety:public = scoreSociety
		this.scoreScience:public = scoreScience
		this.scoreSum:public = scoreJapanese + scoreMathematics + \
						scoreEnglish + scoreSociety + scoreScience
	}
}

doProcess(fin:stream, fout:stream) = {
	fin.readline() // skip first line
	records = Record * csv.read(fin)
	fout.println('生徒氏名,国語,数学,英語,社会,理科,合計')
	fout.printf('%s,%d,%d,%d,%d,%d,%d\n'
		   records:*name
		   records:*scoreJapanese.rank(`descend) + 1
		   records:*scoreMathematics.rank(`descend) + 1
		   records:*scoreEnglish.rank(`descend) + 1
		   records:*scoreSociety.rank(`descend) + 1
		   records:*scoreScience.rank(`descend) + 1
		   records:*scoreSum.rank(`descend) + 1)
}
doProcess(open('codeiq-q1165/sample_input.csv', 'r', 'shift_jis'), sys.stdout)
doProcess(open('codeiq-q1165/class_3c_input.csv', 'r', 'shift_jis'), sys.stdout)
