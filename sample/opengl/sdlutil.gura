import(sdl)

App = class {
	MainLoop(caption:string:nil, width:number, height:number, flags:number) = {
		sdl.Init(sdl.INIT_VIDEO)
		if (caption) {
			this.SetCaption(caption)
		} else {
			this.SetCaption(path.splitext(path.filename(sys.argv[0]))[0])
		}
		bpp = sdl.GetVideoInfo().vfmt.BitsPerPixel
		screen = sdl.SetVideoMode(width, height, bpp, flags)
		openglFlag = ((flags | sdl.OPENGL) != 0)
		if (openglFlag) {
			sdl.GL_SetAttribute * [
				[sdl.GL_RED_SIZE, 5], [sdl.GL_GREEN_SIZE, 5], [sdl.GL_BLUE_SIZE, 5],
				[sdl.GL_DEPTH_SIZE, 32], [sdl.GL_DOUBLEBUFFER, 0]
			]
		}
		this.OnInit()
		this.OnResize(width, height)
		repeat {
			event = sdl.PollEvent()
			if (!event) {
				// nothing to do
			} elsif (event.type == sdl.VIDEOEXPOSE) {
				this.OnDraw()
				openglFlag && this.SwapBuffers()
			} elsif (event.type == sdl.VIDEORESIZE) {
				this.OnResize(event.w, event.h)
				this.OnDraw()
				openglFlag && this.SwapBuffers()
			} elsif (event.type == sdl.QUIT) {
				break
			} elsif (event.type == sdl.KEYDOWN) {
				if (event.sym == sdl.K_ESCAPE) {
					break
				}
				this.OnKeyDown(event)
			} elsif (event.type == sdl.KEYUP) {
				this.OnKeyUp(event)
			} elsif (event.type == sdl.MOUSEMOTION) {
				this.OnMouseMotion(event)
			} elsif (event.type == sdl.MOUSEBUTTONDOWN) {
				this.OnMouseButtonDown(event)
			} elsif (event.type == sdl.MOUSEBUTTONUP) {
				this.OnMouseButtonUp(event)
			}
			this.OnIdle()
		}
	}
	SwapBuffers() = sdl.GL_SwapBuffers()
	SetCaption(caption:string) = sdl.WM_SetCaption(caption, '')
	CheckKeyState(key:number) = sdl.CheckKeyState(key)
	OnInit() = {}
	OnIdle() = {}
	OnResize(width:number, height:number) = {}
	OnDraw() = {}
	OnKeyDown(event:sdl.Event) = {}
	OnKeyUp(event:sdl.Event) = {}
	OnMouseMotion(event:sdl.Event) = {}
	OnMouseButtonDown(event:sdl.Event) = {}
	OnMouseButtonUp(event:sdl.Event) = {}
}

if (__name__ == '__main__') {
	import(opengl) {*}
	AppEx = class(App) {
		OnInit() = {
		}
		OnIdle() = {
		}
		OnResize(width:number, height:number) = {
		}
		OnDraw() = {
			glClearColor(0, 0, 1, 1)
			glClear(GL_COLOR_BUFFER_BIT)
			glFlush()
		}
		OnKeyDown(event:sdl.Event) = {
			println(event)
		}
		OnKeyUp(event:sdl.Event) = {
			println(event)
		}
		OnMouseMotion(event:sdl.Event) = {
			println(event)
		}
		OnMouseButtonDown(event:sdl.Event) = {
			println(event)
		}
		OnMouseButtonUp(event:sdl.Event) = {
			println(event)
		}
	}
	app = AppEx()
	app.MainLoop('sdlutil', 400, 300, sdl.OPENGL | sdl.RESIZABLE)
	//sdl.Quit()
}
