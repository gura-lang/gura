#!/usr/bin/env gura

classref(`image).rgbread(stream:stream) = {
	ImageHeader = struct(imagic:number, type:number, dim:number,
						width:number, height:number, nElems:number)
	buff = stream.read()
	hdr = ImageHeader * buff.unpack('>HHHHHH')
	this.allocbuff(hdr.width, hdr.height)
	p = buff.pointer(512)
	n = hdr.height * hdr.nElems
	rowStart = p.unpack('>*I', n)
	getrow(y:number, iElem:number) = {
		p = buff.pointer(rowStart[y + iElem * hdr.height])
		repeat ():iter {
			[pixel] = p.unpack('B')
			count = pixel & 0x7f
			if (count == 0) {
				break
			} elsif ((pixel & 0x80) == 0) {
				[pixel] = p.unpack('B')
				repeat (count):iter { pixel }
			} else {
				p.unpack('*B', count).each().repeater()
			}
		}
	}
	if (this.format == `rgb) {
		repeat (hdr.height) {|y|
			this.putpixel(0.., y, color(getrow(y, 0), getrow(y, 1), getrow(y, 2)))
		}
	} elsif (this.format == `rgba) {
		repeat (hdr.height) {|y|
			this.putpixel(0.., y, color(getrow(y, 0), getrow(y, 1), getrow(y, 2), 0))
		}
	} else {
		raise(ValueError, 'unknown image format')
	}
	this
} % {`en, 'markdown', R'''Reads an image in RGB format.'''}
