#!/usr/bin/env gura
/////////////////////////////////////////////////////////////////////////////
// Name:        caret.cpp
// Purpose:     wxCaret sample
// Author:      Robert Roebling
// Modified by:
// Created:     04/01/98
// RCS-ID:      $Id: caret.cpp 29599 2004-10-02 12:36:02Z VS $
// Copyright:   (c) wxWindows team
// Licence:     wxWindows licence
/////////////////////////////////////////////////////////////////////////////
// This Gura script has been ported from the original above by Yutaka SAITO.
import(wx)
import(resource)

// ----------------------------------------------------------------------------
// constants
// ----------------------------------------------------------------------------

// IDs for the controls and the menu commands
[
// menu items
Caret_Quit,
Caret_About,
Caret_SetBlinkTime,
Caret_SetFontSize,
Caret_Move,
] = 1..

// controls start here (the numbers are, of course, arbitrary)
Caret_Text = 1000

MyApp = class(wx.App) {
	OnInit() = {
		// create and show the main application window
		frame = MyFrame('Caret wxWidgets sample',
							wx.Point(50, 50), wx.Size(450, 340))
		frame.Show(true)
		// success: wxApp::OnRun() will be called which will enter the main message
		// loop and the application will run. If we returned false here, the
		// application would exit immediately.
		true
	}
}

MyFrame = class(wx.Frame) {
	__init__(title:string, pos:wx.Point, size:wx.Size) = {|nil, wx.ID_ANY, title, pos, size|
		this.EventTable()
		// set the frame icon
		this.SetIcon(wx.IconFromXPMData(resource.mondrian_xpm))
		// create a menu bar
		menuFile = wx.Menu()
		menuFile.Append(Caret_SetBlinkTime, '&Blink time...\tCtrl-B')
		menuFile.Append(Caret_SetFontSize, '&Font size...\tCtrl-S')
		menuFile.Append(Caret_Move, '&Move caret\tCtrl-C')
		menuFile.AppendSeparator()
		menuFile.Append(Caret_About, '&About...\tCtrl-A', 'Show about dialog')
		menuFile.AppendSeparator()
		menuFile.Append(Caret_Quit, 'E&xit\tAlt-X', 'Quit this program')
		// now append the freshly created menu to the menu bar...
		menuBar = wx.MenuBar()
		menuBar.Append(menuFile, '&File')
		// ... and attach this menu bar to the frame
		this.SetMenuBar(menuBar)
		this.m_canvas = MyCanvas(this)
		// create a status bar just for fun (by default with 1 pane only)
		this.CreateStatusBar(2)
		this.SetStatusText('Welcome to wxWidgets!')
	}
	EventTable() = {
		this.Bind(wx.EVT_MENU, Caret_Quit) {|event| this.OnQuit(event) }
		this.Bind(wx.EVT_MENU, Caret_About) {|event| this.OnAbout(event) }
		this.Bind(wx.EVT_MENU, Caret_SetBlinkTime) {|event| this.OnSetBlinkTime(event) }
		this.Bind(wx.EVT_MENU, Caret_SetFontSize) {|event| this.OnSetFontSize(event) }
		this.Bind(wx.EVT_MENU, Caret_Move) {|event| this.OnCaretMove(event) }
	}
	// event handlers (these functions should _not_ be virtual)
	OnQuit(event:wx.CommandEvent) = {
		// true is to force the frame to close
		this.Close(true)
	}
	OnAbout(event:wx.CommandEvent) = {
		wx.MessageBox('The caret wxWidgets sample.\n(c) 1999 Vadim Zeitlin',
						'About Caret', wx.OK | wx.ICON_INFORMATION, this)
	}
	OnCaretMove(event:wx.CommandEvent) = {
		this.m_canvas.MoveCaret(10, 10)
	}
	OnSetBlinkTime(event:wx.CommandEvent) = {
		blinkTime = wx.GetNumberFromUser(
						'The caret blink time is the time between two blinks',
						'Time in milliseconds:',
						'wxCaret sample',
						wx.Caret.GetBlinkTime(), 0, 10000,
						this
						)
		if (blinkTime != -1) {
			wx.Caret.SetBlinkTime(blinkTime)
			this.m_canvas.CreateCaret()
			wx.LogStatusEx(this, 'Blink time set to %ld milliseconds.', blinkTime)
		}
	}
	OnSetFontSize(event:wx.CommandEvent) = {
		fontSize = wx.GetNumberFromUser(
						'The font size also determines the caret size so\nthis demonstrates resizing the caret.',
						'Font size (in points):',
						'wxCaret sample',
						12, 1, 100,
						this
						)
		if (fontSize != -1) {
			this.m_canvas.SetFontSize(fontSize)
		}
	}
}

MyCanvas = class(wx.ScrolledWindow) {
	__init__(parent:wx.Frame) = {|parent, wx.ID_ANY,
				wx.DefaultPosition, wx.DefaultSize, wx.SUNKEN_BORDER|
		this.m_frame = parent
		this.EventTable()
		// the text
		this.m_text = nil
		this.SetBackgroundColour(wx.WHITE)
		this.SetFontSize(12)
		// position (in text coords) of the caret
		this.m_xCaret = this.m_yCaret = 0
		// the size (in text coords) of the window
		this.m_xChars = this.m_yChars = 0
		// the margin around the text (looks nicer)
		this.m_xMargin = this.m_yMargin = 5
		this.CreateCaret()
	}
	EventTable() = {
		this.Bind(wx.EVT_PAINT) {|event| this.OnPaint(event) }
		this.Bind(wx.EVT_SIZE) {|event| this.OnSize(event) }
		this.Bind(wx.EVT_CHAR) {|event| this.OnChar(event) }
	}
	CharAt(x:number, y:number) = { this.m_text[y][x] }
	StoreAt(x:number, y:number, ch:string) = { this.m_text[y][x] = ch }
	// operations
	CreateCaret() = {
		caret = wx.Caret(this, this.m_widthChar, this.m_heightChar)
		this.SetCaret(caret)
		caret.Move(this.m_xMargin, this.m_yMargin)
		caret.Show()
	}
	SetFontSize(fontSize:number) = {
		this.m_font = wx.Font(fontSize, wx.FONTFAMILY_TELETYPE,
						wx.FONTSTYLE_NORMAL, wx.FONTWEIGHT_NORMAL)
		dc = wx.ClientDC(this)
		dc.SetFont(this.m_font)
		this.m_heightChar = dc.GetCharHeight()
		this.m_widthChar = dc.GetCharWidth()
		caret = this.GetCaret()
		if (caret) {
			caret.SetSize(this.m_widthChar, this.m_heightChar)
			this.ChangeSize()
		}
	}
	MoveCaret(x:number, y:number) = {
		this.m_xCaret = x
		this.m_yCaret = y
		this.DoMoveCaret()
	}
	// caret movement
	Home() = { this.m_xCaret = 0 }
	End() = { this.m_xCaret = this.m_xChars - 1 }
	FirstLine() = { this.m_yCaret = 0 }
	LastLine() = { this.m_yCaret = this.m_yChars - 1 }
	PrevChar() = { if (this.m_xCaret == 0) { this.End(); this.PrevLine() }, this.m_xCaret -= 1 }
	NextChar() = { this.m_xCaret += 1, if (this.m_xCaret == this.m_xChars) { this.Home(); this.NextLine() } }
	PrevLine() = { if (this.m_yCaret == 0) { this.LastLine() }, this.m_yCaret -= 1 }
	NextLine() = { this.m_yCaret += 1, if (this.m_yCaret == this.m_yChars) { this.FirstLine() } }
	// event handlers
	// move the caret to m_xCaret, m_yCaret
	DoMoveCaret() = {
		wx.LogStatus('Caret is at (%d, %d)', this.m_xCaret, this.m_yCaret)
		this.GetCaret().Move(this.m_xMargin + this.m_xCaret * this.m_widthChar,
						this.m_yMargin + this.m_yCaret * this.m_heightChar)
	}
	OnSize(event:wx.SizeEvent) = {
		this.ChangeSize()
		event.Skip()
	}
	// update the geometry
	ChangeSize() = {
		size = this.GetClientSize()
		this.m_xChars = (size.x - 2 * this.m_xMargin) / this.m_widthChar
		this.m_yChars = (size.y - 2 * this.m_yMargin) / this.m_heightChar
		if (this.m_xChars == 0) {
			this.m_xChars = 1
		}
		if (this.m_yChars == 0) {
			this.m_yChars = 1
		}
		//free(m_text)
		this.m_text = dim(this.m_yChars, this.m_xChars) {''}
		if (this.m_frame && this.m_frame.GetStatusBar()) {
			msg = format('Panel size is (%d, %d)', this.m_xChars, this.m_yChars)
			this.m_frame.SetStatusText(msg, 1)
		}
	}
	OnPaint(event:wx.PaintEvent) = {
		wx.CaretSuspend(this) {
			dc = wx.PaintDC(this)
			this.PrepareDC(dc)
			dc.Clear()
			dc.SetFont(this.m_font)
			repeat(this.m_yChars) {|y|
				line = ''
				repeat(this.m_xChars) {|x|
					ch = this.CharAt(x, y)
					if (!ch) {
						ch = ' '
					}
					line += ch
				}
				dc.DrawText(line, this.m_xMargin, this.m_yMargin + y * this.m_heightChar)
			}
		}
	}
	OnChar(event:wx.KeyEvent) = {
		keyCode = event.GetKeyCode()
		if (keyCode == wx.WXK_LEFT) {
			this.PrevChar()
		} elsif (keyCode == wx.WXK_RIGHT) {
			this.NextChar()
		} elsif (keyCode == wx.WXK_UP) {
			this.PrevLine()
		} elsif (keyCode == wx.WXK_DOWN) {
			this.NextLine()
		} elsif (keyCode == wx.WXK_HOME) {
			this.Home()
		} elsif (keyCode == wx.WXK_END) {
			this.End()
		} elsif (keyCode == wx.WXK_RETURN) {
			this.Home()
			this.NextLine()
		} else {
			if (!event.AltDown() && wx.Isprint(event.GetKeyCode())) {
				ch = chr(event.GetKeyCode())
				this.StoreAt(this.m_xCaret, this.m_yCaret, ch)
				wx.CaretSuspend(this) {
					dc = wx.ClientDC(this)
					dc.SetFont(this.m_font)
					dc.SetBackgroundMode(wx.SOLID) // overwrite old value
					dc.DrawText(ch, this.m_xMargin + this.m_xCaret * this.m_widthChar,
									this.m_yMargin + this.m_yCaret * this.m_heightChar)
					this.NextChar()
				}
			} else {
				event.Skip()
			}
		}
		this.DoMoveCaret()
	}
}

// Create a new application object: this macro will allow wxWidgets to create
// the application object during program execution (it's better than using a
// static object for many reasons) and also declares the accessor function
// wxGetApp() which will return the reference of the right type (i.e. MyApp and
// not wxApp)
wx.IMPLEMENT_APP(MyApp)
