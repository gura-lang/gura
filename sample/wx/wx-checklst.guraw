#!/usr/bin/env gura
///////////////////////////////////////////////////////////////////////////////
// Name:        checklst.cpp
// Purpose:     wxCheckListBox sample
// Author:      Vadim Zeitlin
// Modified by:
// Created:     13.11.97
// RCS-ID:      $Id: checklst.cpp 43080 2006-11-05 00:45:27Z VZ $
// Copyright:   (c) 1998 Vadim Zeitlin <zeitlin@dptmaths.ens-cachan.fr>
// Licence:     wxWindows license
///////////////////////////////////////////////////////////////////////////////
// This Gura script has been ported from the original above by Yutaka SAITO.
import(wx)
import(resource)

Menu_About = wx.ID_ABOUT,
Menu_Quit = wx.ID_EXIT,

[
Menu_CheckFirst,
Menu_UncheckFirst,
Menu_ToggleFirst,
Menu_Selection,
Menu_Extended,
Menu_Sorting,
Menu_InsertItemsStart,
Menu_InsertItemsMiddle,
Menu_InsertItemsEnd,
Menu_AppendItems,
Menu_RemoveItems,
Menu_GetBestSize,
Menu_MakeItemFirst,
Control_First,
Control_Listbox,
] = wx.ID_HIGHEST..

Btn_Up = wx.ID_UP,
Btn_Down = wx.ID_DOWN

// init our app: create windows
CheckListBoxApp = class(wx.App) {
	OnInit() = {
		pFrame = CheckListBoxFrame(
				nil,
				'wxWidgets Checklistbox Sample',
				)
		this.SetTopWindow(pFrame)
		true
	}
}

// Define a new frame type
CheckListBoxFrame = class(wx.Frame) {
	// ctor & dtor
	__init__(frame:wx.Frame, title:string) = {|frame, wx.ID_ANY, title|
		this.EventHandler()
		// create the status line
		widths = [ -1, 60 ]
		this.CreateStatusBar(2)
		this.SetStatusWidths(widths)
		// Make a menubar
		// --------------
		// file submenu
		menuFile = wx.Menu()
		menuFile.Append(Menu_About, '&About...\tF1')
		menuFile.AppendSeparator()
		menuFile.Append(Menu_Quit, 'E&xit\tAlt-X')
		// listbox submenu
		menuList = wx.Menu()
		menuList.Append(Menu_CheckFirst, 'Check the first item\tCtrl-C')
		menuList.Append(Menu_UncheckFirst, 'Uncheck the first item\tCtrl-U')
		menuList.Append(Menu_ToggleFirst, 'Toggle the first item\tCtrl-T')
		menuList.AppendSeparator()
		menuList.Append(Menu_InsertItemsStart, 'Insert some item at the beginning')
		menuList.Append(Menu_InsertItemsMiddle, 'Insert some item at the middle')
		menuList.Append(Menu_InsertItemsEnd, 'Insert some item at the end')
		menuList.Append(Menu_AppendItems, 'Append some items\tCtrl-A')
		menuList.Append(Menu_RemoveItems, 'Remove some items')
		menuList.AppendSeparator()
		menuList.AppendCheckItem(Menu_Selection, 'Multiple selection\tCtrl-M')
		menuList.AppendCheckItem(Menu_Extended, 'Extended selection')
		menuList.AppendCheckItem(Menu_Sorting, 'Sorting')
		menuList.AppendSeparator()
		menuList.Append(Menu_GetBestSize, 'Get the best size of the checklistbox control')
		menuList.AppendSeparator()
		menuList.Append(Menu_MakeItemFirst, 'Make selected item the first item')
		// put it all together
		menu_bar = wx.MenuBar()
		menu_bar.Append(menuFile, '&File')
		menu_bar.Append(menuList, '&List')
		this.SetMenuBar(menu_bar)
		// make a panel with some controls
		this.m_panel = wx.Panel(this, wx.ID_ANY, wx.DefaultPosition, wx.DefaultSize, wx.TAB_TRAVERSAL)
		this.CreateCheckListbox()
		// create buttons for moving the items around
		button1 = wx.Button(this.m_panel, Btn_Up)
		button2 = wx.Button(this.m_panel, Btn_Down)
		mainsizer = wx.BoxSizer(wx.VERTICAL)
		mainsizer.Add(this.m_pListBox, wx.SizerFlags(1).Expand().Border(wx.ALL, 10))
		bottomsizer = wx.BoxSizer(wx.HORIZONTAL)
		bottomsizer.Add(button1, wx.SizerFlags(0).Border(wx.ALL, 10))
		bottomsizer.Add(button2, wx.SizerFlags(0).Border(wx.ALL, 10))
		mainsizer.Add(bottomsizer, wx.SizerFlags(0).Center())
		// tell frame to make use of sizer (or constraints, if any)
		this.m_panel.SetAutoLayout(true)
		this.m_panel.SetSizer(mainsizer)
		// don't allow frame to get smaller than what the sizers tell ye
		mainsizer.SetSizeHints(this)
		this.Show(true)
	}
	EventHandler() = {
		this.Bind(wx.EVT_MENU, Menu_About) {|event| this.OnAbout(event) }
		this.Bind(wx.EVT_MENU, Menu_Quit) {|event| this.OnQuit(event) }
		this.Bind(wx.EVT_MENU, Menu_CheckFirst) {|event| this.OnCheckFirstItem(event) }
		this.Bind(wx.EVT_MENU, Menu_UncheckFirst) {|event| this.OnUncheckFirstItem(event) }
		this.Bind(wx.EVT_MENU, Menu_ToggleFirst) {|event| this.OnToggleFirstItem(event) }
		this.Bind(wx.EVT_MENU, Menu_Selection) {|event| this.OnToggleSelection(event) }
		this.Bind(wx.EVT_MENU, Menu_Extended) {|event| this.OnToggleExtended(event) }
		this.Bind(wx.EVT_MENU, Menu_Sorting) {|event| this.OnToggleSorting(event) }
		this.Bind(wx.EVT_MENU, Menu_InsertItemsStart) {|event| this.OnInsertItemsStart(event) }
		this.Bind(wx.EVT_MENU, Menu_InsertItemsMiddle) {|event| this.OnInsertItemsMiddle(event) }
		this.Bind(wx.EVT_MENU, Menu_InsertItemsEnd) {|event| this.OnInsertItemsEnd(event) }
		this.Bind(wx.EVT_MENU, Menu_AppendItems) {|event| this.OnAppendItems(event) }
		this.Bind(wx.EVT_MENU, Menu_RemoveItems) {|event| this.OnRemoveItems(event) }
		this.Bind(wx.EVT_MENU, Menu_GetBestSize) {|event| this.OnGetBestSize(event) }
		this.Bind(wx.EVT_MENU, Menu_MakeItemFirst) {|event| this.OnMakeItemFirst(event) }
		this.Bind(wx.EVT_LISTBOX, Control_Listbox) {|event| this.OnListboxSelect(event) }
		this.Bind(wx.EVT_CHECKLISTBOX, Control_Listbox) {|event| this.OnCheckboxToggle(event) }
		this.Bind(wx.EVT_LISTBOX_DCLICK, Control_Listbox) {|event| this.OnListboxDblClick(event) }
		this.Bind(wx.EVT_BUTTON, Btn_Up) {|event| this.OnButtonUp(event) }
		this.Bind(wx.EVT_BUTTON, Btn_Down) {|event| this.OnButtonDown(event) }
	}
	CreateCheckListbox(flags:number => 0) = {
	    // check list box
		aszChoices = [
			'Zeroth',
			'First', 'Second', 'Third',
			'Fourth', 'Fifth', 'Sixth',
			'Seventh', 'Eighth', 'Nineth'
		]
		this.m_pListBox = wx.CheckListBox(
			this.m_panel,			// parent
			Control_Listbox,		// control id
			wx.Point(10, 10),		// listbox poistion
			wx.Size(400, 100),		// listbox size
			aszChoices,				// array of strings
			flags
		)
		// set grey background for every second entry
		this.AdjustColour(range(0, aszChoices.len(), 2))
		this.m_pListBox.Check(2)
		this.m_pListBox.Select(3)
	}
	// notifications
	OnQuit(event:wx.CommandEvent) = {
		this.Close(true)
	}
	OnAbout(event:wx.CommandEvent) = {
		wxMessageBox('Demo of wxCheckListBox control\n(c) Vadim Zeitlin 1998-2002',
						'About wxCheckListBox',
						wx.ICON_INFORMATION, this)
	}
	OnCheckFirstItem(event:wx.CommandEvent) = {
		if (!this.m_pListBox.IsEmpty()) {
			this.m_pListBox.Check(0)
		}
	}
	OnUncheckFirstItem(event:wx.CommandEvent) = {
		if (!this.m_pListBox.IsEmpty()) {
			this.m_pListBox.Check(0, false)
		}
	}
	OnToggleFirstItem(event:wx.CommandEvent) = {
		if (!this.m_pListBox.IsEmpty()) {
			this.m_pListBox.Check(0, !this.m_pListBox.IsChecked(0))
		}
	}
	s_nItem1 = 0
	OnInsertItemsStart(event:wx.CommandEvent) = {
		items = [
			format('New item %lu', (s_nItem1 += 1))
			format('New item %lu', (s_nItem1 += 1))
			format('New item %lu', (s_nItem1 += 1))
		]
		this.m_pListBox.InsertItems(items, 0)
	}
	s_nItem2 = 0
	OnInsertItemsMiddle(event:wx.CommandEvent) = {
		items = [
			format('New item %lu', (s_nItem2 += 1))
			format('New item %lu', (s_nItem2 += 1))
			format('New item %lu', (s_nItem2 += 1))
		]
		this.m_pListBox.InsertItems(items, if (this.m_pListBox.GetCount() > 0) {1} else {0})
	}
	s_nItem3 = 0
	OnInsertItemsEnd(event:wx.CommandEvent) = {
		items = [
			format('New item %lu', (s_nItem3 += 1))
			format('New item %lu', (s_nItem3 += 1))
			format('New item %lu', (s_nItem3 += 1))
		]
		this.m_pListBox.InsertItems(items, this.m_pListBox.GetCount())
	}
	s_nItem4 = 0
	OnAppendItems(event:wx.CommandEvent) = {
		this.m_pListBox.Append(format('New item %lu', (s_nItem4 += 1)))
		this.m_pListBox.Append(format('New item %lu', (s_nItem4 += 1)))
		this.m_pListBox.Append(format('New item %lu', (s_nItem4 += 1)))
	}
	OnRemoveItems(event:wx.CommandEvent) = {
		if (this.m_pListBox.GetCount() > 0) {
			this.m_pListBox.Delete(0)
		}
		if (this.m_pListBox.GetCount() > 0) {
			this.m_pListBox.Delete(0)
		}
		if (this.m_pListBox.GetCount() > 0) {
			this.m_pListBox.Delete(0)
		}
	}
	OnGetBestSize(event:wx.CommandEvent) = {
		bestSize = this.m_pListBox.GetBestSize()
		wx.MessageBox(format('Best size of the checklistbox is:[%i,%i]',
									bestSize.x, bestSize.y
							)
					)
	}
	OnMakeItemFirst(event:wx.CommandEvent) = {
		if (this.m_pListBox.GetSelection() != -1) {
			this.m_pListBox.SetFirstItem(this.m_pListBox.GetSelection())
		} else {
			wx.MessageBox('Nothing selected!')
		}
	}
	OnToggleSelection(event:wx.CommandEvent) = {
		sizer = this.m_panel.GetSizer()
		sizer.Detach(this.m_pListBox)
		this.m_pListBox.Destroy()
		this.CreateCheckListbox(if (event.IsChecked()) {wx.LB_EXTENDED} else {0})
		sizer.Insert(0, this.m_pListBox, wx.SizerFlags(1).Expand().Border(wx.ALL, 10))
		this.m_panel.Layout()
	}
	OnToggleExtended(event:wx.CommandEvent) = {
		sizer = this.m_panel.GetSizer()
		sizer.Detach(this.m_pListBox)
		this.m_pListBox.Destroy()
		this.CreateCheckListbox(if (event.IsChecked()) {wx.LB_EXTENDED} else {0})
		sizer.Insert(0, this.m_pListBox, wx.SizerFlags(1).Expand().Border(wx.ALL, 10))
		this.m_panel.Layout()
	}
	OnToggleSorting(event:wx.CommandEvent) = {
		sizer = this.m_panel.GetSizer()
		sizer.Detach(this.m_pListBox)
		this.m_pListBox.Destroy()
		this.CreateCheckListbox(if (event.IsChecked()) {wx.LB_SORT} else {0})
		sizer.Insert(0, this.m_pListBox, wx.SizerFlags(1).Expand().Border(wx.ALL, 10))
		this.m_panel.Layout()
	}
	OnListboxSelect(event:wx.CommandEvent) = {
		nSel = event.GetSelection()
		wx.LogStatusEx(this, 'Item %d selected (%schecked)', nSel,
						if (this.m_pListBox.IsChecked(nSel)) {''} else {'not '})
	}
	OnListboxDblClick(event:wx.CommandEvent) = {
	    selection = -1
		if (this.m_pListBox.GetWindowStyle() & wx.LB_EXTENDED != 0) {
			list = this.m_pListBox.GetSelections()
			if (list.len() == 1) {
				selection = list[0]
			}
		} else {
			selection = this.m_pListBox.GetSelection()
		}
		if (selection != -1) {
			strSelection = format('Item %d double clicked', selection)
		} else {
			strSelection = 'List double clicked in multiple selection mode'
		}
		dialog = wx.MessageDialog(this, strSelection, 'wxCheckListBox message', wx.ICON_INFORMATION)
		dialog.ShowModal()
	}
	OnCheckboxToggle(event:wx.CommandEvent) = {
		nItem = event.GetInt()
		wx.LogStatusEx(this, 'item %d was %schecked', nItem,
						if (this.m_pListBox.IsChecked(nItem)) {''} else {'un'})
	}
	OnButtonUp(event:wx.CommandEvent) = {
		this.OnButtonMove(true)
	}
	OnButtonDown(event:wx.CommandEvent) = {
		this.OnButtonMove(false)
	}
	OnButtonMove(up:boolean) = {
		selection = -1
		if (this.m_pListBox.GetWindowStyle() & wx.LB_EXTENDED != 0) {
			list = this.m_pListBox.GetSelections()
			if (list.len() == 1) {
				selection = list[0]
			}
		} else {
			selection = this.m_pListBox.GetSelection()
		}
		if (selection != wx.NOT_FOUND) {
			label = this.m_pListBox.GetString(selection)
			positionNew = if (up) {selection - 1} else {selection + 2}
			if (positionNew < 0 || positionNew > this.m_pListBox.GetCount()) {
				wx.LogStatusEx(this, "Can't move this item %s", if (up) {'up'} else {'down'})
			} else {
				wasChecked = this.m_pListBox.IsChecked(selection)
				positionOld = if (up) {selection + 1} else {selection}
				// insert the item
				this.m_pListBox.InsertItems([label], positionNew)
				// and delete the old one
				this.m_pListBox.Delete(positionOld)
				selectionNew = if (up) {positionNew} else {positionNew - 1}
				this.m_pListBox.Check(selectionNew, wasChecked)
				this.m_pListBox.SetSelection(selectionNew)
				this.m_pListBox.SetFocus()
				this.AdjustColour(selection)
				this.AdjustColour(selectionNew)
				wx.LogStatusEx(this, 'Item moved %s', if (up) {'up'} else {'down'})
			}
		} else {
			wx.LogStatusEx(this, 'Please select single item')
		}
	}
	AdjustColour(index:number):map = {
		// even items have grey backround, odd ones - white
		//c = if (index % 2 != 0) {255} else {200}
		//this.m_pListBox.GetItem(index).SetBackgroundColour(wx.Color(c, c, c))
	}
}


MyFrame = class(wx.Frame) {
	__init__(title:string, pos:wx.Point => wx.DefaultPosition,
			size:wx.Size => wx.DefaultSize) = {|nil, wx.ID_ANY, title, pos, size|
		this.SetIcon(wx.IconFromXPMData(resource.sample_xpm))
	}
}

wx.IMPLEMENT_APP(CheckListBoxApp)
