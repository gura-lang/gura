#!/usr/bin/env gura
///////////////////////////////////////////////////////////////////////////////
// Name:        ownerdrw.cpp
// Purpose:     Owner-draw sample, for Windows
// Author:      Vadim Zeitlin
// Modified by:
// Created:     13.11.97
// RCS-ID:      $Id: ownerdrw.cpp 40587 2006-08-13 01:17:53Z VZ $
// Copyright:   (c) 1998 Vadim Zeitlin <zeitlin@dptmaths.ens-cachan.fr>
// Licence:     wxWindows license
///////////////////////////////////////////////////////////////////////////////
// This Gura script has been ported from the original above by Yutaka SAITO.
import(wx)
import(resource)

Frame = class(wx.Frame) {
	[Menu_Sub1, Menu_Sub2, Menu_Sub3,
		Menu_Test1, Menu_Test2, Menu_Test3, Menu_Bitmap,
		Menu_Bitmap2, Menu_Submenu, Menu_Toggle] = wx.NewIds()
	__init__(parent:wx.Window, title:string, x:number, y:number, w:number, h:number) = {|parent,
							wx.ID_ANY, title, wx.Point(x, y), wx.Size(w, h)|
		self.SetIcon(wx.IconFromXPMData(resource.sample_xpm))
		self.InitMenu()
		panel = wx.Panel(self)
		vbox = wx.BoxSizer(wx.HORIZONTAL)
		panel.SetSizer(vbox)
		choices = [
			'Hello', 'world', 'and', 'goodbye', 'cruel', 'world',
			'-------', 'owner-drawn', 'listbox'
		]
		wx.CheckListBox(panel, wx.ID_ANY,
					wx.DefaultPosition, wx.DefaultSize, choices) {|ctrl|
			vbox.Add(ctrl, wx.SizerFlags(1).Expand())
		}
		choices = [
			'Red', 'Blue', 'Pink', 'Green', 'Yellow', 'Black', 'Violet'
		]
		wx.ListBox(panel, wx.ID_ANY, wx.DefaultPosition, wx.DefaultSize,
											choices, wx.LB_OWNERDRAW) {|ctrl|
			vbox.Add(ctrl, wx.SizerFlags(1).Expand())
			//ctrl.GetItem(range(ctrl.GetCount())):*SetTextColour(wx.Colour(255, 0, 0))
		}
	}
	InitMenu() = {
		fontLarge = wx.Font(18, wx.FONTFAMILY_ROMAN,
						wx.FONTSTYLE_NORMAL, wx.FONTWEIGHT_BOLD, false)
		fontUlined = wx.Font(12, wx.FONTFAMILY_DEFAULT,
						wx.FONTSTYLE_NORMAL, wx.FONTWEIGHT_NORMAL, true)
		fontItalic = wx.Font(12, wx.FONTFAMILY_MODERN,
						wx.FONTSTYLE_ITALIC, wx.FONTWEIGHT_BOLD, false)
		fontBmp = wx.Font(14, wx.FONTFAMILY_DEFAULT,
						wx.FONTSTYLE_NORMAL, wx.FONTWEIGHT_NORMAL, false)
		bmpBell = wx.BitmapFromXPMData(resource.bell_xpm)
		bmpSound = wx.BitmapFromXPMData(resource.sound_xpm)
		bmpNoSound = wx.BitmapFromXPMData(resource.nosound_xpm)
		bmpInfo = wx.BitmapFromXPMData(resource.info_xpm)
		bmpInfo_mono = wx.BitmapFromXPMData(resource.info_mono_xpm)
		menuBar = wx.MenuBar()
		wx.Menu() {|menu|
			wx.MenuItem(menu, self.Menu_Sub1, 'Submenu &first', 'large') {|item|
				item.SetFont(fontLarge)
				menu.AppendItem(item)
			}
			wx.MenuItem(menu, self.Menu_Sub2, 'Submenu &second', 'italic') {|item|
				item.SetFont(fontItalic)
				menu.AppendItem(item)
			}
			wx.MenuItem(menu, self.Menu_Sub3, 'Submenu &third', 'underlined') {|item|
				item.SetFont(fontUlined)
				menu.AppendItem(item)
			}
			menuSub = menu
		}
		wx.Menu() {|menu|
			menuBar.Append(menu, '&File')
			wx.MenuItem(menu, self.Menu_Test1, '&Uncheckable',
								'red item', wx.ITEM_CHECK) {|item|
				item.SetFont(wx.ITALIC_FONT)
				item.SetTextColour(wx.Colour(255, 0, 0))
				menu.AppendItem(item)
			}
			wx.MenuItem(menu, self.Menu_Test2, '&Checkable',
								'checkable item', wx.ITEM_CHECK) {|item|
				item.SetFont(wx.SMALL_FONT)
				menu.AppendItem(item)
			}
			wx.MenuItem(menu, self.Menu_Test3, '&Disabled',
								'disabled item', wx.ITEM_CHECK) {|item|
				item.SetFont(wx.NORMAL_FONT)
				menu.AppendItem(item)
				menu.Enable(self.Menu_Test3, false)
			}
			wx.MenuItem(menu, self.Menu_Bitmap, '&Bell',
								'check/uncheck me!', wx.ITEM_CHECK) {|item|
				item.SetFont(fontBmp)
				item.SetBitmaps(bmpBell)
				menu.AppendItem(item)
			}
			wx.MenuItem(menu, self.Menu_Bitmap2, 'So&und',
								'icon changes!', wx.ITEM_CHECK) {|item|
				item.SetFont(fontBmp)
				item.SetBitmaps(bmpSound, bmpNoSound)
				menu.AppendItem(item)
			}
			wx.MenuItem(menu, self.Menu_Submenu, '&Sub menu',
								'', wx.ITEM_CHECK, menuSub) {|item|
				item.SetFont(wx.SWISS_FONT)
				menu.AppendItem(item)
			}
			menu.AppendSeparator()
			wx.MenuItem(menu, self.Menu_Toggle, '&Disable/Enable\tCtrl+D',
					'enables/disables the About-Item', wx.ITEM_NORMAL) {|item|
				item.SetFont(wx.NORMAL_FONT)
				menu.AppendItem(item)
			}
		}
		self.SetMenuBar(menuBar)
	}
}

App = class(wx.App) {
	OnInit() = {
		frame = Frame(nil, 'wxWidgets Ownerdraw Sample', 50, 50, 450, 340)
		frame.Show()
		self.SetTopWindow(frame)
		true
	}
}

wx.IMPLEMENT_APP(App)
