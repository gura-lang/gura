#!/usr/bin/env gura
import(wx)

Frame = class(wx.Frame) {
	[ID_BTN_1, ID_BTN_2] = wx.NewIds()
	__init__(parent:wx.Window) = {|parent, wx.ID_ANY, path.filename(sys.argv[0])|
		wx.Panel(this) {|panel|
			vbox = wx.BoxSizer(wx.VERTICAL)
			panel.SetSizer(vbox)
			wx.Button(panel, this.ID_BTN_1, 'Push Me') {|ctrl|
				vbox.Add(ctrl, wx.SizerFlags().Expand())
			}
			wx.Button(panel, this.ID_BTN_2, 'Push Me') {|ctrl|
				vbox.Add(ctrl, wx.SizerFlags().Expand())
			}
			wx.Button(panel, wx.ID_ANY, 'Push Me') {|ctrl|
				vbox.Add(ctrl, wx.SizerFlags().Expand())
				ctrl.Connect(wx.EVT_BUTTON, this.OnButton)
			}
			wx.Button(panel, wx.ID_ANY, 'Push Me') {|ctrl|
				vbox.Add(ctrl, wx.SizerFlags().Expand())
				ctrl.Bind(wx.EVT_BUTTON) {|event|
					printf('EVT_BUTTON Bind:direct #%d\n', event.GetId())
				}
			}
		}
		this.Bind(wx.EVT_BUTTON, this.ID_BTN_1, this.ID_BTN_2) {|event|
			printf('EVT_BUTTON Bind:ID #%d\n', event.GetId())
		}
	}
	OnButton(event:wx.Event) = {
		printf('EVT_BUTTON Connect #%d\n', event.GetId())
	}
}

App = class(wx.App) {
	OnInit() = {
		frame = Frame(nil)
		frame.Show()
		this.SetTopWindow(frame)
		true
	}
}

wx.IMPLEMENT_APP(App)
