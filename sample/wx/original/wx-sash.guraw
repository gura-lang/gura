#!/usr/bin/env gura
import(wx)
import(wxutil)

log = nil

Frame = class(wx.Frame) {
	__init__(parent:wx.Window) = {|parent, wx.ID_ANY, path.filename(sys.argv[0])|
		wx.SashLayoutWindow(this, wx.ID_ANY,
					style => wx.NO_BORDER | wx.SW_3D | wx.CLIP_CHILDREN) {|sash|
			sash.SetDefaultSize(wx.Size(1000, 100));
			sash.SetOrientation(wx.LAYOUT_HORIZONTAL);
			sash.SetAlignment(wx.LAYOUT_TOP);
			sash.SetBackgroundColour(wx.Colour(255, 255, 0));
			sash.SetSashVisible(wx.SASH_BOTTOM, true);
			this.sashTop = sash
		}
		wx.SashLayoutWindow(this, wx.ID_ANY,
					style => wx.NO_BORDER | wx.SW_3D | wx.CLIP_CHILDREN) {|sash|
			sash.SetDefaultSize(wx.Size(100, 1000));
			sash.SetOrientation(wx.LAYOUT_VERTICAL);
			sash.SetAlignment(wx.LAYOUT_RIGHT);
			sash.SetBackgroundColour(wx.Colour(0, 0, 255));
			sash.SetSashVisible(wx.SASH_LEFT, true);
			this.sashRight = sash
		}
		wx.SashLayoutWindow(this, wx.ID_ANY,
					style => wx.NO_BORDER | wx.SW_3D | wx.CLIP_CHILDREN) {|sash|
			sash.SetDefaultSize(wx.Size(1000, 100));
			sash.SetOrientation(wx.LAYOUT_HORIZONTAL);
			sash.SetAlignment(wx.LAYOUT_BOTTOM);
			sash.SetBackgroundColour(wx.Colour(255, 0, 0));
			sash.SetSashVisible(wx.SASH_TOP, true);
			this.sashBottom = sash
		}
		wx.SashLayoutWindow(this, wx.ID_ANY,
					style => wx.NO_BORDER | wx.SW_3D | wx.CLIP_CHILDREN) {|sash|
			sash.SetDefaultSize(wx.Size(100, 1000));
			sash.SetOrientation(wx.LAYOUT_VERTICAL);
			sash.SetAlignment(wx.LAYOUT_LEFT);
			sash.SetBackgroundColour(wx.Colour(0, 255, 0));
			sash.SetSashVisible(wx.SASH_RIGHT, true);
			this.sashLeft = sash
		}
		wx.Panel(this, style => wx.BORDER_SUNKEN) {|panel|
			this.remainingSpace = panel
		}
		this.Bind(wx.EVT_SIZE) {|event|
			wx.LayoutAlgorithm().LayoutWindow(this, this.remainingSpace)
		}
		this.Bind(wx.EVT_SASH_DRAGGED) {|event|
			(event.GetDragStatus() == wx.SASH_STATUS_OUT_OF_RANGE) && return
			id = event.GetId()
			if (id == this.sashLeft.GetId()) {
				this.sashLeft.SetDefaultSize(wx.Size(event.GetDragRect().GetWidth(), 1000))
			} elsif (id == this.sashRight.GetId()) {
				this.sashRight.SetDefaultSize(wx.Size(event.GetDragRect().GetWidth(), 1000))
			} elsif (id == this.sashTop.GetId()) {
				this.sashTop.SetDefaultSize(wx.Size(1000, event.GetDragRect().GetHeight()))
			} elsif (id == this.sashBottom.GetId()) {
				this.sashBottom.SetDefaultSize(wx.Size(1000, event.GetDragRect().GetHeight()))
			}
			wx.LayoutAlgorithm().LayoutWindow(this, this.remainingSpace)
			this.remainingSpace.Refresh()
		}
	}
}

App = class(wx.App) {
	OnInit() = {
		frame = Frame(nil)
		frame.Show()
		this.SetTopWindow(frame)
		true
	}
}

app = App()
app.MainLoop()
