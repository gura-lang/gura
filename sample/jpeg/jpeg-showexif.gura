#!/usr/bin/env gura
import(jpeg)
import(bmp)

PrintIFD(ifd:jpeg.ifd, indentLevel:number => 0) = {
	ifd.each {|tag|
		printf('%s[%04x] %-32s %s\n', '  ' * indentLevel, tag.id, tag.name, tag.value:cooked):nomap
		//tag.ifd && PrintIFD(tag.ifd, indentLevel + 1)
	}
}

if (sys.argv.len() < 2) {
	sys.stderr.println('usage: jpeg-showexif.gura file')
	sys.exit(0)
}
jpeg.exif(sys.argv[1]) {|exif|
	if (!exif) {
		println("exif doesn't exist")
		break
	}
	println('endian: ', exif.endian)
	println('= ifd0 =')
	PrintIFD(exif.ifd0)
	println('= ifd0.Exif =')
	PrintIFD(exif.ifd0.Exif)
	println('= ifd0.Exif.Interoperability =')
	try {
		PrintIFD(exif.ifd0.Exif.Interoperability)
	} catch {}
	if (exif.ifd1) {
		println('= ifd1 =')
		PrintIFD(exif.ifd1)
	}
	if (exif.thumbnail) {
		println(exif.thumbnail)
		println(exif.thumbnail_jpeg)
	}
}
