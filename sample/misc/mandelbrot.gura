#!/usr/bin/env gura
import(re)
import(argopt)
import(bmp)
import(png)
import(jpeg)
import(xpm)
import(ppm)

main() = {
	[width, height] = [500, 500]
	[realMin, realMax] = [-2.0, 1.0]
	[imagMin, imagMax] = [-1.5, 1.5]
	argopt.Parser {|p|
		p.addFlag('help',		'h',	'print help')
		p.addParam('canvas',	'c',	'canvas size',						'X,Y')
		p.addParam('real',		'r',	'value range for real number',		'MIN,MAX')
		p.addParam('imag',		'i',	'value range for imaginary number',	'MIN,MAX')
		try {
			[cfg, argv] = p.parse(sys.argv)
		} catch {|e|
			sys.stderr.println(e.text)
			sys.exit(1)
		}
		if (cfg['help']) {
			R'''
			usage: ${path.filename(sys.argv[0])} [options]
			options:
			 ${p.formatHelp():linefeed}
			'''.template().render(sys.stderr)
			sys.exit(0)
		}
		if (value = cfg['canvas']) {
			if (!(m = value.match(r'(\d+),(\d+)'))) {
				sys.stderr.println('invalid value for size option')
				sys.exit(1)
			}
			[width, height] = int([m[1], m[2]])
		}
		if (value = cfg['real']) {
			if (!(m = value.match(r'(-?[0-9.]+),(-?[0-9.]+)'))) {
				sys.stderr.println('invalid value for real option')
				sys.exit(1)
			}
			try {
				[realMin, realMax] = tonumber([m[1], m[2]]):strict:raise
			} catch {
				sys.stderr.println('invalid value for real option')
				sys.exit(1)
			}
		}
		if (value = cfg['imag']) {
			if (!(m = value.match(r'(-?[0-9.]+),(-?[0-9.]+)'))) {
				sys.stderr.println('invalid value for imag option')
				sys.exit(1)
			}
			try {
				[imagMin, imagMax] = tonumber([m[1], m[2]]):strict:raise
			} catch {
				sys.stderr.println('invalid value for imag option')
				sys.exit(1)
			}
		}
	}
	//colors = palette(`basic).each():list
	//colors = palette(`win256).each():list
	colors = color(0, 0, interval(20, 255, 64))
	printf('canvas=%d,%d, real=%g,%g, imag=%g,%g, level=%d\n',
				width, height, realMin, realMax, imagMin, imagMax, colors.len())
	img = image(`rgba, width, height)
	mandelbrot(img, colors, realMin, realMax, imagMin, imagMax) {|progress|
		sys.stderr.printf('Progress: %d%%\r', progress)
	}
	if (argv.len() > 0) {
		fileName = argv[0]
		img.write(fileName)
		sys.stderr.println(fileName, ' was created')
	} else {
		import(show)
		img.show()
	}
}

mandelbrot(img:image, colors[]:color,
		realMin:number, realMax:number, imagMin:number, imagMax:number):void {block?} = {
	nPixels = img.width * img.height
	progressPrev = 0
	cross (x in range(img.width), y in range(img.height)) {|iPixel|
		real = x * (realMax - realMin) / img.width + realMin
		imag = y * (imagMax - imagMin) / img.height + imagMin
		zn = z0 = complex(real, imag)
		c = colors.each {|c|
			zm = zn * zn + z0
			(zm.norm > 4) && break(c)
			zn = zm
			`black
		}
		img.putpixel(x, y, c)
		progress = int((iPixel + 1) / nPixels * 100)
		if (progress != progressPrev) {
			block && block(progress)
			progressPrev = progress
		}
	}
	img
}

main()
