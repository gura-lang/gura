#!/usr/bin/env gura
// Tower of Hanoi

resolve(n:number, from:number, to:number, via:number) = {
	if (n == 1) {
		printf('%d => %d\n', from, to)
		bars[to].Place(bars[from].Remove())
		PrintStage()
	} else {
		resolve(n - 1, from, via, to)
		printf('%d => %d\n', from, to)
		bars[to].Place(bars[from].Remove())
		PrintStage()
		resolve(n - 1, via, to, from)
	}
}

Bar = struct(disks[]:string) {
	GetLine(i:number):map = {
		i += this.disks.len() - nDisks
		if (i < 0) { blank } else { this.disks[i] }
	}
	Remove() = {
		disk = this.disks[0]
		this.disks.erase(0)
		disk
	}
	Place(disk:string) = {
		this.disks.insert(0, disk)
	}
}

PrintStage() = {
	repeat(nDisks) {|i|
		println(bars[0..2]:*GetLine(i).join(' '))
	}
}

if (sys.argv.len() < 2) {
	sys.stderr.println('usage: gura hanoi.gura num_of_disks')
	sys.exit(1)
}
nDisks = int(sys.argv[1])
disks = [(((nDisks - 1)..0) * ' ') + (((1..nDisks) * 2 - 1) * '@') + (((nDisks - 1)..0) * ' ')]
blank = ((nDisks - 1) * ' ') + '|' + ((nDisks - 1) * ' ')
bars = [Bar(disks), Bar([]), Bar([])]
PrintStage()
resolve(nDisks, 0, 2, 1)
