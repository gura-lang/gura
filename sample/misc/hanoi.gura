#!/usr/bin/env gura
// Tower of Hanoi

nDisks = 5

resolve(n:number, from:number, to:number, via:number) = {
	if (n == 1) {
		printf('%d => %d\n', from, to)
		bars[to].Place(bars[from].Remove())
		PrintStage()
	} else {
		resolve(n - 1, from, via, to)
		printf('%d => %d\n', from, to)
		bars[to].Place(bars[from].Remove())
		PrintStage()
		resolve(n - 1, via, to, from)
	}
}

disks = [(((nDisks - 1)..0) * ' ') + (((1..nDisks) * 2 - 1) * '@') + (((nDisks - 1)..0) * ' ')]
blank = ((nDisks - 1) * ' ') + '|' + ((nDisks - 1) * ' ')
Bar = struct(disks[]:string) {
	GetLine(i:number):map = {
		i += this.disks.len() - nDisks
		if (i < 0) { blank } else { this.disks[i] }
	}
	Remove() = {
		disk = this.disks[0]
		this.disks.erase(0)
		disk
	}
	Place(disk:string) = {
		this.disks.insert(0, disk)
	}
}
bars = [Bar(disks), Bar([]), Bar([])]
PrintStage() = {
	repeat(nDisks) {|i|
		println(bars[0..2]:*GetLine(i).join(' '))
	}
}
PrintStage()
resolve(nDisks, 0, 2, 1)
