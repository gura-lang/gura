#!/usr/bin/env gura
//-----------------------------------------------------------------------------
// stamper.gura
//-----------------------------------------------------------------------------
import(re)
import(jpeg)
import(argopt)

MakeUniqPathName(pathName:string, dirNameOut:string:nil):map = {
	try {
		stat = path.stat(pathName)
	} catch { stat = nil }
	(!stat || stat.isdir) && return(nil)
	charsAtoZ = chr(ord('A')..ord('Z'))
	try { exif = jpeg.exif(pathName) } catch { exif = nil }
	if (exif) {
		try {
			exif.DateTime.value.match(r'(\d{4}):(\d{2}):(\d{2})\s+(\d{2}):(\d{2}):(\d{2})') {|m|
				[year, month, day, hour, min, sec] = int(m[1..6])
			}
		} catch { exif = nil }
	}
	if (!exif) {
		mtime = stat.mtime
		[year, month, day, hour, min, sec] = \
			[mtime.year, mtime.month, mtime.day, mtime.hour, mtime.min, mtime.sec]
	}
	[dirName, fileName] = path.split(pathName)
	[baseName, extName] = path.splitext(fileName)
	if (dirNameOut) {
		dirName = dirNameOut
	}
	cross (upper in charsAtoZ, lower in charsAtoZ) {
		fileNameUniq = format('%04d_%02d%02d_%02d%02d%02d%s%s.%s',
			year, month, day, hour, min, sec, upper, lower, extName)
		pathNameUniq = path.join(dirName, fileNameUniq)
		!path.exists(pathNameUniq) && break
		pathNameUniq = nil
	}
	pathNameUniq
}

argopt.Parser {|p|
	p.addParam('outdir',	'o', 'specify output directory')
	p.addFlag('rename',		'r', 'rename existing files')
	p.addFlag('create',		'c', 'create new files with modified names')
	try {
		[cfg, argv] = p.parse(sys.argv)
	} catch {|e|
		println(e.text)
		sys.exit(1)
	}
	if (argv.len() < 1) {
		sys.stderr.println('usage: stamper.gura [options] file ...')
		println('options:')
		println('  ', p.formatHelp())
		sys.exit(1)
	}
}
[dirNameOut, renameFlag, createFlag] = cfg['outdir', 'rename', 'create']
pathNames = path.glob(argv[0..]):list
nPads = pathNames:*len().max()
if (dirNameOut && !path.exists(dirNameOut)) {
	fs.mkdir(dirNameOut):tree
}
pathNames.each {|pathName|
	if (pathNameUniq = MakeUniqPathName(pathName, dirNameOut)) {
		if (!renameFlag && !createFlag) {
			printf('test:   %-*s -> %s\n', nPads, pathName, pathNameUniq)
		} elsif (pathName == pathNameUniq) {
			printf('skip:   %-*s\n', nPads, pathName)
		} elsif (renameFlag) {
			fs.rename(pathName, pathNameUniq)
			printf('rename: %-*s -> %s\n', nPads, pathName, pathNameUniq)
		} else {
			copy(pathName, pathNameUniq):finalize
			printf('create: %-*s -> %s\n', nPads, pathName, pathNameUniq)
		}
	}
}
