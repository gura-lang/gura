#!/usr/bin/env gura
import(argopt)

argopt.Parser {|p|
	p.addFlag('recursive', 'r', 'recursively search sub directories')
	try {
		[cfg, argv] = p.parse(sys.argv)
	} catch {|e|
		println(e.text)
		sys.exit(1)
	}
	if (argv.len() < 1) {
		sys.stderr.println('usage: wc.gura [options] file [file..]')
		println('options:')
		println('  ', p.formatHelp())
		sys.exit(1)
	}
}
if (cfg['recursive']) {
	fileNames = path.walk('', -1, argv[0..]*):file
} else {
	fileNames = path.glob(argv[0..]):list:file
}
lenMax = fileNames:*len().max()
nlinesSum = 0
fileNames.each {|fileName|
	nlines = readlines(fileName).len()
	printf('%-*s %d\n', lenMax, fileName, nlines)
	nlinesSum += nlines
}
printf('%d\n', nlinesSum)
